# genrommake: generate a Makefile to control the final ROM Image construction stage
#
BEGIN {
    i = 0
}

{
    if (index($0, "#") == 0)                              # Only process non-comment lines
        if ((NF >= 5) && (index($1, "ROMImage") == 0))    # only lines with "ROM modules" entries are listed
        {                                                 # The "ROMImage" line is a pseudo entry
            for (j=5; j<=NF; j++)                         # process each ROM module on the line
            {
                Name[i]    = $1
                Type[i]    = $2
                Source[i]  = $3
                Install[i] = $4
                Module[i]  = $j
                if (index(Name[i], "SharedCLibrary") == 1)
                    CLibDir = Install[i]
                i++
            }
    }
}

END {
    printf("# Makefile for ROMImage\n\n")

    printf("CD      = dir\n")
    printf("LD      = link\n")
    printf("MKDIR   = cdir\n")
    printf("MKIMAGE = BigSplit1\n\n")

    printf("SRCDIR  = <src$dir>\n")
    printf("INSTDIR = <install$dir>\n")
    printf("CLIBDIR = <install$dir>.%s\n", CLibDir)
    printf("ABSSYM  = RISC_OSLib:o.AbsSym\n")
    printf("\n")

    printf("MODULES =")

    ncomp = i-1
    for (i=ncomp; i>=0; i--)
        printf(" \\\n %s", Module[i])
    printf("\n\n")

    printf("#\n")
    printf("# Build rules:\n")
    printf("#\n")

    printf("install_rom: ${MODULES} dirs\n")
    printf("        ${MKIMAGE} { < ForBigSplt }\n\n")

    printf("dirs:\n")
    printf("        ${MKDIR} ${CLIBDIR}.linked\n")
    printf("        ${MKDIR} ${CLIBDIR}.map\n\n")

    for (i=0; i<=ncomp; i++)
    {
        printf("%s:\n", Module[i])

        if (index(Type[i], "C") == 1)
        {
            printf("        @${CD} ${SRCDIR}.%s\n", Source[i])
            printf("        @set alias$amu_rom_link amu MACHINE=<Machine> LOCALE=<Locale> ADDRESS=<PositionInRom> rom_link %0\n")
            printf("        amu_rom_link LINKDIR=${CLIBDIR}.linked\n")
            printf("        @unset alias$amu_rom_link\n")
            printf("        @back\n")
            printf("        setolength File$Length ${CLIBDIR}.linked.%s\n", Module[i])
            printf("        @echo %-25s  <PositionInRom>  <File$Length>\n", Module[i])
            printf("        @seteval PositionInRom PositionInRom + ((File$Length + 3) AND NOT 3) + 4\n\n")
        }
        else if (index(Name[i], "Kernel") == 1)
        {
            printf("        @seteval PositionInRom &03800000\n")
            printf("        @setolength File$Length ${INSTDIR}.%s.%s\n", Install[i], Module[i])
            printf("        @echo %-25s  <PositionInRom>  <File$Length>\n", Module[i])
            printf("        @seteval PositionInRom PositionInRom + ((File$Length + 3) AND NOT 3) \n\n")
        }
        else
        {
            printf("        @setolength File$Length ${INSTDIR}.%s.%s\n", Install[i], Module[i])
            printf("        @echo %-25s  <PositionInRom>  <File$Length>\n", Module[i])
            printf("        @seteval PositionInRom PositionInRom + ((File$Length + 3) AND NOT 3) + 4\n\n")
        }
    }
}
