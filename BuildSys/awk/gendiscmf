# genmake:
#
# Notes:
#    Up to 5 fields may be used: 5th is an optional amu MACRO
#
BEGIN {
    i = 1
}

{
    if (index($0, "#") == 0)              # Only process non-comment lines
        if (NF >= 3) {                    # legal entries have three or more fields
            Name[i]   = $1
            Type[i]   = $2
            Source[i] = $3
            if (NF == 3)
                Install[i] = ""
            else
                Install[i] = $4
            if (NF == 5)
                Macro[i] = $5
            else
                Macro[i] = ""
            i++
        }
}

END {
    printf("# System Makefile\n\n")

    printf("CD      = dir\n")
    printf("MKDIR   = cdir\n")
    printf("SYSDIR  = <build$dir>.System\n")
    printf("SRCDIR  = <src$dir>\n")
    printf("INSTDIR = <discinstall$dir>\n")
    printf("RESDIR  = <resource$dir>.Resources\n")
    printf("MAKE    = @amu ${MFLAGS} RESDIR=${RESDIR}\n")
    printf("\n")

    ncomp = i-1

    printf("BUILD =")
    for (i=ncomp; i>0; i--)
        if ((Install[i] != "") && (index(Name[i], "ROMImage") == 0))
            printf(" \\\n %s", Name[i])
    printf("\n\n")

    printf("EXPORT =")
    for (i=ncomp; i>0; i--)
        if (index(Type[i], "EXP") != 0)
            printf(" \\\n %s", Name[i])
    printf("\n\n")

    printf("RESOURCE =")
    for (i=ncomp; i>0; i--)
        if (index(Type[i], "RES") != 0)
            printf(" \\\n %s", Name[i])
    printf("\n\n")

    printf("#\n")
    printf("# Build rules:\n")
    printf("#\n")

    printf("clean: ${BUILD}\n")
    printf("        @echo clean: Complete\n\n")

    printf("export: ${EXPORT} ${BUILD}\n")
    printf("        @echo export: Complete\n\n")

    printf("# disc build\n")
    printf("all: ${BUILD}\n")
    printf("        @echo all: Complete\n\n")

    printf("install: ${BUILD}\n")
    printf("        @echo install: Complete\n\n")

    printf("#\n")
    printf("# Component rules:\n")
    printf("#\n\n")

    for (i=1; i<=ncomp; i++)
    {
        if (index(Name[i], "ROMImage") == 1)
            printf("ROMImage: ${BUILD}\n")
        else
            printf("%s:\n", Name[i])
        printf("        @echo $@ ...\n")

        ndirs = split(Install[i], dirs, ".")

        for (j=1; j<=ndirs; j++)
        {
            printf("        @${MKDIR} ${INSTDIR}")
            for (k=1; k<=j; k++)
                printf(".%s", dirs[k])
            printf("\n")
            printf("        @Access ${INSTDIR}")
            for (k=1; k<=j; k++)
                printf(".%s", dirs[k])
            printf(" /r\n")
        }

        if (index(Name[i], "ROMImage") == 1)
            printf("        @${CD} ${SYSDIR}.%s\n", Source[i])
        else
            printf("        @${CD} ${SRCDIR}.%s\n", Source[i])
        if (Install[i] == "")
            printf("        ${MAKE}\n")
        else
            printf("        ${MAKE} INSTDIR=${INSTDIR}.%s %s\n", Install[i], Macro[i])
        printf("\n")
    }
}
