; > Sources.ROMFonts

; *********************************
; ***    C h a n g e   L i s t  ***
; *********************************

; Date       Description
; ----       -----------
; 19-Feb-90     0.01    File created.
; 26-Feb-90     0.02    Fixed bug: Font$Path was set to ResourceFS:, not Resources:
; 22-Jun-90     0.03    Forgot to include Sidney font!
; 20-Jul-90     0.04    Saved 24k from IntMetrics by omitting bboxes and rearranging char map
;                       Replaced NewHall.Bold by Sidney (Symbol)
; 07-Jan-91     0.05    Saved 96k by deriving Homerton and Corpus Oblique fonts from normal ones
;                       Regenerated all IntMetrics files from the AFM files
; 15-Apr-91     0.06    Introduced Encodings./Base0, Latin1, Latin2, Latin3 and Latin4
;                       Replaced Homerton.Medium, Trinity.Medium and Trinity.Medium.Italic with /Base0 versions
;                       Regenerated all other IntMetrics files so they're version 2 (new kern format)
; 17-May-91     0.07    (WS) Replaced Corpus.Medium, Corpus.Bold, Trinity.Bold.Italic with /Base0 versions
;                       Trinity.Bold kept back, the Aring kills the font manager!
; 21-May-91     0.08    (WS) all 12 major fonts now in /Base0 form.
; 25-Jun-91     0.10    (WS) Selwyn removed - now on the disc.
; (Log information missed)
; 01-Apr-92     0.14    (SAH) Fixed position of tilde accents in the bold fonts
; 08-Feb-93             BCockburn; changed GETs to use Hdr: rather than &.Hdr.
; 19-Jul-93     0.15    (ECN) Fixed slant of German quote characters in Corpus.Medium
;

Module_BaseAddr

        GET     Hdr:ListOpts
        GET     Hdr:Macros
        GET     Hdr:System
        GET     Hdr:ModHand
        GET     Hdr:Services
        GET     Hdr:Proc
        GET     Hdr:FSNumbers
        GET     Hdr:NewErrors
        GET     Hdr:Variables
        GET     Hdr:ResourceFS
        GET     Version

        GET     Hdr:NDRDebug

        GBLL    debug
        GBLL    debugxx
        GBLL    hostvdu

debug   SETL    false
debugxx SETL    true :LAND: debug
hostvdu SETL    true

        LEADR   Module_LoadAddr


; **************** Module code starts here **********************

        ASSERT  (.-Module_BaseAddr) = 0

        DCD     0 ; ROMFonts_Start    - Module_BaseAddr
        DCD     ROMFonts_Init     - Module_BaseAddr
        DCD     ROMFonts_Die      - Module_BaseAddr
        DCD     ROMFonts_Service  - Module_BaseAddr
        DCD     ROMFonts_Title    - Module_BaseAddr
        DCD     ROMFonts_HelpStr  - Module_BaseAddr
        DCD     0
        DCD     0
        DCD     0
        DCD     0
        DCD     0

ROMFonts_Title     DCB     "ROMFonts", 0
ROMFonts_HelpStr   DCB     "ROM Fonts", 9, "$VString ($Date)", 0
                ALIGN

; *****************************************************************************
;
;       ROMFonts_Init - Initialisation entry
;

stk_buffsize    *       256

ROMFonts_Init ENTRY

        ADR     R0, ROMFonts
        SWI     XResourceFS_RegisterFiles   ; ignore errors (starts on Service_ResourceFSStarting)

        SUB     sp, sp, #stk_buffsize

        ADR     R0, FontPath
        MOV     R1, sp
        MOV     R2, #stk_buffsize
        MOV     R3, #0
        MOV     R4, #VarType_Macro      ; don't expand it
        SWI     XOS_ReadVarVal          ; error => variable does not exist
        BVS     %FT12                   ; if error, set up the new one

        CMP     R4, #VarType_Macro      ; check for *SetMacro Font$Path <Font$Prefix>.
        BNE     %FT02

        MOV     R14, #0                 ; put in terminator
        STRB    R14, [R1, R2]

        ADR     R0, FontPrefix2         ; R0 -> "<Font$Prefix>."
01      LDRB    R2, [R0], #1
        LDRB    R3, [R1], #1
        CMP     R2, R3
        BNE     %FT02
        CMP     R2, #0
        BNE     %BT01

12      ADR     R0, SetFontPath         ; R0 -> "SetMacro Font$Path ..."
        SWI     XOS_CLI
        BVS     %FT02

        ADR     R0, FontPrefix          ; if Font$Prefix not defined,
        MOV     R2, #-1                 ; set it to " " to pre-empt !Fonts
        MOV     R3, #0
        MOV     R4, #VarType_Expanded
        SWI     XOS_ReadVarVal          ; returns R2=0 if doesn't exist
        CMP     R2, #0                  ; clears V as well!

        ADREQ   R0, SetFontPrefix
        SWIEQ   XOS_CLI
02
        ADD     sp, sp, #stk_buffsize

        EXIT

FontPath        DCB     "Font$Path", 0
FontPrefix      DCB     "Font$Prefix", 0
SetFontPath     DCB     "SetMacro Font$Path <Font$Prefix>.,Resources:$.Fonts.", 0
FontPrefix2     DCB     "<Font$Prefix>.", 0
SetFontPrefix   DCB     "Set Font$Prefix "" """, 0
                ALIGN

; *****************************************************************************
;
;       ROMFonts_Die - Die entry
;

ROMFonts_Die ENTRY

        ADR     R0, ROMFonts
        SWI     XResourceFS_DeregisterFiles

        EXITS                   ; ignore errors (ResourceFS might be dead)

; *****************************************************************************
;
;       ROMFonts_Service - Main entry point for services
;
; in:   R1 = service reason code
;       R2 = sub reason code
;       R3-R5 parameters
;
; out:  R1 = 0 if we claimed it
;       R2 preserved
;       R3-R5 = ???
;

ROMFonts_Service ROUT

        Debug   xx,"ROMFonts_Service:",R1

        TEQ     R1, #Service_ResourceFSStarting
        MOVNE   PC, LR

   ;;   LDR     R12, [R12]     ;; not actually required in this case

; In    R2 -> address inside ResourceFS module to call
;       R3 = workspace pointer for module
; Out   R2 called with R0 -> files, R3 -> workspace

svc_desktopstarting ENTRY "R0-R3"

        ADR     R0, ROMFonts
        MOV     LR, PC                          ; get return address
        MOV     PC, R2                          ; R2 = address to call

        EXITS

; *****************************************************************************

        MACRO
$lab    MyDesk  $name
$lab    ResourceFile $name, $name
        MEND

ROMFonts
        MyDesk  Fonts.Corpus.Bold.IntMetric0
        MyDesk  Fonts.Corpus.Bold.Oblique.IntMetric0
        MyDesk  Fonts.Corpus.Bold.Oblique.Outlines0
        MyDesk  Fonts.Corpus.Bold.Outlines0

        MyDesk  Fonts.Corpus.Medium.IntMetric0
        MyDesk  Fonts.Corpus.Medium.Oblique.IntMetric0
        MyDesk  Fonts.Corpus.Medium.Oblique.Outlines0
        MyDesk  Fonts.Corpus.Medium.Outlines0

        MyDesk  Fonts.Encodings./Base0
        MyDesk  Fonts.Encodings.Latin1
        MyDesk  Fonts.Encodings.Latin2
        MyDesk  Fonts.Encodings.Latin3
        MyDesk  Fonts.Encodings.Latin4
 
        MyDesk  Fonts.Homerton.Bold.IntMetric0
        MyDesk  Fonts.Homerton.Bold.Oblique.IntMetric0
        MyDesk  Fonts.Homerton.Bold.Oblique.Outlines0
        MyDesk  Fonts.Homerton.Bold.Outlines0

        MyDesk  Fonts.Homerton.Medium.IntMetric0
        MyDesk  Fonts.Homerton.Medium.Oblique.IntMetric0
        MyDesk  Fonts.Homerton.Medium.Oblique.Outlines0
        MyDesk  Fonts.Homerton.Medium.Outlines0

        MyDesk  Fonts.Trinity.Bold.IntMetric0
        MyDesk  Fonts.Trinity.Bold.Italic.IntMetric0
        MyDesk  Fonts.Trinity.Bold.Italic.Outlines0
        MyDesk  Fonts.Trinity.Bold.Outlines0

        MyDesk  Fonts.Trinity.Medium.IntMetric0
        MyDesk  Fonts.Trinity.Medium.Italic.IntMetric0
        MyDesk  Fonts.Trinity.Medium.Italic.Outlines0
        MyDesk  Fonts.Trinity.Medium.Outlines0

        DCD     0

      [ debug
        InsertNDRDebugRoutines
      ]

        END

