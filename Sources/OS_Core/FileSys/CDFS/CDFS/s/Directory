; >Directory

;**************************************************************************
;**************************************************************************

;   This will move to the correct LBA, indicated by a pathname, pointed

; at by R0.


; contains:
;               AddDiscNameInList      - puts disc into drive list
;               CheckForISO9660        - checks disc for ISO or HISIERRA
;               ConvertDotToUnderLine
;               CutSpace               - removes messy spaces from a string
;               Dir
;               FindDiscNameInList     - Looks through drive mounted list
;               FullPathName
;               LoadBlockFromDrive
;               WildCompareStrings     - RISC OS 2 ONLY

;**************************************************************************
Dir
; on entry:
;          R0->pathname
;          R1=0 if looking for a file, 1 if looking for a directory
;             2 if don't care (ie opening a file)

; on exit:
;          R1 -> block of object info, 0 if not found,
;          R2 = 1 if a file, 2 if a directory )
;          R3 -> start of disc buffer
;          R4 = drive number
;          all other regs & flags preserved
;**************************************************************************

; object type = FILE / NOTFOUND / DIRECTORY

; R6 = PointerToEntry
; R8 = Drive number (0..7), name is a dir(8), ultimate object(9..10)
;      end of path reached (11) if clear
; R9 = Word
; R10 = PointerToPath
; R11 = PointerToDiscBuffer ( each individual buffer, starts as main one )
; R12 -> workspace
; R13 -> FD stack  

; Variables used:
; tempLength                    = length in blocks of directory
; tempBlockSize
; DiscNumberOfDirectoryInBuffer

 STASH "R5 - R11, R14"


;****************
; R10 -> pathname passed in ( changed later )
;****************

 MOV R8, R1, ASL #9

 MOV R10, R0

;****************
; This will return the drive number for a given disc ( prompts for disc name
;                                                     etc; )
;****************


 BL FindDriveNumber           ; R0 -> pathname, RETURNS R1 = drive number

;****************
; R8 = drive number implied ( constant )
;****************

 ORR R8, R8, R1

;****************
; If drive has not yet been used, then find its device & LUN number
;****************

 Debug " R4 = number of drives "

; LDRB R4, numberofdrives

 AND R0, R8, #255


; CMP R8, R4                    ; drive number > number of drives ?

; BLGT AnotherDriveHasBeenAdded ; R0 = drive number added, RETURNS V set if error
; BVS Error

;****************

 Debug " Get disc number "



 [ RISC_OS=2

; MOV R2, #0
 BL PreGetUniqueNumber ; R0 = drive number, RETURNS R1, R2 = 0 or 1


 ]

;****************
; This sorts out the URD, LIB, CSD etc; and also validates the path
;****************

 MOV R2, R1
 MOV R0, R10
 AND R1, R8, #255

 BL FullPathName             ; RETURNS R0 -> whole name, R1 = drive number
                             ; R2 = disc number



;****************
; R10 now points at the new pathname ( stripped of leading drive number )
;****************

 MOV R10, R0

;****************
; This gets a pointer to the disc main directory ( and header info )
;****************

 AND R0, R8, #255

 BL TestKnowDisc              ; R0 = drive number, RETURNS R1 -> buffer
                              ; RETURNS R2 = disc number
; TEQ R1, #0                   ; Not found the buffer for that disc
; addr R0, InternalError, EQ
; BEQ Error

;****************
; R11 -> start of directory ( past header information in root directory )
;****************


 STR R2, DiscNumberOfDirectoryInBuffer

; Make sure that blocky offset starts from zero

 MOV R14, #0
 STR R14, tempInk


;****************
; Remember where main directory information is
;****************

 Debug " Store R1 "

 STR R1, maindirpointer

 ADD R1, R1, #MAINDIRECTORYOFFSETINABUFFER








;****************
; tempbufferpointer -> start of this directory buffer
;****************

 STR R1, tempbufferpointer












;****************
; Remember length
;****************

; R11 -> directory details ( past header info, so have to back track )

 LDR R4, [ R1, #LENGTHOFMAINDIROFFSETINABUFFER - MAINDIRECTORYOFFSETINABUFFER ]
 STR R4, tempLength

;****************
; Remember block size
;****************

; R11 -> directory details ( past header info, so have to back track )

 LDR R4, [ R1, #BLOCKSIZEOFFSETINABUFFER - MAINDIRECTORYOFFSETINABUFFER ]
 STR R4, tempBlockSize

;****************
; Remember disc type ( ISO or HISIERRA )
;****************

; R11 -> directory details ( past header info, so have to back track )

 LDRB R3, [ R1, #DISCTYPEOFFSETINABUFFER - MAINDIRECTORYOFFSETINABUFFER ]
 STRB R3, tempDisctype

;****************
; Remember block start
;****************

 LDR R9, [ R1, #LBAOFMAINDIROFFSETINABUFFER - MAINDIRECTORYOFFSETINABUFFER]
 STR R9, tempBlock
 
;****************
; Start with branch/leaf 1
;****************

 MOV R9, #0                  ; Word = 1

;****************


;*******************************************
try_again
;*******************************************

 MOV R11, R1

;****************
; Move to next branch/leaf in path
;****************

 Debug " Next word "

 ADD R9, R9, #1

;****************
; Put next leaf/branch in 'tempbuffer'
;****************

 MOV R0, R10

 ADD R1, R12, #:INDEX:tempbuffer

; R0 -> pathname$,R1 -> word$, R9 = word number
; R0 & R2 - R5 corrupted

; WordFromPath    ; Macro

 MOV R5, #0
 MOV R4, R1

;*****************
; First move to correct '.'
;*****************

01

 LDRB R3, [ R0 ], #1

 TEQ R3, #0                     ; Reached end of path, but didn't find word ?
 BEQ directory

 TEQ R3, #DIVIDER
 ADDEQ R5, R5, #1                   ; Increase word count if found '.'

 CMP R5, R9

 BLT %BT01

 Debug " Found right branch "

;**************
; Found start position, now copy word to caller
;**************

02

 LDRB R3, [ R0 ], #1
 STRB R3, [ R4 ], #1
 TEQ R3, #DIVIDER
 TEQNE R3, #0

 BNE %BT02

; --- End of path ? BUT DID I WANT A DIRECTORY OR A FILE ? ---

 TEQ      R3, #0
 ORRNE    R8, R8, #1:SHL:11
 BICEQ    R8, R8, #1:SHL:11

; TEQ R3, #0
; BNE %FT12

 TST R8, #512
 ORRNE R8, R8, #256   ; Directory
 BICEQ R8, R8, #256   ; File

; --- If divider following, then must be a directory wanted ---

 TEQ R3, #DIVIDER
 ORREQ R8, R8, #256   ; Directory
 BICNE R8, R8, #256   ; File

; --- Get rid of last dot ---

 MOV R3, #0
 STRB R3, [ R4, #-1 ]

;****************
; Look through directory for the branch/leaf name [ Vset if not found ]
;****************

 Debug " R0 = R11 "

 MOV R0, R11

; R0 -> memory, R1 -> name$, RETURN R2 = position  Vset if not found

 MOV R5, R0
 MOV R0, R1

01

 ADD R1, R5, #OBJECTNAMEOFFSET         ; R0 -> wildcarded string
                                       ; R1 -> prepared string in buffer
; --- Do I care what I'm looking for ? ---

 CLV

 TST R8, #1024
 BNE %FT11                        ; [ no ]

; --- If haven't reached end of path, then must look for a directory ---

; CLV

 TST R8, #256                          ; If it's a directory that you want
                                       ; but you don't find one, ignore it
 LDRB R14, [ R5, #OBJECTTYPEOFFSET ]
 TEQNE R14, #DIRECTORY
 SEV NE

11

;-------------------------------------------------------------------
; RISC OS 3 finds it's wildcarded filenames differently
;-------------------------------------------------------------------
 [ RISC_OS=2
 BLVC WildCompareStrings               ; RETURNS VCLEAR if found

 MOVVC R2, R5                          ; [ yes ]
 BVC %FT02                             ;
 |
         STASH      "r0-r3"
15
         LDRB       r2, [ r0 ], #1
         LDRB       r3, [ r1 ], #1
         TEQ        r2, r3

; not found
         GRAB       "r0-r3", NE
         BNE        %FT16

         CMP        r2, #32
         BGE        %BT15

; found
         GRAB       "r0-r3"
         MOV        r2, r5
         B          %FT02
16

 ]

;-------------------------------------------------------------------


 ADD R5, R5, #OBJECTNAMEOFFSET + 1

04
 LDRB R4, [ R5 ], #1
 TEQ R4, #0
 BNE %BT04

 ALIGNREG R5

 LDR R4, [ R5, #LBASTARTOFFSET ]
 MOVS R4, R4, LSR #8

 BNE %BT01

; Move to next block, if there is one

 LDR R14, tempLength

 LDR R0, tempBlockSize
 SUBS R14, R14, R0

 MOVLE R1, #0                            ; NOT FOUND
 MOVLE R2, #NOTFOUND                     ;
 GRABS "R5 - R11, PC", LE                ;

 STR R14, tempLength

 LDR R14, tempInk
 ADD R14, R14, #MAX_BLOCKS_BUFFERED
 STR R14, tempInk
 LDR R0, tempBlock                   ; need to keep this as start block
 ADD R0, R14, R0                     ; 'cause use in other procedures (sorry)

 SUB R9, R9, #1                      ; 'cause I haven't found anything

 B more_blocky



02  ; Found

;****************
; R6 -> entry details
;****************

 Debug " R6 = R2 "

; blocky offset back to zero

 MOV R6, #0
 STR R6, tempInk

 MOV R6, R2

;****************
; Change wildcarded name in path to full name that was found
;****************

; MOV R0, R10
; ADD R1, R2, #OBJECTNAMEOFFSET
; MOV R2, R9

 [ debug=ON
 Debug " Name found = "
 STASH R0
 MOV R0, R1
 Debug0
 GRAB R0
 ]

; BL CompleteName                       ; R0-> wild path, R1 -> name, R2=word

;****************
; Copy entry details into 'TempArea' ( so that 'ReadCatalogue' can use it )
;****************


 Debug " Copy TempArea "

 MOV R1, R6                                       ; Copy from

 ADD R2, R12, #:INDEX:TempArea                    ; Copy to

 MOV R3, #OBJECTNAMEOFFSET + MAXLENGTHOFNAME + 1  ; Copy length

 ; R1 -> from, R2 -> to, R3 = length


 SWI       XCD_ByteCopy


;****************
; The object was a file ? [ yes - then end search ]
;****************

 LDRB R4, [ R6, #OBJECTTYPEOFFSET ]
 TEQ R4, #FILE                ; If it is A FILE then the end has been reached
 BNE    %FT23

 TST   R8, #1:SHL:11          ; end of path ?
 MOVNE R1, #0                 ; [ no - so NOT FOUND ]
 MOVNE R2, #NOTFOUND          ;

 MOVEQ R0, R10                ; [ yes - so return file details ]
 MOVEQ R1, R6                 ;
 MOVEQ R2, #FILE              ;
 LDREQ R3, maindirpointer     ;
 ANDEQ R4, R8, #255           ;

 GRABS "R5 - R11, PC"

23

;****************
; Work out real length of dir/file from block size and length
;****************

 Debug " Real length "

 LDR R5, [ R6, #LENGTHOFFSET ]

 STR R5, tempLength

;****************
; Attempt to buffer this directory, if possible
;****************



;*******
; R0 = LBA of start of next dir ( LBA is stored packed )
;*******

 LDR R0, [ R6, #LBASTARTOFFSET ]
 MOV R0, R0, LSR #8
 STR R0, tempBlock

more_blocky


;*************
; This will store a directory in the buffer, if possible ( or not there )
;*************

 Debug " Buffer directory "

; R0 = Start LBA
; R1 ~
; R2 ~
; R3 ~
; R4 ~
; R5 ~
; R6 ~

 STASH "R0, R2"
 MOV R2, R0
 LDR R0, DiscNumberOfDirectoryInBuffer
 BL FindDiscInBufferList  ; R0 = disc, RETURNS R1 -> buf, R2 = LBA
                          ; C set if not found, else C clear

;*************
; Make R1 -> directory if found, else R1 -> place to put dir
;*************

 LDRCS R1, =:INDEX:sparedirectorybuffer + MAINDIRECTORYOFFSETINABUFFER
 ADDCS R1, R1, R12
 STR R1, tempbufferpointer

 GRAB "R0, R2"
 BCC try_again

;*************
; This will store a directory in the buffer, if not known
;*************


;R0 = start LBA, R1 -> place to put, R2 UNUSED
;R3 = disc type, R4 = blocksize, R5 = drive number, RETURNS R6 = size of mem

 STASH "R0 - R5"

 ADD R3, R12, #:INDEX:tempBlockSize
 LDMIA R3, { R4, R6 }

 LDRB R3, tempDisctype

 AND R5, R8, #255

 BL StoreDirectory          ; RETURNS EQ if end of dir reached, else NE

 TEQ    R6, #0

 GRAB  "R0-R5",EQ
 MOVEQ R1, #0                            ; NOT FOUND
 MOVEQ R2, #NOTFOUND                     ;
 GRABS "R5 - R11, PC", EQ                ;

 MOV R2, R0
 MOV R3, R1
 MOV R1, R6                                ;
 LDR R0, DiscNumberOfDirectoryInBuffer
 BL AddDirectoryToBuffer      ; R0 = disc, R1 = size, R2 = block, R3->dire

;----------------------------------------
; The main directory entry may have moved
;----------------------------------------
 LDR       r0, DiscNumberOfDirectoryInBuffer
 MOV       r2, #PVD
 BL        FindDiscInBufferList

 [ RISC_OS=2
 ADRCSL    r0, Internalerror
 BCS       Error
 |
 MOVCS     r0, #ERROR_INTERNALERROR
 BCS       Error
 ]

 STR       r1, maindirpointer

;----------------------------------------

 GRAB "R0-R5"

 B try_again

; --- Reached end of path, but didn't find word ? ---

directory


 MOV R0, R10                        ; R0 -> expanded pathname
 MOV R1, R11                        ; R1 -> current buffer
 MOV R2, #DIRECTORY                 ; R2 = directory attrib
 LDR R3, maindirpointer             ; R3 -> main dir header
 AND R4, R8, #255                   ; R4 = drive number
 GRAB "R5 - R11, R14"
 BICS PC, R14, #Overflow_Flag

;********************************************************************
; Compare 2 strings
;entry:
; R0 -> first string
; R1 -> second string
; R2 = length
; exit:
; Z = 1 if found, else Z = 0
; All other flags preserved

CompareStrings ROUT
;********************************************************************
; If length = 0 THEN must be same !

 TEQ R2, #0
 ORREQS PC, R14, #Zero_Flag

; R2 -> end of R1

 STASH "R0 - R4"

 ADD R2, R2, R1

01

 LDRB R4, [ R0 ], #1
 LDRB R3, [ R1 ], #1

 TEQ R4, R3

 GRAB "R0 - R4", NE
 BICNES PC, R14, #Zero_Flag

 CMP R1, R2
 BLT %BT01

 GRAB "R0 - R4"
 ORRS PC, R14, #Zero_Flag

;********************************************************************
; Strip leading and trailing spaces from a string ( R0 -> string )
CutSpace ROUT
;********************************************************************

 STASH "R0 - R4, R14"

 MOV R3, R0                   ; Find the length of the string

01

 LDRB R2, [ R3 ], #1
 TEQ R2, #0

 BNE %BT01

 SUB R2, R3, R0               ; R2 = length of string
                              ; R3 -> end of string
 MOV R1, R3

;***************
; First chop the trailing spaces
;***************

 SUB R3, R3, #2

02

 LDRB R4, [ R3 ], #-1

 TEQ R3, R0                      ; If string is all spaces
 MOVEQ R4, #0
 STREQB R4, [ R3, #1 ]
 GRAB "R0 - R4, PC", EQ

 TEQ R4, #SPACE
 BEQ %BT02

 MOV R4, #0
 STRB R4, [ R3, #2 ]!

 SUB R2, R3, R0                   ; length of string

;***************
; Now chop the leading spaces
;***************

 MOV R1, R0

03

 LDRB R4, [ R1 ], #1

 CMP R1, R3
 GRAB "R0 - R4, PC", GE

 TEQ R4, #SPACE
 BEQ %BT03


 SUB R1, R1, #1

 CMP R1, R0

 GRAB "R0 - R4, R14", LE
 MOVLES PC, R14

;**************
; Shuffle string back if needed
;**************

04

 LDRB R4, [ R1 ], #1
 STRB R4, [ R0 ], #1

 CMP R1, R3
 BLE %BT04

 GRAB "R0 - R4, PC"


 [ RISC_OS=2

;********************************************************************
FullPathName ROUT;( RETURNS R0 -> pathname, R1 = drive number, R2 =disc number )

; uses tempbuffer_offset
; uses TempArea_offset
;********************************************************************

 STASH "R1 - R8, R14"

; R6 always -> path passed in
; R7 always = drive number
; R8 = unique disc number

 MOV R7, R1

 MOV R8, R2

 MOV R6, R0

 LDRB R1, [ R0 ]
 TEQ R1, #":"                   ; drive specified in pathname

 BNE %FT02

;**************
; Move past number to path name
; R6 -> $.fhgsj.fjfhgd
;**************

01

 LDRB R1, [ R6 ], #1
 TEQ R1, #0                   ; terminator or divider
 SUBEQ R6, R6, #1             ; Move back a pos. if end of string
 TEQNE R1, #"."               ;
 BNE %BT01


;**************
; Make sure that 'drive.\', 'drive.@'
; give a 'bad name' error
;**************

 LDRB R1, [ R6 ]
 TEQ R1, #"\"
 TEQNE R1, #"@"

 [ RISC_OS=2
 BEQ       badname
 |
 MOVEQ     r0, #ERROR_BADNAME
 BEQ       Error
 ]

;*************
; Copy the '$.jfhdjs.gdf' into 'tempbuffer' and make sure that the whole
; path is specified in upper case
;*************

; R0 -> start of pathname, $.FHSDHGA.GFJA

02

;03

;*************
; If lib, urd, previous, current directories were spec. then fill them in
; R6 -> $.jhjh.ghghg
; R7 = drive number
;*************


 LDRB R4, [ R6 ]              ; What is this character I see before me ?

 TEQ R4, #"$"                 ; Wonderful ! The main directory !
 BEQ fill_in_ROOT



 [ RISC_OS=2

 TEQ R4, #"&"                 ; The User Root Directory
 BEQ fill_in_URD

 TEQ R4, #"@"                 ; The current directory
 BEQ fill_in_CSD

 TEQ R4, #"\"                 ; The previous directory
 BEQ fill_in_previous
 
 TEQ R4, #"%"                 ; The library directory
 BEQ fill_in_library

 B fill_in_CSD                ; Must mean current directory

 ]


;*************
; Remove the '^' ( move up branch ) from the full string
; R0 & R4 -> start of path
;*************

Cut_crap

 fill_in_up_pathMacro   ; MACRO to remove '^' from path and END !

;**************
fill_in_URD
;**************
; R1 -> buffer
; R2 = drive number
; R6 -> '&.arfle.fred'



 fill_in_URDMacro      ; MACRO to remove '^' from path and END !


 B Cut_crap

;*******************
fill_in_CSD              ; The current directory R1 -> buffer
;*******************     ; R6 -> '&....' R2 = drive number

 fill_in_CSDMacro        ; MACRO to handle currently selected directory

 B Cut_crap

;******************
fill_in_parent
; This only appends '^.fks.fds' to the CSD and fill_in_upMacro deals with
; the '^' in the string
;******************
; R1 -> buffer
; R2 = drive number
; R6 -> '^.arfle.fred'


 fill_in_parentMacro

 B Cut_crap

;******************
fill_in_previous
;******************
; R1 -> buffer
; R2 = drive number
; R6 -> '\.arfle.fred'

 fill_in_previousMacro

 B Cut_crap

;********************
fill_in_library
;********************
; R1 -> buffer
; R6 -> '%.arfle.fred'
; R7 = drive number

 fill_in_libraryMacro

 B Cut_crap



;*******************
fill_in_ROOT             ; The ROOT directory  R1 -> start of buffer
;*******************     ; R6 -> '$....' R2 = drive number
                         ; RETURNS R0 -> new pathname


 fill_in_rootMacro


 B Cut_crap


 |

;------------------------------------------------------------------------------------------
FullPathName ROUT;( RETURNS R0 -> pathname, R1 = drive number, R2 =disc number )

; uses tempbuffer_offset
; uses TempArea_offset
;------------------------------------------------------------------------------------------

            STASH       "r1-r4, r14"

            LDRB        r3, [ r0 ]
            TEQ         r3, #":"
            GRAB        "r1-r4, pc", NE

            ADD         r0, r0, #1

02
            LDRB        r3, [ r0 ], #1
            TEQ         r3, #"."
            BNE         %BT02

            GRAB        "r1-r4, pc"


;------------------------------------------------------------------------------------------

 ]

;-----------------------------------------------------------------------------

;********************************************************************

PreLoadBlockFromDrive ; This sets R1=buffer in preperation

 LDR R1, =:INDEX:buffer
 ADD R1, R1, R12

;********************************************************************
LoadBlockFromDrive ROUT ; R0 = block, R1 -> memory, R2 = length ( blocks )
                   ; R3 = drive number
; , R4 = unique disc number ????????
;********************************************************************

 STASH "R0 - R7, R14"

 MOV R4, R0
 MOV R5, R1

; ADD R7, R12, #sparecontrolblock_offset

 MOV R0, R3

 BL PreConvertDriveNumberToDeviceID   ; R0 = drive number, R7 -> memory

 MOVVC R3, R5

 MOVVC R1, R4

 MOVVC R0, #0

; ADD EXTRA PARAMETER HERE
 MOVVC R4, #myblocksize

 SWIVC XCD_ReadData

 BVS Error

 GRAB "R0 - R7, R14"
 MOVS PC, R14

;********************************************************************
FindDriveNumber ROUT ;( R0 -> pathname, RETURNS R1 = drive )
;********************************************************************

 BIC R14, R14, #Overflow_Flag
 STASH "R0, R2 - R7, R14"

 Debug " FindDriveNumber "

;**********************
; convert lower case to upper case
;**********************

 MOV R6, R0

01

 LDRB R2, [ R6 ]

 UpperCase R2, R14

 STRB R2, [ R6 ], #1

 TEQ R2, #0

 BNE %BT01


 MOV R6, R0

 LDRB R1, [ R6 ]
 TEQ R1, #":"                     ; drive specified in pathname
 BEQ %FT02                        ; either by name, or number ?
                                  ; [ must mean current drive number ]

 TEQ R1, #"\"                     ; Use the old drive number ! if previous
 LDREQB R1, olddrivenumber        ; path is required
 LDRNEB R1, CurrentDriveNumber    ; 
 GRABS "R0, R2 - R7, PC"          ; 


02

 ADD R1, R0, #1                 ; Copy into a safe area

 LDRB R3, [ R1 ]                ; Nothing specified ? ( no name, no number )
 TEQ R3, #0                     ;
 [ RISC_OS=2
 BEQ badname                     ; [ nothing ] so error
 |
      MOVEQ      r0, #ERROR_BADNAME
      BEQ        Error
 ]

 ADD R3, R12, #:INDEX:TempArea  ;
 MOV R4, R3                     ;
                                ;
04                              ;
 LDRB R2, [ R1 ], #1            ;
 STRB R2, [ R3 ], #1            ;
 TEQ R2, #"."                   ;
 TEQNE R2, #0                   ;
 TEQNE R2, #&0D                 ;
 TEQNE R2, #SPACE               ;
 BNE %BT04                      ;

 ; Make sure that 'drive.&', 'drive.%', 'drive.\', 'drive.@'
 ; give a 'bad name' error

 TEQ R2, #0

 BEQ fine

 LDRB R2, [ R1 ]
 TEQ R2, #"&"
 TEQNE R2, #"%"
 TEQNE R2, #"\"
 TEQNE R2, #"@"

 [ RISC_OS=2
 BEQ badname
 |
      MOVEQ      r0, #ERROR_BADNAME
      BEQ        Error
 ]

fine

 MOV R2, #0                     ; Null terminate entry
 STRB R2, [ R3, #-1 ]

 MOV R1, R4

 STASH R0

 MOV R0, #10

 SWI XOS_ReadUnsigned

 GRAB "R0"                 ; must be a name
 BVS SoItIsADiscName
                           ; was a number

 [ debug=ON
 STASH "R4,R14"
 Debug " Drive number = "
 MOV R4, R2
 DebugDisplay R4
 GRAB "R4,R14"
 ]

 CMP R2, #MAXNUMBEROFDRIVESSUPPORTED
 [ RISC_OS=2
 addr R0, BadDrive, GT         ; Not a valid drive ( > than allowable number
 BGT Error
 |
     MOVGT       r0, #ERROR_BADDRIVE
     BGT         Error
 ]


 MOV R1, R2

 GRABS "R0, R2 - R7, PC"

;******************
SoItIsADiscName
;******************

 MOV R0, R4
 
 BL CheckDiscName              ; R0 -> disc name, RETURNS R1 TRUE/FALSE

 TEQ R1, #FALSE
 [ RISC_OS=2
 BEQ badname
 |
       MOVEQ        r0, #ERROR_BADNAME
       BEQ          Error
 ]

 BL FindDiscNameInList         ; R0 -> disc name, RETURNS R1 = drive number

 CMP R1, #-1                   ; Disc known ?

 LDREQB R1, CurrentDriveNumber ; [ no ]

 BL PromptForDisc         ; R0 -> disc name to prompt for, R1 = drive

; MOV R0, R1
; BL TestKnowDisc   ; R0 = drive number, RETURNS R1->buf, RETURNS R2 = disc
; MOV R1, R0

 GRABS "R0, R2 - R7, PC"                ; 'V' already cleared


;********************************************************************
PromptForDisc ROUT ; R0 -> name of disc, R1 = drive number
;********************************************************************

 BIC R14, R14, #Overflow_Flag
 STASH "R0 - R8, R14"

; STASH "R0-R4,R14"
; SWI &100+12
; Debug "Drive = "
; MOV R4, R1
; DebugDisplay R4
; WaitForAKey
; GRAB "R0-R4,R14"

 ; First look at disc in drive to see if known

 MOV R6, R0
 MOV R8, R1
 MOV R2, R0

 LDR R14, discbuffersize
 TEQ R14, #0
 BEQ fiddle_buffer_for_prompt

;-----------------------------
; Is disc in drive already ?
;-----------------------------

; Is drive mounted ?
; Is disc in drive same ?


; Is disc_name in disc list ?        - no so not_in_memory

 BL FindDiscNameInList         ; R0 -> disc name, RETURNS R1 = drive number

 CMP R1, #-1                   ; Disc known ?

 BEQ fiddle_buffer_for_prompt


 Debug " R1 != -1 "


; Is drive same as requested drive ? - no so not_in_memory

 TEQ R8, R1
 BNE fiddle_buffer_for_prompt


 Debug " r1 = r8 "


; Is disc in drive same as requested disc ?

 STASH R2

 MOV R2, #1
 MOV R0, R8
 BL GetUniqueNumber
 TEQ R1, #0
 GRAB R2
 BEQ fiddle_buffer_for_prompt


 Debug " BL GetUniqueNumber "


 LDR R14, =:INDEX:discsMounted
 ADD R14, R14, R12
 LDR R14, [ R14, R0, LSL #2 ]
 TEQ R14, R1
; Yes so exit without prompt
 GRABS "R0 - R8, PC", EQ

 B fiddle_buffer_for_prompt

;-----------------------------

not_in_memory

 MOV R0, R6

prompt_for_disc

 MOV R1, #FILINGSYSTEMNUMBER ; R1 = CDFS
 MOV R2, R0            ; R2 -> disc name
 MOV R3, R8            ; R3 = drive number
 MOV R0, #Medium_Not_Present ; R0 = media not present ( see page 137 of PRM )
 MOV R4, #0            ; R4 = iteration count
 MOV R5, #-1           ; R5 = Timeout never
 ADR R6, COMPACTDISC   ; R6 = media type name

 SWI XOS_UpCall

 [ RISC_OS=2
 BVS discnotfound                 ; Escape pressed
 |
             MOVVS       r0, #ERROR_DISCNOTFOUND
             BVS         Error
 ]


 TEQ R0, #0                      ; Forget it ?
 [ RISC_OS=2
 BNE discnotfound                 ; [ yes ]
 |
             MOVNE       r0, #ERROR_DISCNOTFOUND
             BNE         Error
 ]

fiddle_buffer_for_prompt

 MOV R0, R8


; ADD R7, R12, #sparecontrolblock_offset
 BL PreConvertDriveNumberToDeviceID ; R0 = drive number, R7 -> controlblock
 BVS Error


;********
; Is drive ready ?
;********

 SWI XCD_DriveStatus

 TEQ R0, #1                      ; Drive not ready so try again
 SEV NE                          ;
 GRAB "R0 - R8, R14", VS         ;
 STASH "R0 - R8, R14", VS        ;
 MOVVS R8, R1                    ;
 BVS prompt_for_disc             ;


;********

 MOV R0, #0                      ;
 MOV R1, #PVD                    ; make sure that disc is in mode 1 or 2
 SWI XCD_EnquireDataMode         ;


;---------------------------------------------

 [ Version > 218

; This just makes sure that it's not an audio disc

 TEQ       r0, #0
 SEV       EQ
 GRAB      "r0 - r8, r14", VS
 STASH     "r0 - r8, r14", VS
 MOVVS     r8, r1
 BVS       prompt_for_disc

 | ; old way that only checks for mode 1 and mode 2
 TEQ R0, #1
 TEQNE R0, #2
 SEV NE
 GRAB "R0 - R8, R14", VS         ;
 STASH "R0 - R8, R14", VS        ;
 MOVVS R8, R1
 BVS prompt_for_disc             ;

 ]

;---------------------------------------------


 [ debug=ON
 SWI XOS_NewLine
 Debug " SET DISC CHANGED "
 SWI XOS_NewLine
 ]

 SWI XCD_DiscHasChanged


; LDR R1, =buffer_offset
; ADD R1, R12, R1


 STASH R2 - R3
 MOV R0, #PVD
 MOV R2, #1
 MOV R3, R8
 BL PreLoadBlockFromDrive ; R0 = block, R1 -> memory, R2 = length ( blocks )
                       ; R3 = drive number
 GRAB R2 - R3

 MOV R0, R8
 BL GetDiscNameNotInBuffer ; R0 = drive, R1 -> name of disc in drive
 MOV R0, R1
 BL CutSpace               ; R0 -> disc name

 MOV R1, R2                ; R0 -> disc name to hope for

 LengthOfString R0, R2, R14
 LengthOfString R1, R3, R14


 TEQ R2, R3

 BLEQ CompareStrings    ; returns 'eq' or 'ne'

 GRAB "R0 - R8, R14", NE
 STASH "R0 - R8, R14", NE
 MOVNE R8, R1
 BNE prompt_for_disc


 ; Found the right disc, now tell UpCall about it

 MOV R0, #Medium_Found
 SWI XOS_UpCall

 GRAB "R0 - R8, PC"

COMPACTDISC = "CD-ROM"                ; Keep together
NULL        = 0                       ;

 ALIGN

;********************************************************************
FindDiscNameInList ROUT    ; R0 -> disc name, RETURNS R1 = drive number
                           ;                          R1 = -1 if not found
;********************************************************************

; R2 -> disc name in list
; R5 = number of discs left to search
; R6 -> disc name to search for
; R8 = -1 if no names found so far, else = drive found at

 STASH "R0, R2 - R8, R14"

 MOV R8, #-1

 [ debug=ON
 STASH R0
 Debug " FindDiscNameInList discname = "
 Debug0
 SWI XOS_NewLine
 GRAB R0
 ]

; LDR R5, discbuffersize
; TEQ R5, #0
; MOVEQ R1, #-1
; GRABS "R0, R2 - R8, PC", EQ

 MOV R5, #MAXNUMBEROFDRIVESSUPPORTED
 MOV R7, R0
 LDR R2, =:INDEX:DiscNameList
 ADD R2, R2, R12

 [ debug=ON
 STASH r4
 Debug " disc name list = &"
 MOV r4, r2
 DebugDisplay r4
 GRAB r4
 ]


REPEAT_find_disc_name


 MOV R4, R2                     ; R4 -> disc name in list
 MOV R6, R7                     ; R6 -> start of name to search for
 ADD R0, R4, #LENGTHOFDISCNAME  ; R0 -> end of R4

REPEAT_compare_disc_names       ; Is disc name in list = disc name ?
                                ;
  LDRB R3, [ R4 ], #1           ;
  LDRB R1, [ R6 ], #1           ;

  CMP R4, R0                    ; End of disc name ?
  BGE disc_name_not_found       ; [ yes ]

  TEQ R3, #0                    ;
  TEQEQ R1, #0                  ; If both terminate at same time,must be same
  BEQ UNTIL_compare_disc_names  ;
                                ;
  TEQ R3, #0                    ;
  TEQEQ R1, #"."                ; If both terminate at same time,must be same
  BEQ UNTIL_compare_disc_names  ;
                                ;
  TEQ R3, R1                    ;
  BEQ REPEAT_compare_disc_names ;
                                ;
                                ; [ no ]


disc_name_not_found

  SUBS R5, R5, #1
  ADDNE R2, R2, #LENGTHOFDISCNAME
  BNE REPEAT_find_disc_name

  CMP     R8, #-1               ; Name occurs once, ie/ NOT Ambig name
  MOVNE   R1, R8                ;
  GRABS "R0, R2 - R8, PC", NE   ;


 MOV R1, #-1                    ; not found

 Debug " Not found "
                                ;
 GRABS "R0, R2 - R8, PC"        ;


UNTIL_compare_disc_names

 Debug " Found "

 CMP     R8, #-1                 ; Ambiguous disc name check

 [ RISC_OS=2
 addr    R0, AmbiguousDiscNameError, NE
 BNE     Error
 |
        addr      r0, AmbiguousDiscNameError_tag, NE
        ADRNE     r1, message_block
        MOVNE     r2, #0
        SWINE     XMessageTrans_ErrorLookup
        BNE       Error
 ]

 RSB     R8, R5, #MAXNUMBEROFDRIVESSUPPORTED ; drive found at = 28 - R5

 SUBS    R5, R5, #1
 ADDNE   R2, R2, #LENGTHOFDISCNAME
 BNE     REPEAT_find_disc_name

 MOV     R1, R8
 GRABS   "R0, R2-R8,PC"

;********************************************************************
AddDiscNameInList ; R0 = drive number, R1 -> disc name
 ROUT
;********************************************************************

 STASH "R0 - R7, R14"

; Find where to put the name

 LDR R2, =:INDEX:DiscNameList   ; R2 -> start of list place to put name
 ADD R2, R2, R12                ;
                                ;
 MOV R3, #LENGTHOFDISCNAME      ;
                                ;
 MLA R2, R0, R3, R2             ;

 ADD R3, R2, R3                 ; R3 -> end of disc name in list

 MOV R5, R2

REPEAT_copy_disc_name

 LDRB R4, [ R1 ], #1
 STRB R4, [ R2 ], #1
 CMP R2, R3

 BLT REPEAT_copy_disc_name

 MOV R2, #0
 STRB R2, [ R3, #-2 ]

 GRABS "R0 - R7, PC"



 [ RISC_OS=2
;********************************************************************
WildCompareStrings ROUT ; R0 -> wild carded string, R1 -> string
                        ; RETURNS VCLEAR if same, ELSE VSET
;********************************************************************

 STASH "R0 - R2, R4 - R6"

hello_wildman

 LDRB R4, [ R0 ], #1
 LDRB R3, [ R1 ], #1

 TEQ R4, #WILDANY                ; Lots of wild chars ?   '*'
 BEQ found_lots_of_chars         ; [ yes ]

 TEQ R4, #0                      ; Strings are the same
 TEQEQ R3, #0                    ;
 GRAB "R0 - R2, R4 - R6", EQ     ;
 BICEQS PC, R14, #Overflow_Flag  ; [ yes ]


 TEQ R4, R3                      ; Chars are the same ?
 TEQNE R4, #WILDCHAR             ;
                                 ;
 GRAB "R0 - R2, R4 - R6", NE     ;
 ORRNES PC, R14, #Overflow_Flag  ; [ no ]

 B hello_wildman

;************
found_lots_of_chars              ; R0-> past star, R1->string
;************

 LDRB R4, [ R0 ], #1
 TEQ R4, #0                      ; End of string, so terminate ?
 GRAB "R0 - R2, R4 - R6", EQ     ;
 BICEQS PC, R14, #Overflow_Flag  ; [ yes ]

 TEQ R4, #WILDANY                ; Followed by another '*'
 BEQ found_lots_of_chars         ;

; R0 -> wild string
; R1 -> string
; R4 = letter after *, could be '#'

find_that_char

 LDRB R3, [ R1 ], #1
 TEQ R3, #0
 GRAB "R0 - R2, R4 - R6", EQ     ; End of normal string ?
 ORREQS PC, R14, #Overflow_Flag  ; [ yes ]

 TEQ R4, #WILDCHAR
 TEQNE R3, R4
 BNE find_that_char

; R0 -> wild string
; R1 -> normal string
; R3 = normal char = R4
; R4 = wild char = R3 or '#'

 SUB R5, R0, #1
 MOV R6, R1

oh_my_word

 LDRB R4, [ R0 ], #1
 LDRB R3, [ R1 ], #1

 TEQ R4, #WILDANY                ; Another wild card
 BEQ found_lots_of_chars         ;

 TEQ R3, #0                      ; Strings terminated together
 TEQEQ R4, #0                    ;
 GRAB "R0 - R2, R4 - R6", EQ     ;
 BICEQS PC, R14, #Overflow_Flag  ; [ yes ]

 TEQ R3, #0                      ; End of either string ?
 TEQNE R4, #0                    ;
 GRAB "R0 - R2, R4 - R6", EQ     ;
 ORREQS PC, R14, #Overflow_Flag  ; [ yes ]


 TEQ R4, #WILDCHAR               ; Next part doesn't match, maybe later on ?
 TEQNE R3, R4                    ; 
 LDRNEB R4, [ R5 ]               ;
 MOVNE R0, R5                    ;
 MOVNE R1, R6                    ;
 BNE find_that_char              ; [ yes ]

 B oh_my_word


 ] ; ONLY NEEDED FOR RISC OS 2


;********************************************************************
;********************************************************************
;********************************************************************

 LTORG

 END
