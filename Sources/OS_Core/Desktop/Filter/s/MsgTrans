; > s.MsgTrans

;---------------------------------------------------------------------------
; MsgTrans_OpenFile
;
;       Out:    r0 -> message file block
;
;       Open our messages file.
;
MsgTrans_OpenFile
        ENTRY   "r1-r2"

        ADR     r0, message_block
        LDR     lr, flags
        TST     lr, #f_messagesopen
        EXIT    NE

        ADR     r1, message_file_name
        MOV     r2, #0
        SWI     XMessageTrans_OpenFile
        LDRVC   lr, flags
        ORRVC   lr, lr, #f_messagesopen
        STRVC   lr, flags

        EXIT


;---------------------------------------------------------------------------
; MsgTrans_CloseFile
;
;       Close our messages file.
;
MsgTrans_CloseFile
        ENTRY   "r0,r1"

        ADR     r0, message_block
        LDR     r1, flags
        TST     r1, #f_messagesopen
        BICNE   r1, r1, #f_messagesopen
        STRNE   r1, flags
        SWINE   XMessageTrans_CloseFile

        EXITS


;---------------------------------------------------------------------------
; MsgTrans_ErrorLookup
;       In:     r0 -> token error block
;               r4-r7 -> optional parameters
;       Out:    r0 -> translated error block
;
;       Lookup an error in our messages file.
;
MsgTrans_ErrorLookup
        ENTRY   "r0-r2"

        CLRV
        BL      MsgTrans_OpenFile
        MOVVC   r1, r0
        LDRVC   r0, [sp]
        MOVVC   r2, #0
        SWIVC   XMessageTrans_ErrorLookup
        STR     r0, [sp]

        EXIT


;---------------------------------------------------------------------------
; MsgTrans_Lookup
;
;       In:     r1 -> token
;               r2 -> buffer (or 0)
;               r3 = size of buffer (if r2 non-zero)
;               r4-r7 = parameters
;       Out:    r2 -> translated string
;               r3 = size of result before terminator
;
;       Look up the given token in our messages file.
;
MsgTrans_Lookup
        ENTRY   "r0"

        CLRV
        BL      MsgTrans_OpenFile
        SWIVC   XMessageTrans_Lookup
        STRVS   r0, [sp]

        EXIT


message_file_name
        DCB     "Resources:$.Resources.FilterMgr.Messages",0
        ALIGN


        LNK     s.PreFilter
