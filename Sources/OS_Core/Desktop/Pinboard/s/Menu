; s.Menu
;
; Handle the menus.
;
Menu_Pinboard           *       1
Menu_TinyDirs           *       2
Menu_TinyDirsIcon       *       3



        ^ :INDEX:menu_store, workspace
tinydirs_menu_layout    #       28+24*4
rom_tinydirs_menu
tinydirs_menu_layout    Menu    T1
tm_selectall            Item    M11
tm_clearselection       Item    M12
tm_removeselection      Item    M34
tm_openparent           Item    M14
        DCB 0

        ^ :INDEX:menu_store, workspace
tinydirs_icon_menu_layout    #       28+24*2
rom_tinydirs_icon_menu
tinydirs_icon_menu_layout    Menu    T2
tim_info                Item    M21, tinydirs_menu_layout
tim_quit                Item    M22
        DCB 0


        ^ :INDEX:menu_store, workspace

pinboard_menu_layout    #       28+24*9
rom_pinboard_menu
pinboard_menu_layout    Menu    TaskID
pm_info                 Item    M31, tinydirs_menu_layout
pm_tidy                 Item    M32
pm_grid                 Item    M33
pm_remove               Item    M34
pm_select_all           Item    M35
pm_clear_selection      Item    M36
pm_backdrop             Item    M37, backdrop_menu_layout
pm_nobackdrop           Item    M38
pm_save                 Item    M39
        DCB 0

backdrop_menu_layout    #       28+24*3
rom_backdrop_menu
backdrop_menu_layout    Menu    T4
bm_scaled               Item    M41
bm_tiled                Item    M42
bm_centred              Item    M43
        DCB 0



        ALIGN


create_menu          ROUT

; r1 = window
; r2 = icon
; r10 = x
; r11 = y

        Debug   pi,"Create menu w,i",r1,r2

        Push    "r1-r11"
        BL      IntMenuDeleted
        Pull    "r1-r11"

        LDR     r14,backdrop_handle
        TEQ     r1,r14
        BEQ     create_pinboard_menu
        CMP     r1,#-2
        Pull    "PC",NE

 [ iconise_to_iconbar
        LDR     lr, Pinboard_options
        TST     lr, #PinboardOption_IconiseToIconBar
        BEQ     %FT00

        Push    "r6,r7"
        MOV     r6, r2
        BL      find_iconized
        TEQ     r7, #0
        Pull    "r6,r7"
        Pull    "PC",NE
00
 ]

create_iconbar_menu      ROUT

        LDR     r14,TinyDirs_Handle
        CMP     r2,r14
        BEQ     TinyDirsIcon_Menu



        LDR     r14,TinyDirs_Selected
        CMP     r14,#0
        BNE     %FT01

        Debug   pi,"No selected icons, do soft selection"

        STR     r1,soft_selection_window
        STR     r2,soft_selection_icon

        MOV     r14,r1
        ADR     r1,dataarea
        STR     r14,[r1]
        STR     r2,[r1,#4]
        MOV     r14,#is_selected
        STR     r14,[r1,#8]
        STR     r14,[r1,#12]
        SWI     XWimp_SetIconState
        Pull    "PC",VS

        MOV     r14,#1
        STR     r14,TinyDirs_Selected

recreate_iconbar_menu
01
        ADR     r0, message_file_block + 4
        ADRL    r1, rom_tinydirs_menu
        ADRL    r2, tinydirs_menu_layout
        MOV     r3, #menu_space
        SWI     XMessageTrans_MakeMenus
        Pull    "PC",VS

        LDR     r0,TinyDirs_Selected
        CMP     r0,#1                                   ; Is there more than one selected icon ?
                                                        ; Grey out the 'open parent entry'  if so

        LDR     r14,menu_store+28+tm_openparent*24+8          ;
        ORRNE   r14,r14,#is_shaded
        BICEQ   r14,r14,#is_shaded
        STR     r14,menu_store+28+tm_openparent*24+8

        LDR     r14,menu_store+28+tm_clearselection*24+8          ;
        ORRLT   r14,r14,#is_shaded
        BICGE   r14,r14,#is_shaded
        STR     r14,menu_store+28+tm_clearselection*24+8
        LDR     r14,menu_store+28+tm_removeselection*24+8          ;
        ORRLT   r14,r14,#is_shaded
        BICGE   r14,r14,#is_shaded
        STR     r14,menu_store+28+tm_removeselection*24+8
        BLE     %FT01

        ADR     r0,message_file_block+4                 ; Change to remove selection if more than
        ADRL    r1,remove_selection_token               ; one selected icon.
        ADRL    r2,menu_store+400
        MOV     r3,#menu_space-400
        SWI     XMessageTrans_Lookup
        Pull    "PC",VS


        MOV     r1,#12
        ADD     r1, r1, r3, LSL #4
        LDR     r4, menu_store+16
        CMP     r1, r4
        STRGT   r1, menu_store+16

        DebugS  pi,"String is ",r2
        CMP     r2,#0
        STR     r2,menu_store+28+tm_removeselection*24+12
        ADD     r3,r3,#1
        STR     r3,menu_store+28+tm_removeselection*24+20
        MOV     r2,#0
        STR     r2,menu_store+28+tm_removeselection*24+16

        LDR     r14,menu_store+28+tm_removeselection*24+8
        ORR     r14,r14,#if_indirected
        STR     r14,menu_store+28+tm_removeselection*24+8



01
        ADR     r1,menu_store
        SUB     r2,r10,#144
        MOV     r3,#96+4*44
        SWI     XWimp_CreateMenu

        MOV     r14,#Menu_TinyDirs
        STR     r14,CurrentMenu

        Pull    "PC"


TinyDirsIcon_Menu       ROUT

        ADR     r0, message_file_block + 4
        ADRL    r1, rom_tinydirs_icon_menu
        ADRL    r2, tinydirs_icon_menu_layout
        MOV     r3, #menu_space
        SWI     XMessageTrans_MakeMenus
        Pull    "PC",VS

        LDR     r14,info_dbox_handle                    ; Point info entry at info box.
        STR     r14,menu_store+28+tim_info*24+4


        ADR     r1,menu_store
        SUB     r2,r10,#64
        MOV     r3,#96+2*44
        SWI     XWimp_CreateMenu

        MOV     r14,#Menu_TinyDirsIcon
        STR     r14,CurrentMenu

        Pull    "PC"

create_pinboard_menu ROUT

        Debug   pi,"Create pinboard menu"

        CMP     r2,#-1
        BEQ     %FT01

        Debug   pi,"Menu on an icon"

        MOV     R6,R2
        BL      find_iconized
        CMP     r7,#0
        BEQ     %FT01

        MOV     r0,#&79                 ; If an iconized window check for Shift to produce app. menu.
        MOV     r1,#&80
        SWI     XOS_Byte
        CMP     r1,#&FF
        BEQ     app_menu

01
        LDR     r14,Pinboard_Selected
        CMP     r14,#0
        BNE     %FT01

        Push    "r1,r2"
        BL      FindIcon
        Pull    "r1,r2"
        BNE     %FT01

        Debug   pi,"No selected icons, do soft selection"

        STR     r1,soft_selection_window
        STR     r2,soft_selection_icon

        MOV     r14,r1
        ADR     r1,dataarea
        STR     r14,[r1]
        STR     r2,[r1,#4]
        MOV     r14,#is_selected
        STR     r14,[r1,#8]
        STR     r14,[r1,#12]
        SWI     XWimp_SetIconState
        Pull    "PC",VS

        MOV     r14,#1
        STR     r14,Pinboard_Selected

recreate_pinboard_menu
01
        ADR     r0, message_file_block + 4
        ADRL    r1, rom_backdrop_menu
        ADRL    r2, backdrop_menu_layout
        MOV     r3, #menu_space
        SWI     XMessageTrans_MakeMenus
        Pull    "PC",VS

        ADR     r0, message_file_block + 4
        ADRL    r1, rom_pinboard_menu
        ADR     r2, menu_store
        MOV     r3, #menu_space
        SWI     XMessageTrans_MakeMenus
        Pull    "PC",VS

        LDR     r14,info_dbox_handle                    ; Point info entry at info box.
        STR     r14,menu_store+28+pm_info*24+4

        LDR     r14,saveas_handle                       ; Point save entry at save box.
        STR     r14,menu_store+28+pm_save*24+4

        LDR     r14,Pinboard_Icons
        TEQ     r14,#0                                  ; Are there any icons on the pinboard ?

        LDR     r14,menu_store+28+pm_tidy*24+8          ; Shade 'Tidy' if no icons.
        ORREQ   r14,r14,#is_shaded
        BICNE   r14,r14,#is_shaded
        STR     r14,menu_store+28+pm_tidy*24+8

        LDR     r14,menu_store+28+pm_select_all*24+8    ; Shade 'Select all' if no icons.
        ORREQ   r14,r14,#is_shaded
        BICNE   r14,r14,#is_shaded
        STR     r14,menu_store+28+pm_select_all*24+8

        LDR     r0,Pinboard_Selected
        TEQ     r0,#0                                   ; Are there any selected icons on the pinboard ?

        LDR     r14,menu_store+28+pm_clear_selection*24+8       ; Shade 'Clear Selection' if no selected icons.
        ORREQ   r14,r14,#is_shaded
        BICNE   r14,r14,#is_shaded
        STR     r14,menu_store+28+pm_clear_selection*24+8

        LDR     r14,menu_store+28+pm_remove*24+8        ; Shade 'Remove icon' if no selected icons.
        ORREQ   r14,r14,#is_shaded
        BICNE   r14,r14,#is_shaded
        STR     r14,menu_store+28+pm_remove*24+8

        CMP     r0,#1                                   ; Is there more than one selected icon ?
        BLE     %FT01

        ADR     r0,message_file_block+4                 ; Change to remove selection if more than
        ADR     r1,remove_selection_token               ; one selected icon.
        ADRL    r2,menu_store+400
        MOV     r3,#menu_space-400
        SWI     XMessageTrans_Lookup
        Pull    "PC",VS


        MOV     r1,#12
        ADD     r1, r1, r3, LSL #4
        LDR     r4, menu_store+16
        CMP     r1, r4
        STRGT   r1, menu_store+16

        DebugS  pi,"String is ",r2
        CMP     r2,#0
        STR     r2,menu_store+28+pm_remove*24+12
        ADD     r3,r3,#1
        STR     r3,menu_store+28+pm_remove*24+20
        MOV     r2,#0
        STR     r2,menu_store+28+pm_remove*24+16

        LDR     r14,menu_store+28+pm_remove*24+8
        ORR     r14,r14,#if_indirected
        STR     r14,menu_store+28+pm_remove*24+8


01
        LDR     r14,menu_store+28+pm_backdrop*24+8      ; Shade 'Make Backdrop' if not 1 icon selected.
        ORRNE   r14,r14,#is_shaded
        BICEQ   r14,r14,#is_shaded
        STR     r14,menu_store+28+pm_backdrop*24+8

        BNE     %FT03

        LDR     r0,backdrop_handle
        ADR     r1,dataarea
        MOV     r2,#is_selected
        MOV     r3,#is_selected
        SWI     XWimp_WhichIcon
        Pull    "PC",VS


        LDR     r2,[r1]
        LDR     r1,backdrop_handle
        Debug   pi,"Find icon ",r1,r2
        BL      FindIcon                                ; r2 -> Icon block or 0 if not found.
        BNE     %FT01

        Debug   pi,"Icon found."

        LDR     r2,[r2,#ic_filetype]
        LDR     r14,=&ff9
        TEQ     r2,r14
        BEQ     %FT03

01
        LDR     r14,menu_store+28+pm_backdrop*24+8      ; Shade 'Make Backdrop' if selected icon is not a sprite.
        ORR     r14,r14,#is_shaded
        STR     r14,menu_store+28+pm_backdrop*24+8

03
        LDR     r0,backdrop_options
        TST     r0,#bd_OptionActive
        LDR     r14,menu_store+28+pm_nobackdrop*24+8    ; Shade 'Remove backdrop' if no backdrop.
        ORREQ   r14,r14,#is_shaded
        BICNE   r14,r14,#is_shaded
        STR     r14,menu_store+28+pm_nobackdrop*24+8

        LDR     r0,Pinboard_options
        TST     r0,#PinboardOption_Grid
        LDR     r14,menu_store+28+pm_grid*24+0    ; Tick 'Grid' if grid enabled.
        ORRNE   r14,r14,#1
        BICEQ   r14,r14,#1
        STR     r14,menu_store+28+pm_grid*24+0


        ADR     r1,menu_store
        SUBS    r2,r10,#64
        MOVMI   r2,#0
        MOV     r3,r11
        SWI     XWimp_CreateMenu

        MOV     r14,#Menu_Pinboard
        STR     r14,CurrentMenu

        Pull    "PC"

; ----------------------------------------------------------------------------------------------------------------------
; app_menu, send menu message to application.
;
app_menu
        ADR     r1,dataarea
        MOV     r2,r10
        MOV     r3,r11
        MOV     r4,#2
        LDR     r5,[r7,#w_window_handle]
        MOV     r6,#-1
        STMIA   r1,{r2-r6}
        MOV     r0,#6      ; fake mouse event
        LDR     r2,[r7,#w_task]
        SWI     XWimp_SendMessage

        Pull    "PC"

remove_selection_token  DCB     "M34s",0
        ALIGN

        LNK     s.MenuSelect
