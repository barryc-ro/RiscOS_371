;----------------------------------------------------------------------------------------------
;                Convert addressing modes
;----------------------------------------------------------------------------------------------

; IF addressmode = LBAFormat THEN EXIT ( already done )

; IF addressmode = PBFormat THEN address = address - 2 seconds of data

; IF addressmode = MSFFormat THEN change address to minute,second,frame

;----------------------------------------------------------------------------------------------


;----------------------------------------------------------------------------------------------
ConvertToLBA ROUT
; on entry:
;          r0 = address mode
;          r1 = address
; on exit:
;          if oVerflow set then r0 -> error block
;          r1 = new address
;
; Errors can occur, eg/ invalid mode number
; When this is called, 'address' should contain the address on the disc
; in whatever format is being used.  'addressmode' should contain the number
; of the type of addressing being used.  Eg LBAFormat, MSFFormat, PBFormat
;
;----------------------------------------------------------------------------------------------

 BIC       r14, r14, #Overflow_Flag
;--------------------------------------------------
; If already in LBA format then exit straight away
;--------------------------------------------------

 TEQ       r0, #LBAFormat
 MOVEQS    pc, r14

 STASH     "r0, r2 - r5, r14"

;--------------------------------------------------
; Check for allowable addressing mode
;--------------------------------------------------
 CMP       r0, #3
 GRAB      "r0, r2 - r5, r14", CS
 addr      r0, InvalidFormat, CS
 ORRCSS    pc, r14, #Overflow_Flag

;--------------------------------------------------
; Branch to routines to convert from each mode to LBA
;--------------------------------------------------
; R0 = address mode
; R1 = address

 TEQ       r0, #1
 BEQ       ChangeFromMSFFormat

;--------------------------------------------------
;  Change from Physical Block TO Logical Block Address
;  Subtract 2 seconds worth of blocks to give LBA
;--------------------------------------------------

ChangeFromPBFormat 

 SUBS      r1, r1, #( MaxNumberOfBlocks + 1 ) * 2

 GRAB      "r0, r2 - r5, r14", LT
 addr      r0, PhysicalBlockError, LT
 ORRLTS    pc, r14, #Overflow_Flag


 GRABS     "r0, r2 - r5, pc"


;----------------------------------------------------------------------------------------------
;    Change Minutes, Seconds, Frame TO LBA
ChangeFromMSFFormat    ; R1 = address, RETURNS R1 = address
;----------------------------------------------------------------------------------------------

; R1 = address

;-----------------------------------------
; R3 = frames
;-----------------------------------------
 MOV       r14, #255
 AND       r3, r1, r14

;-----------------------------------------
; R4 = seconds
;-----------------------------------------
 AND       r4, r14, r1, LSR #8

;-----------------------------------------
; R5 = minutes
;-----------------------------------------

 AND       r5, r14, r1, LSR #16

;-----------------------------------------
; Make sure that seconds are ( 0 - 59 )
;-----------------------------------------

 CMP       r4, #MaxNumberOfSeconds + 1
 GRAB      "r0, r2 - r5, r14", CS
 addr      r0, BadSeconds, CS
 ORRCSS    pc, r14, #Overflow_Flag

;-----------------------------------------
; Make sure that frames are ( 0 - 74 )
;-----------------------------------------

 CMP       r3, #MaxNumberOfBlocks + 1
 GRAB      "r0, r2 - r5, r14", CS
 addr      r0, BadBlocks, CS
 ORRCSS    pc, r14, #Overflow_Flag

;-----------------------------------------
; minutes = minutes * 60 + seconds
;-----------------------------------------

 MOV       r14, r5, LSL #6                     ; R14 = minutes * 64
 SUB       r5, r14, r5, LSL #2                 ; R5 = R14 - ( minutes * 4 )
 ADD       r5, r5, r4

;-----------------------------------------
; minutes = minutes * 75 + blocks
;-----------------------------------------
 MOV       r14, #MaxNumberOfBlocks + 1
 MLA       r5, r14, r5, r3

;-----------------------------------------
; minutes = minutes - 2 seconds
;-----------------------------------------
 SUB       r1, r5, #( MaxNumberOfBlocks + 1 ) * 2 

;-----------------------------------------

 GRABS      "r0, r2 - r5, pc"


;----------------------------------------------------------------------------------------------
ConvertToMSF ROUT
;                             Convert from LBA or PB to MSF format
; IF addressmode = MSFFormat THEN EXIT ( already done )
;
; IF addressmode = LBAFormat THEN ...
;
;
; on entry:
;          r0 = address mode
;          r1 = address
;
; on exit:
;          if oVerflow clear THEN r1 = new address ELSE r0->error block
;
; flags preserved
;----------------------------------------------------------------------------------------------

 BIC        r14, r14, #Overflow_Flag

;-----------------------------------------
; If already in MSF format then exit straight away
;-----------------------------------------

 TEQ        r0, #MSFFormat
 MOVEQS     pc, r14

 STASH      "r0, r2 - r5, r14"


;-----------------------------------------
; Check for allowable addressing mode
;-----------------------------------------

 CMP        r0, #3
 GRAB       "r0, r2 - r5, r14", CS
 addr       r0, InvalidFormat, CS
 ORRCSS     pc, r14, #Overflow_Flag

;-----------------------------------------
; Branch to routines to convert from each mode to MSF
;-----------------------------------------
; r0 = address mode
; r1 = address

 TEQ        r0, #0
 BEQ        ConvertLBAtoMSF

;-----------------------------------------
ConvertPBtoMSF
; R1 = address
;-----------------------------------------
; address = address - 150 ( error if < 0 )

 SUBS       r1, r1, #( MaxNumberOfBlocks + 1 ) * 2
 GRAB       "r0, r2 - r5, r14", LT
 addr       r0, BadSeconds, LT
 ORRLTS     pc, r14, #Overflow_Flag

;-----------------------------------------
ConvertLBAtoMSF
; R1 = address
;-----------------------------------------
; R3 = Frames
; R4 = seconds
; R5 = minutes


;-----------------------------------------
; Frame = address MOD ( MaxNumberOfBlocks + 1 )
; R3 = R3 MOD ( MaxNumberOfBlocks + 1 )
;-----------------------------------------


;-----------------------------------------
; Seconds = ( address DIV 75 ) MOD 60
; R4
;-----------------------------------------

 MOV        r3, r1                                    ; r2 = address DIV 75
                                                      ;
 DivRem     r2, r3, #MaxNumberOfBlocks + 1, r14       ; r3 = address MOD 75

 DivRem     r4, r2, #MaxNumberOfSeconds + 1, r14      ; r4 = r2 DIV 60
                                                      ;
                                                      ; r2 = r2 MOD 60

; R4 = minutes
; R3 = frames
; R2 = seconds

;-----------------------------------------
; Push results together into one word
; Minutes << 16 + Seconds << 8 + Frames
;-----------------------------------------
 ORR        r3, r3, r2, LSL #8
 ORR        r1, r3, r4, LSL #16

 GRABS      "r0, r2 - r5, pc"


;----------------------------------------------------------------------------------------------
;----------------------------------------------------------------------------------------------
;----------------------------------------------------------------------------------------------
;----------------------------------------------------------------------------------------------
;----------------------------------------------------------------------------------------------

 END
