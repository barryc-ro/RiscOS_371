; DriverCode.s




;     NEVER CHANGE R7 or R11









; This carries out the CD_ SWI commands for the Chinon 431 drive














 




;-----------------------------------------------------------------------------------------------
driver_handler_code ROUT
;
; on entry:
;          r0  - r6= Whatever values the various CD_ SWIs use
;          r7 -> control block
;                control block + 0   = SCSI device id (0-7)
;                control block + 4   = SCSI card number (0-3 usually 0)
;                control block + 8   = Logical Unit Number (0-7 usually 0)
;                control block + 12  = Drive type for this module
;          r8  = full drive type
;          r11 = the number of the CD_ SWI, ie/ 0 = CD_ReadData, 1 = CD_SeekTo etc;
;          r12 -> workspace for this module
;          r13 -> a full descending stack, approx. 1024 bytes
;          r14 = address to return to
; on exit:
;          r0 must be preserved or be return value for the particular SWI
;          r1 - r12 & r14 corrupted
;-----------------------------------------------------------------------------------------------

;--------------------
; Special tag
;--------------------
 DCD       &EE50EE50

;--------------------
; Code
;--------------------

 STASH     "r0, r14"

;----------------------------------------------------------
; Branch to the correct piece of code to handle the command
; The two top bits of R11 (SWIN) are used to indicate the
; number of retries
;----------------------------------------------------------

 BIC       r14, SWIN, #3:SHL:30
 ADR       r10, jump_table_start
 LDR       r14, [ r10, r14, LSL #1 ]
 MOV       r14, r14, LSL #16
 ADD       pc, pc, r14, LSR #16


jump_table_start
 DCW (notsupported    - jump_table_start - 4)      ;  0   OK  ReadData
 DCW (notsupported    - jump_table_start - 4)      ;  1   OK  SeekTo
 DCW (notsupported    - jump_table_start - 4)      ;  2   OK  DriveStatus
 DCW (notsupported    - jump_table_start - 4)      ;  3   OK  DriveReady
 DCW (GetParameters   - jump_table_start - 4)      ;  4   OK
 DCW (SetParameters   - jump_table_start - 4)      ;  5   OK
 DCW (OpenDrawer      - jump_table_start - 4)      ;  6   OK  
 DCW (EjectButton     - jump_table_start - 4)      ;  7   OK
 DCW (EnquireAddress  - jump_table_start - 4)      ;  8   OK
 DCW (EnquireDataMode - jump_table_start - 4)      ;  9   OK
 DCW (PlayAudio       - jump_table_start - 4)      ; 10   OK
 DCW (PlayTrack       - jump_table_start - 4)      ; 11   OK
 DCW (AudioPause      - jump_table_start - 4)      ; 12   OK
 DCW (EnquireTrack    - jump_table_start - 4)      ; 13   OK
 DCW (ReadSubChannel  - jump_table_start - 4)      ; 14   OK
 DCW (notsupported    - jump_table_start - 4)      ; 15   OK  CheckDrive
 DCW (DiscChanged     - jump_table_start - 4)      ; 16   OK
 DCW (StopDisc        - jump_table_start - 4)      ; 17   OK  
 DCW (notsupported    - jump_table_start - 4)      ; 18   OK  DiscUsed
 DCW (AudioStatus     - jump_table_start - 4)      ; 19   OK
 DCW (notsupported    - jump_table_start - 4)      ; 20   OK  Inquiry
 DCW (DiscHasChanged  - jump_table_start - 4)      ; 21   OK
 DCW (notsupported    - jump_table_start - 4)      ; 22   OK  Control
 DCW (Supported       - jump_table_start - 4)      ; 23   OK
 DCW (notsupported    - jump_table_start - 4)      ; 24   OK  Prefetch
 DCW (notsupported    - jump_table_start - 4)      ; 25   OK  Reset
 DCW (notsupported    - jump_table_start - 4)      ; 26   OK  CloseDrawer
 DCW (IsDrawerLocked  - jump_table_start - 4)      ; 27   OK
 DCW (notsupported    - jump_table_start - 4)      ; 28   OK  AudioControl
 DCW (notsupported    - jump_table_start - 4)      ; 29   OK  AudioLevel
 DCW (Identify        - jump_table_start - 4)      ; 30   OK  should identify CDS-431

 ALIGN

notsupported
 ADR       r0, not_supported_error
 B         error_handler_lookup

;-----------------------------------------------------------------------------------------------
;ReadData ROUT
;
; on entry:
;          r0 =   addressing mode
;          r1 =   block number
;          r2 =   number of blocks
;          r3 ->  where to put data
;          r4 =   number of bytes from each block wanted
;          r7 ->  control block
;          r12 -> my workspace
;          r13 -> full descending stack
; on exit:
;          if error then r0-> error block, else all regs preserved
;
;-----------------------------------------------------------------------------------------------

; This is never called - the CDFSDriver internal routine is used instead

;-----------------------------------------------------------------------------------------------
;SeekTo ROUT
;
; on entry:
;          
; on exit:
;          if error then r0-> error block, else all regs preserved
;
;-----------------------------------------------------------------------------------------------

; This is never called - the CDFSDriver internal routine is used instead

;-----------------------------------------------------------------------------------------------
;DriveStatus ROUT
;
; on entry:
;          r7 -> control block
; on exit:
;          r0 = status { 1=OK, 2=BUSY, 4=NOTREADY, 8=UNAVAILABLE }
;-----------------------------------------------------------------------------------------------

; This is never called - the CDFSDriver internal routine is used instead

;-----------------------------------------------------------------------------------------------
;DriveReady ROUT
;
; on entry:
;          r7 -> control block
; on exit:
;          r0 = 0 if drive is OK, else r0 = 1
;
;-----------------------------------------------------------------------------------------------

; This is never called - the CDFSDriver internal routine is used instead

;-----------------------------------------------------------------------------------------------
GetParameters ROUT
;
; on entry:
;          r0 -> parameter block
;          r7 -> control block
; on exit:
;          usual error stuff
;-----------------------------------------------------------------------------------------------

 MOV       r6, #0

SetParameters_jumps_in_here
EnquireDataMode_jumps_in_here

 STASH     r0

;--------------------------------
; SCSI Mode Sense command
;--------------------------------

 BL        space_saver

 ORR       r0, r0, #escapepolloff + readdata   ; prevent 'escape key'

 MOV       r3, #&1a
 ORR       r3, r3, r10, LSL #8+5
 MOV       r4, #12
 STMIA     r2, { r3, r4 }

 ADR       r3, buffer + 16

 SWI       XSCSI_Op

 GRAB      r8

 BVS       error_handler


 TEQ       r6, #0
 BNE       SetParameters_jumps_back



; R0 -> storage area
; R7 -> control block

 LDRB      r1, buffer + 16 + 11
 LDRB      r4, buffer + 16 + 10
 ORR       r1, r1, r4, LSL #8

 TEQ       r1, #mode1datasize
 MOVNE     r5, #2
 MOVEQ     r5, #1

 MOV       r1, #5
 MOV       r2, #1
 MOV       r6, #0
 STMIA     r8, { r1, r2, r5, r6 }



 GRABS     "r0, pc"

;-----------------------------------------------------------------------------------------------
SetParameters ROUT
;
; on entry:
;          r0 -> parameter block
;          r7 -> control block
; on exit:
;          usual error stuff
;-----------------------------------------------------------------------------------------------

;-----------------------
; Get parameters
;-----------------------
 MOV       r6, #1
 B         SetParameters_jumps_in_here

;-----------------------

SetParameters_jumps_back

; r8 -> parameter block


;-----------------------------------------------
; Put in block size ( only thing Chinon can do )
;-----------------------------------------------
 LDR       r14, [ r8, #8 ]

 TEQ       r14, #1
 MOV       r14, #mode1datasize
 ADDNE     r14, r14, #(mode2datasize - mode1datasize)

 STRB      r14, buffer + 16 + 11
 MOV       r14, r14, LSR #8
 STRB      r14, buffer + 16 + 10

;--------------------------------
; SCSI Mode Select command
;--------------------------------

 BL        space_saver

 ORR       r0, r0, #escapepolloff + writedata  ; prevent 'escape key'

 MOV       r3, #&15
 ORR       r3, r3, r10, LSL #8+5
 MOV       r4, #12
 STMIA     r2, { r3, r4 }

 ADR       r3, buffer + 16

 STRB      r5, buffer + 16 + 0

 SWI       XSCSI_Op
 BVS       error_handler

 TEQ       r6, #2
 BEQ       EnquireDataMode2_jumps_back

 TEQ       r6, #3
 BEQ       EnquireDataMode3_jumps_back

 GRABS     "r0, pc"

;-----------------------------------------------------------------------------------------------
OpenDrawer ROUT
;
; on entry:
;          r7 -> control block
; on exit:
;          usual error stuff
;-----------------------------------------------------------------------------------------------

;-------------------
; SCSI Eject command
;-------------------

 BL        space_saver

 MOV       r1, #&c0
 ORR       r1, r1, r10, LSL #8+5
 STMIA     r2, { r1, r3, r5 }

 MOV       r1, #10

 MOV       r4, #0

 SWI       XSCSI_Op

 GRABS     "r0, pc", VC

;---------------------------------------------------
; If the error is Illegal Request then drawer locked
;---------------------------------------------------

 LDR       r1, [ r0 ]
 LDR       r2, =IllegalRequest
 TEQ       r2, r1

 addr      r0, drawer_locked_error, EQ
 BEQ       error_handler_lookup
 B         error_handler

;-----------------------------------------------------------------------------------------------
EjectButton ROUT
;
; on entry:
;          r0 = 0 to allow eject, 1 to prevent
;          r7 -> control block
; on exit:
;          usual error stuff
;-----------------------------------------------------------------------------------------------

;-----------------------
; Check for valid switch
;-----------------------
 CMP       r0, #2
 BCS       invalidparameter

;-----------------------

 MOV       r6, r0

;--------------------------------
; SCSI Prevent/allow command
;--------------------------------

 BL        space_saver

 MOV       r4, #&1e
 ORR       r4, r4, r10, LSL #8+5
 STMIA     r2, { r4, r6 }

 MOV       r4, #8

 SWI       XSCSI_Op

 BVS       error_handler

 MOV       r0, r6
 BL        SetDrawerStatus

 GRABS     "r0, pc"

;-----------------------------------------------------------------------------------------------
EnquireAddress ROUT
;
; on entry:
;          r0 = addressing mode for returned data
;          r7 -> control block
; on exit:
;          usual error stuff
;          r0 = current laser position
;-----------------------------------------------------------------------------------------------

 MOV       r6, r0

;--------------------------------
; SCSI Read sub-channel command
;--------------------------------

 BL        space_saver

 ORR       r0, r0, #escapepolloff + readdata   ; prevent 'escape key'

 MOV       r1, #&42
 ORR       r1, r1, #(2:SHL:8)
 ORR       r1, r1, #(64:SHL:16)
 MOV       r4, #48
 ORR       r1, r1, r10, LSL #8+5
 STMIA     r2, { r1, r3, r4 }

 MOV       r1, #10

 ADR       r3, buffer + 16

 SWI       XSCSI_Op

 BVS       error_handler




 LDRB      r3, buffer + 16 + 11      ;   Frame
 LDRB      r1, buffer + 16 + 10      ;   Second
 LDRB      r2, buffer + 16 + 9       ;   Minute
 ORR       r2, r1, r2, LSL #8
 ORR       r1, r3, r2, LSL #8        ; R1 = MSF address

;---------------------------------------
; If caller wants return in LBA then ...
;---------------------------------------
 TEQ       r6, #MSFFormat

 MOVNE     r0, #MSFFormat

 SWINE     XCD_ConvertToLBA
 BVS       error_handler

;---------------------------------------
; If caller wants return in PB format
;---------------------------------------

 TEQ       r6, #PBFormat

 ADDEQ     r0, r1, #( MaxNumberOfBlocks + 1 ) * 2

 MOVNE     r0, r1

 GRABS     "r1, pc"



;-----------------------------------------------------------------------------------------------
PlayAudio ROUT
;
; on entry:
;          r0 = addressing mode
;          r1 = start address
;          r2 = end address
;          r7 -> control block
; on exit:
;          usual error stuff
;-----------------------------------------------------------------------------------------------

 SWI       XCD_ConvertToMSF

 MOVVC     r5, r1

;-------------------------------------------
; Store MSF start address in parameter block
;-------------------------------------------

 MOVVC     r1, r2

 SWIVC     XCD_ConvertToMSF

;-------------------------------
; Store end of play ( in M S F )
;-------------------------------

;-------------------------------
; Reverse byte order of
; start and end MSF
;-------------------------------
 MOVVC     r14, #&ff00
 ORRVC     r14, r14, r14, LSL #16
 ANDVC     r8, r14, r5, ROR #8
 ANDVC     r5, r14, r5
 ORRVC     r5, r8, r5, ROR #24

 ANDVC     r8, r14, r1, ROR #8
 ANDVC     r6, r14, r1
 ORRVC     r6, r8, r6, ROR #24

;--------------------------------
; SCSI PlayMSF command
;--------------------------------
; r5 = FSM0  start
; r6 = FSM0  end

 STASH     r5, VC
 BLVC      space_saver
 GRAB      r5, VC

 MOVVC     r3, #&47
 ORRVC     r3, r3, r10, LSL #8+5
 ORRVC     r3, r3, r5, LSL #16
 MOVVC     r4, r5, LSR #16
 ORRVC     r4, r4, r6, LSL #8
 MOVVC     r5, r6, LSR #24
 STMVCIA   r2, { r3, r4, r5 }

 MOVVC     r1, #10

 MOVVC     r3, #0

 MOVVC     r4, #0

 MOVVC     r5, #0

 SWIVC     XSCSI_Op

;------------------------------------
; On DataProtect must be playing data
;------------------------------------
 GRABS     "r0, pc", VC

 LDR        r1, [ r0 ]
 LDR        r2, =DataProtect
 TEQ        r1, r2
 BEQ        notaudiotrack

 B          error_handler

;-----------------------------------------------------------------------------------------------
PlayTrack ROUT
;
; on entry:
;          r0 = track number
;          r1 = &ff end of disc, &fe play to end of track
;          r7 -> control block
; on exit:
;          usual error stuff
;-----------------------------------------------------------------------------------------------

 STMIA     r12, { r0, r1 }

;---------------------
; 1. read track info
;---------------------

 LDMIA     r7, { r4, r5, r6 }
 ADR       r7, temp_control_block
 STMIA     r7, { r4, r5, r6, r8 }
 ADR       r1, buffer + 100

 SWI       XCD_EnquireTrack
 BVS       error_handler

;------------------------
; 2. read more track info
;------------------------

; R4 = Number of tracks on disc (in BCD)
 LDRB      r3, buffer + 16 + 3
 ConvertBCDToNormal AL, r3, r1, r4

;------------------------------------------------
; Read either next track address, or last track ?
;------------------------------------------------
; Check if playing to end of disc
; if playing last track, convert to playing to end of disc
 LDMIA     r12, { r0, r1 }
 TEQ       r1, #&FF
 TEQNE     r0, r3
 MOVEQ     r0, #&AA
 MOVEQ     r1, #&FF
 ADDNE     r0, r0, #1

 ADR       r1, buffer + 128
 SWI       XCD_EnquireTrack

;---------------------
; 3. play audio a->b
;---------------------
 MOVVC     r0, #LBAFormat
 LDRVC     r1, buffer + 100
 ADDVC     r1, r1, #150
 LDRVC     r2, buffer + 128

 SWIVC     XCD_PlayAudio
 BVS       error_handler

 GRABS     "r0, pc"

;-----------------------------------------------------------------------------------------------
AudioPause ROUT
;
; on entry:
;          r0 = 0 to resume, 1 to pause
;          r7 -> control block
; on exit:
;          usual error stuff
;-----------------------------------------------------------------------------------------------

 STR       r0, [ r12 ]

;----------------------------
; Make sure that r0 = 0 to 1
;----------------------------
 CMP       r0, #2
 BCS       invalidparameter

;------------------------------------------------
; 1. ReadSubChannel
;------------------------------------------------

 LDMIA     r7, { r1, r2, r3 }
 ADR       r7, temp_control_block
 STMIA     r7, { r1, r2, r3, r8 }

 ADR       r1, buffer + 128

 MOV       r0, #64
 SWI       XCD_ReadSubChannel
 BVS       error_handler

;------------------------------------------------
; 2. This uses information from the Chinon read
; sub-channel, not the general SWI
;------------------------------------------------

 LDR       r0, [ r12 ]
 BL        SetPausedStatus

; If playing audio, and want to pause off, THEN ignore

 LDRB      r2, buffer + 16 + 1
 TEQ       r2, #&11
 TEQEQ     r0, #0
 GRABS     "r0, pc", EQ

; If not playing audio, and pause on THEN ignore

 TEQ       r2, #&11
 TEQNE     r0, #0
 GRABS     "r0, pc", NE

; If playing audio, and want to pause THEN pause audio play

 TEQ       r2, #&11
 TEQEQ     r0, #1
 BEQ       %FT01       ; Pause audio

; If not playing audio, and pause off THEN play audio from current position


; Find last position on disc

 MOV       r0, #LBAFormat
 ADR       r1, buffer + 40
 SWI       XCD_DiscUsed

;------------------------------------------------
; 3. Play audio to the end of the disc
;------------------------------------------------

 MOVVC     r0, #LBAFormat
 LDRVC     r1, buffer + 128 + 4
 ADDVC     r1, r1, #150
 LDRVC     r2, buffer + 40
 SWIVC     XCD_PlayAudio
 BVS       error_handler

 GRABS     "r0, pc"


;---------------------------------
01;---- Pause audio (Stop disc )  !!!!!!! WORKS !!!!!!!
;---------------------------------

 SWI      XCD_StopDisc
 BVS      error_handler

 GRABS     "r0, pc"

;-----------------------------------------------------------------------------------------------
;CheckDrive ROUT
;
; on entry:
;          r7 -> control block
; on exit:
;          usual error stuff
;          r0 = drive status bits
;-----------------------------------------------------------------------------------------------

; This is never called - the CDFSDriver internal routine is used instead

;-----------------------------------------------------------------------------------------------
DiscChanged ROUT
;
; on entry:
;          r7 -> control block
; on exit:
;          usual error stuff
;          r0 = 0 if not changed, 1 if changed
;-----------------------------------------------------------------------------------------------

 MOV       r6, #0

drawer_status_jumps_in_here

;--------------------------------
; SCSI TestUnitReady command
;--------------------------------

 BL        space_saver

 MOV       r4, r10, LSL #8+5
 STMIA     r2, { r4, r5 }

 MOV       r4, #0

 SWI       XSCSI_Op


; Error
 BVS       error_handler

; No error
 TEQ       r6, #0
 BLEQ      HasDiscChanged
 BLNE      GetDrawerStatus

 GRABS     "r1, pc"

;-----------------------------------------------------------------------------------------------
StopDisc ROUT
;
; on entry:
;          r7 -> control block
; on exit:
;          usual error stuff
;-----------------------------------------------------------------------------------------------

;--------------------------------
; SCSI AudioStop command
;--------------------------------

 BL        space_saver

 MOV       r3, #&c6
 ORR       r3, r3, r10, LSL #8+5
 MOV       r4, #0
 STMIA     r2, { r3, r4, r5 }

 MOV       r1, #10

 MOV       r3, #0

 SWI       XSCSI_Op

 BVS       error_handler

 GRABS     "r0, pc"

;-----------------------------------------------------------------------------------------------
;DiscUsed ROUT
;
; on entry:
;          r0 =  addressing mode that the disc length should be returned in
;          r1 -> storage area to put data in
;          r7 -> control block
; on exit:
;
;-----------------------------------------------------------------------------------------------

; This is never called - the CDFSDriver internal routine is used instead

;-----------------------------------------------------------------------------------------------
AudioStatus ROUT
;
; on entry:
;          r7 -> control block
;          r8 =  full drive type
; on exit:
;          usual error stuff
;          r0 = 0 playing audio, 1 paused, 2 muted, 3 play completed, 4 error, 5 not requested
;-----------------------------------------------------------------------------------------------

; Fix the full drive type so that I can use XCD_ SWIs
 LDMIA     r7, { r4, r5, r6 }
 ADR       r7, temp_control_block
 STMIA     r7, { r4, r5, r6, r8 }

 MOV       r0, #64
 ADR       r1, buffer + 128
 SWI       XCD_ReadSubChannel
 BVS       error_handler

; RETURNS R0=0/1 not/paused
 BL        GetPausedStatus

 MOV       r4, r0

 LDRB      r1, buffer + 16 + 1

;--- Default state = audio play not requested

 MOV       r0, #NOPLAY

;--- Playing audio ?

 TEQ       r1, #&11
 MOVEQ     r0, #PLAYINGAUDIO

;--- Paused ?

 TEQ       r1, #&15
 TEQEQ     r4, #1        ; and I know it's paused ?
 MOVEQ     r0, #PAUSED

 GRABS     "r1, pc"

;-----------------------------------------------------------------------------------------------
;Inquiry ROUT
;
; on entry:
;          r0 -> place to put the inquiry data
;          r7 -> control block
; on exit:
; 
;-----------------------------------------------------------------------------------------------

; This is never called - the CDFSDriver internal routine is used instead

;-----------------------------------------------------------------------------------------------
DiscHasChanged ROUT
;
; on entry:
;          r7 -> control block
; on exit:
;          usual error stuff
;-----------------------------------------------------------------------------------------------

 BL        SetDiscChanged

 GRABS     "r0, pc"

;-----------------------------------------------------------------------------------------------
;Control ROUT
;
; on entry:
;          r0 =  0, 1 or 2 to set SCSI error response
;          r7 -> control block
; on exit:
;          usual error stuff
;-----------------------------------------------------------------------------------------------

; This is never called - the CDFSDriver internal routine is used instead

;-----------------------------------------------------------------------------------------------
Supported ROUT
;
; on entry:
;          r7 -> control block
; on exit:
;          usual error stuff
;          r0 = information word with bits set to indicate level of support
;-----------------------------------------------------------------------------------------------
 MOV       r0, #CHINONVERSIONBITS
 GRABS     "r1, pc"

;-----------------------------------------------------------------------------------------------
Prefetch ROUT
;
; on entry:
;          r0 =   addressing mode
;          r1 =   block number
;          r7 ->  control block
; on exit:
;          if error then r0-> error block, else all regs preserved
;
;-----------------------------------------------------------------------------------------------

; this is performed by CDFSDriver internal routines

;-----------------------------------------------------------------------------------------------
;Reset ROUT
;
; on entry:
;          r7 ->  control block
; on exit:
;          if error then r0-> error block, else all regs preserved
;
;-----------------------------------------------------------------------------------------------

; this is performed by CDFSDriver internal routines

;-----------------------------------------------------------------------------------------------
;CloseDrawer ROUT
;
; on entry:
;          r7 ->  control block
; on exit:
;          if error then r0-> error block, else all regs preserved
;
;-----------------------------------------------------------------------------------------------

; not supported

;-----------------------------------------------------------------------------------------------
IsDrawerLocked ROUT
;
; on entry:
;          r7 ->  control block
; on exit:
;          if error then r0-> error block, else all regs preserved
;          r0 = 0 if unlocked, 1 if locked
;
;-----------------------------------------------------------------------------------------------

 MOV       r6, #1
 B         drawer_status_jumps_in_here

;-----------------------------------------------------------------------------------------------
;AudioControl ROUT
;
; on entry:
;          r7 ->  control block
; on exit:
;          if error then r0-> error block, else all regs preserved
;
;-----------------------------------------------------------------------------------------------

; not supported

;-----------------------------------------------------------------------------------------------
;AudioLevel ROUT
;
; on entry:
;          r7 ->  control block
; on exit:
;          if error then r0-> error block, else all regs preserved
;
;-----------------------------------------------------------------------------------------------

; not supported

;-----------------------------------------------------------------------------------------------
Identify ROUT
; on entry:
;           r0  -> 36 byte SCSI inquiry data or 0
;           r1  -> if r0 =0 THEN this -> an error block ie/ the reason inquiry data failed
;           r7  -> control block, ignore the driver type, ie/ control block + 20
;           r11 =  reason code for CD_Identify
;           r12 -> their workspace
;           r13 -> full descending stack
; on exit:
;           r2 = -1 if not recognized, else internal drive type
;           r0-r1, r3-12 can be corrupted
;-----------------------------------------------------------------------------------------------

;----------------------------
; Is there any inquiry data ?
;----------------------------
 TEQ       r0, #0
 BEQ       not_Chinon

;----------------------------
; Removable media ?
;----------------------------
 LDRB      r1, [ r0 ]
 LDRB      r2, [ r0, #1 ]
 TST       r2, #128

;----------------------------
; Funny LUN ?
;----------------------------
 TEQNE     r1, #&7f
 BEQ       not_Chinon

;---------------------------- 
; Is it a Chinon drive ?
;---------------------------- 
; r1 -> manufacturers name
 MOV       r4, r0

 ADD       r0, r0, #8
 ADR       r1, Chinon_name
 MOV       r2, #?Chinon_name
 BL        CompareStrings

;---------------------------- 
; Check description
;---------------------------- 
 ADDEQ     r0, r4, #16
 ADREQ     r1, Chinon_description
 MOVEQ     r2, #?Chinon_description
 BLEQ      CompareStrings

 MOVEQ     r2, #0

 GRABS     "r0, pc", EQ

;---------------------------- 
not_Chinon
 MOV       r2, #-1
 GRABS     "r0, pc"


;-----------------------------------------------------------------------------------------------
Chinon_name        = "CHINON"
Chinon_description = "CD-ROM CDS-431"
 ALIGN



;-----------------------------------------------------------------------------------------------
EnquireDataMode ROUT
;
; on entry:
;          r0 = addressing mode for block
;          r1 = block
;          r7 -> control block
;          r8 = large drive type
; on exit:
;          usual error stuff
;          r0 = addressing mode (1 for mode 1, 2 for mode 2, 3 for mode 2 form 1)
;-----------------------------------------------------------------------------------------------

;----------------------------------
; Store all of the entry parameters
;----------------------------------
 STMIA     r12, { r0, r1, r7, r8 }

;-----------------
; Change to mode 1
;-----------------
 ADR       r0, mode_1_block
 MOV       r6, #2
 B         EnquireDataMode_jumps_in_here


EnquireDataMode2_jumps_back
;---------------
; Read some data
;---------------
 LDMIA     r12, { r0, r1, r2, r8 }
 LDMIA     r7, { r4, r5, r6 }
 ADR       r7, temp_control_block
 STMIA     r7, { r4, r5, r6, r8 }
 MOV       r2, #1
 ADR       r3, buffer + 128
 MOV       r4, #1
 SWI       XCD_ReadData

;-----------------------------
; No error - so must be mode 1
;-----------------------------
 MOVVC     r0, #1
 GRABS     "r1, pc", VC

;-----------------
; Change to mode 2
;-----------------
 ADR       r0, mode_1_block
 MOV       r6, #3
 B         EnquireDataMode_jumps_in_here

EnquireDataMode3_jumps_back

;-------------------------
; Read some data
;-------------------------
 LDMIA     r12, { r0, r1, r2, r8 }
 LDMIA     r7, { r4, r5, r6 }
 ADR       r7, temp_control_block
 STMIA     r7, { r4, r5, r6, r8 }
 MOV       r2, #1
 ADR       r3, buffer + 128
 MOV       r4, #1
 SWI       XCD_ReadData

;-----------------------------
; No error - so must be mode 2
;-----------------------------
 MOVVC     r0, #2
 GRABS     "r1, pc", VC

;----------------------
; What error occured ?
;----------------------
 LDR       r1, [ r0 ]
 LDR       r2, =&00803400 + 14 ; DATA IN WRONG MODE error
 TEQ       r1, r2
 MOVEQ     r0, #0
 GRABS     "r1, pc", EQ

 B          error_handler

;-----------------------------------------------------------------------------------------------
ReadSubChannel ROUT
;
; on entry:
;          r0 = sub-channel = 64
;          r1 -> storage area
;          r7 -> control block
; on exit:
;          usual error stuff
;-----------------------------------------------------------------------------------------------

;----------------------------
; Make sure valid sub-channel
;----------------------------
 TEQ       r0, #64
 addr      r0, channel_not_supported_error, NE
 BNE       error_handler_lookup

;--------------------------------
; SCSI ReadTOC command
;--------------------------------
; r5 = FSM0  start
; r6 = FSM0  end

 STASH     r1

 BL        space_saver

 MOV       r1, #&42
 ORR       r1, r1, #64:SHL:16
 ORR       r1, r1, #2:SHL:8
 ORR       r1, r1, r10, LSL #8+5
 MOV       r4, #48
 STMIA     r2, { r1, r3, r4 }

 ORR       r0, r0, #escapepolloff + readdata   ; prevent 'escape key'

 MOV       r1, #10

 ADR       r3, buffer + 16

 SWI       XSCSI_Op

 GRAB      r5

 BVS       error_handler


 ADD       r14, r12, #:INDEX:buffer + 16 + 4
 LDMIA     r14, { r0, r1, r2 }

;--- Control bits

 MOV       r14, r0, LSR #10
 AND       r14, r14, #2_11
 STRB      r14, [ r5, #8 ]

;--- Track number

 MOV       r0, r0, LSR #16

; Convert BCD to normal
 AND       r4, r0, #255
 ConvertBCDToNormal AL, r4, r14, r3

 STRB      r4, [ r5, #9 ]

;--- Index number

 MOV       r0, r0, LSR #8
 STRB      r0, [ r5, #10 ]

;--- Change FSM0 TO 0MSF

 BIC       r14, r1, #255:SHL:16   ; R14 = F0M0  R1=FSM0
 EOR       r0, r14, r1            ; R0  = 0S00
 ORR       r14, r0, r14, ROR #16  ; R14 = FSM0
 MOV       r1, r14, LSR #8

;--- Change FSM0 TO 0MSF

 BIC       r14, r2, #255:SHL:16   ; R14 = F0M0  R2=FSM0
 EOR       r0, r14, r2            ; R0  = 0S00
 ORR       r14, r0, r14, ROR #16  ; R14 = FSM0
 MOV       r2, r14, LSR #8

; R1 = absolute address
; R2 = relative

 MOV       r0, #MSFFormat
 SWI       XCD_ConvertToLBA

 MOVVC     r3, r1          ; R3=absolute address in LBA
 MOVVC     r1, r2
 SWIVC     XCD_ConvertToLBA
 BVS       error_handler

 STMIA     r5, { r1, r3 }

 GRABS     "r0, pc"

;-----------------------------------------------------------------------------------------------
EnquireTrack ROUT
;
; on entry:
;          r0 = 0 start/end track, 1 to 99 info on that track
;          r1 -> storage area
;          r7 -> control block
; on exit:
;          usual error stuff
;-----------------------------------------------------------------------------------------------

 TEQ       r0, #&aa
 BEQ       %FT01

;----------------------------
; Make sure that r0 = 1 to 100 ('cause called by XCD_PlayTrack)
;----------------------------
 ADD       r14, r0, #1
 CMP       r14, #102
 BCS       invalidparameter


; Need to convert to binary coded decimal

 ConvertNormalToBCD r0, r2, r14

01

;--------------------------------
; SCSI ReadTOC command
;--------------------------------
; r5 = FSM0  start
; r6 = FSM0  end

 STASH     r0-r1

 STASH     r0
 BL        space_saver
 GRAB      r14

 MOV       r1, #&43
 ORR       r1, r1, #2_10:SHL:8        ; ????
 ORR       r1, r1, r10, LSL #8+5
 MOV       r3, r14, LSL #16
 MOV       r4, #12
 STMIA     r2, { r1, r3, r4 }

 ORR       r0, r0, #escapepolloff + readdata   ; prevent 'escape key'

 MOV       r1, #10

 ADR       r3, buffer + 16

 SWI       XSCSI_Op

 GRAB      "r1, r6"

;-------------------------------------------
; If illegal request then not an audio track
;-------------------------------------------
 TEQVC     pc, #0
 LDRVS     r2, [ r0 ]
 LDRVS     r14, =IllegalRequest
 TEQVS     r2, r14
 BEQ       notaudiotrack

 BVS       error_handler


;-----------------------------------------
; Was info on a specific track requested ?
;-----------------------------------------

; R4 = resulting first word

 TEQ       r1, #0

;---  ( CONTROL BITS )

 LDRNEB    r3, buffer + 16 + 4 + 1
 MOVNE     r3, r3, LSR #2                ;  only certain bits needed
 ANDNE     r3, r3, #3
 STRNEB    r3, [ r6, #4 ]

;--- start/end track or track address

 LDREQB    r3, buffer + 16 + 2
 ConvertBCDToNormal EQ, r3, r4, r14
 LDREQB    r1, buffer + 16 + 3
 ConvertBCDToNormal EQ, r1, r5, r14

 LDRNEB    r3, buffer + 16 + 4 + 7     ; 
 LDRNEB    r1, buffer + 16 + 4 + 6     ; 
 ORR       r1, r3, r1, LSL #8

 LDRNEB    r3, buffer + 16 + 4 + 4     ; 
 ORRNE     r1, r1, r3, LSL #24
 LDRNEB    r3, buffer + 16 + 4 + 5     ; 
 ORRNE     r1, r1, r3, LSL #16

;--- If track address then convert to LBA format


 MOVNE     r0, #MSFFormat
 SWINE     XCD_ConvertToLBA

 STR       r1, [ r6 ]


 GRABS     "r0, pc"


mode_1_block
 DCD 5
 DCD 1
 DCD 1
 DCD 0


mode_2_block
 DCD 5
 DCD 1
 DCD 2
 DCD 0


;-----------------------------------------------------------------------------------------------
; Routine to load default values and save space
;-----------------------------------------------------------------------------------------------
space_saver ROUT
 LDMIA     r7, { r8, r9, r10 }

 ORR       r0, r8, r9,  LSL #3                 ; card number
 ORR       r0, r0, r10, LSL #5                 ; lun
 ORR       r0, r0, #escapepolloff + nodata     ; prevent 'escape key'
 MOV       r1, #6
 ADR       r2, buffer
 MOV       r3, #0
 MOV       r5, #0
 MOV       r8, #1

 MOV       pc, r14

;-----------------------------------------------------------------------------------------------
;-----------------------------------------------------------------------------------------------
;-----------------------------------------------------------------------------------------------
;-----------------------------------------------------------------------------------------------

 END
