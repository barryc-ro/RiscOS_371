; > $.Source.PMF.Key2

; ARTHUR keyboard code (Keyboard table handler)

; Author        Tim Dobson
; Started       15-Apr-87

; ************************************************************
; ***    C h a n g e   L i s t  (better late than never!)  ***
; ************************************************************

; Date       Description
; ----       -----------
; 19-Feb-88  Modified InkeyTran2 so that INKEY(-95) returns state of new key
;             next to left shift key on international keyboards
; 11-Aug-88  Modified A500 keyboard as follows:-
;             LOOKS = ALT; MENU = F0; F10 = F10; keypad # = F11;
;             keypad % = F12; keypad / = F13

        MACRO
        IKT     $A, $B, $C, $D, $E, $F, $G, $H
        IKT2    $A
        IKT2    $B
        IKT2    $C
        IKT2    $D
        IKT2    $E
        IKT2    $F
        IKT2    $G
        IKT2    $H
        MEND

        MACRO
        IKT2    $TE
        LCLA    T
T       SETA    $TE
        [ (T :AND: &FF00) = 0
T       SETA    T :OR: &FF00
        ]
        [ (T :AND: &FF0000) = 0
T       SETA    T :OR: &FF0000
        ]
        [ (T :AND: &FF000000) = 0
T       SETA    T :OR: &FF000000
        ]
        &       T
        MEND


; Special keys

K0ShiftKey      * &0A
K0LooksKey      * &0B
K0ControlKey    * &0C
K0RightMouse    * &0D
K0CentreMouse   * &0E
K0LeftMouse     * &0F
K0TabKey        * &30
K0CapsKey       * &40
K0BreakKey      * &05 ; key marked ESCAPE is BREAK
K0NumPadHash    * &04
K0NumPadPercent * &17
K0NumPadSlash   * &07
K0NumPadStar    * &06
K0NumPad7       * &14
K0NumPad8       * &77
K0NumPad9       * &27
K0NumPadMinus   * &16
K0NumPad4       * &24
K0NumPad5       * &97
K0NumPad6       * &67
K0NumPadPlus    * &87
K0NumPad1       * &64
K0NumPad2       * &37
K0NumPad3       * &57
K0NumPadEnter   * &47
K0NumPad0       * &94
K0NumPadDot     * &44

K1ShiftLeft     * &4C
K1ShiftRight    * &58
K1CtrlLeft      * &3B
K1CtrlRight     * &61
K1AltLeft       * &5E
K1AltRight      * &60
K1RightMouse    * &72
K1CentreMouse   * &71
K1LeftMouse     * &70
K1TabKey        * &26
K1CapsKey       * &5D
K1NumKey        * &22
K1ScrollKey     * &0E
K1BreakKey      * &0F
K1BackTickKey   * &10
K1PoundKey      * &1D
K1NumPadSlash   * &23
K1NumPadStar    * &24
K1NumPadHash    * &25
K1NumPad7       * &37
K1NumPad8       * &38
K1NumPad9       * &39
K1NumPadMinus   * &3A
K1NumPad4       * &48
K1NumPad5       * &49
K1NumPad6       * &4A
K1NumPadPlus    * &4B
K1NumPad1       * &5A
K1NumPad2       * &5B
K1NumPad3       * &5C
K1NumPadEnter   * &67
K1NumPad0       * &65
K1NumPadDot     * &66

; UserKeyWorkSpace allocation

        ^ 0, R12
ShiftCount      # 1
CtrlCount       # 1
MyMouseButtons  # 1
KeyReturn       # 2     ; length byte (1), value byte
KeyNULReturn    # 3     ; length byte (2), NUL, value byte

        ASSERT (:INDEX: @) <= UserKeyWorkSpaceSize

; *****************************************************************************

OldKeyStruct
        &       KeyTran-OldKeyStruct
        &       (KeyTranEnd-KeyTran) :SHR: 2
        &       InkeyTran-OldKeyStruct
        &       ShiftingKeyList-OldKeyStruct
        &       SpecialList-OldKeyStruct
        &       SpecialCodeTable-OldKeyStruct
        &       KeyStructInit-OldKeyStruct
        &       PendingAltCode-OldKeyStruct

ShiftingKeyList
        =       ShiftingKeyListEnd-ShiftingKeyList-1
        =       K0ShiftKey, K0ControlKey, K0LooksKey
        =       K0RightMouse, K0CentreMouse, K0LeftMouse
        =       K0BreakKey
ShiftingKeyListEnd
        ALIGN

SpecialList
        [ Keyboard_Type = "A1A500"
        =       SpecialListEnd-SpecialList-1
        =       K0ShiftKey, K0ControlKey, K0LooksKey
        =       K0RightMouse, K0CentreMouse, K0LeftMouse
        =       K0CapsKey, K0TabKey, K0BreakKey
SpecialListPad
        =       K0NumPadHash, K0NumPadPercent, K0NumPadSlash, K0NumPadStar
        =       K0NumPad7, K0NumPad8, K0NumPad9, K0NumPadMinus
        =       K0NumPad4, K0NumPad5, K0NumPad6, K0NumPadPlus
        =       K0NumPad1, K0NumPad2, K0NumPad3, K0NumPadEnter
        =       K0NumPad0, K0NumPadDot
        ]
SpecialListEnd
        ALIGN

SpecialCodeTable
        [ Keyboard_Type = "A1A500"
        &       ProcessKShift-SpecialCodeTable
        &       ProcessKCtrl-SpecialCodeTable
        &       ProcessKAlt-SpecialCodeTable
        &       ProcessKRight-SpecialCodeTable
        &       ProcessKCentre-SpecialCodeTable
        &       ProcessKLeft-SpecialCodeTable
        &       ProcessKCaps-SpecialCodeTable
        &       ProcessKTab-SpecialCodeTable
        &       ProcessKBreak-SpecialCodeTable
        &       ProcessK0Pad-SpecialCodeTable
        &       ProcessK0Pad-SpecialCodeTable
        &       ProcessK0Pad-SpecialCodeTable
        &       ProcessK0Pad-SpecialCodeTable
        &       ProcessK0Pad-SpecialCodeTable
        &       ProcessK0Pad-SpecialCodeTable
        &       ProcessK0Pad-SpecialCodeTable
        &       ProcessK0Pad-SpecialCodeTable
        &       ProcessK0Pad-SpecialCodeTable
        &       ProcessK0Pad-SpecialCodeTable
        &       ProcessK0Pad-SpecialCodeTable
        &       ProcessK0Pad-SpecialCodeTable
        &       ProcessK0Pad-SpecialCodeTable
        &       ProcessK0Pad-SpecialCodeTable
        &       ProcessK0Pad-SpecialCodeTable
        &       ProcessK0Pad-SpecialCodeTable
        &       ProcessK0Pad-SpecialCodeTable
        &       ProcessK0Pad-SpecialCodeTable
        ]

; *****************************************************************************

NewKeyStruct
        &       KeyTran2-NewKeyStruct
        &       (KeyTran2End-KeyTran2) :SHR: 2
        &       InkeyTran2-NewKeyStruct
        &       NewShiftingKeyList-NewKeyStruct
        &       NewSpecialList-NewKeyStruct
        &       NewSpecialCodeTable-NewKeyStruct
        &       KeyStructInit-NewKeyStruct
        &       PendingAltCode-NewKeyStruct

NewShiftingKeyList
        =       NewShiftingKeyListEnd-NewShiftingKeyList-1
        =       K1ShiftLeft, K1ShiftRight, K1CtrlLeft, K1CtrlRight
        =       K1RightMouse, K1CentreMouse, K1LeftMouse, K1BreakKey
NewShiftingKeyListEnd
        ALIGN

NewSpecialList
        [ Keyboard_Type = "A1A500"
        =       NewSpecialListEnd-NewSpecialList-1
        =       K1ShiftLeft, K1ShiftRight, K1CtrlLeft, K1CtrlRight
        =       K1AltLeft, K1AltRight
        =       K1RightMouse, K1CentreMouse, K1LeftMouse
        =       K1CapsKey, K1TabKey
        =       K1NumKey, K1ScrollKey
        =       K1BreakKey, K1BackTickKey, K1PoundKey
NewSpecialListPad
        =       K1NumPadSlash, K1NumPadStar, K1NumPadHash
        =       K1NumPad7, K1NumPad8, K1NumPad9, K1NumPadMinus
        =       K1NumPad4, K1NumPad5, K1NumPad6, K1NumPadPlus
        =       K1NumPad1, K1NumPad2, K1NumPad3, K1NumPadEnter
        =       K1NumPad0, K1NumPadDot
        ]
NewSpecialListEnd
        ALIGN

NewSpecialCodeTable
        [ Keyboard_Type = "A1A500"
        &       ProcessKShift-NewSpecialCodeTable
        &       ProcessKShift-NewSpecialCodeTable
        &       ProcessKCtrl-NewSpecialCodeTable
        &       ProcessKCtrl-NewSpecialCodeTable
        &       ProcessKAlt-NewSpecialCodeTable
        &       ProcessKAlt-NewSpecialCodeTable
        &       ProcessKRight-NewSpecialCodeTable
        &       ProcessKCentre-NewSpecialCodeTable
        &       ProcessKLeft-NewSpecialCodeTable
        &       ProcessKCaps-NewSpecialCodeTable
        &       ProcessKTab-NewSpecialCodeTable
        &       ProcessKNum-NewSpecialCodeTable
        &       ProcessKScroll-NewSpecialCodeTable
        &       ProcessKBreak-NewSpecialCodeTable
        &       ProcessKBackTick-NewSpecialCodeTable
        &       ProcessKPound-NewSpecialCodeTable
        &       ProcessK1Pad-NewSpecialCodeTable
        &       ProcessK1Pad-NewSpecialCodeTable
        &       ProcessK1Pad-NewSpecialCodeTable
        &       ProcessK1Pad-NewSpecialCodeTable
        &       ProcessK1Pad-NewSpecialCodeTable
        &       ProcessK1Pad-NewSpecialCodeTable
        &       ProcessK1Pad-NewSpecialCodeTable
        &       ProcessK1Pad-NewSpecialCodeTable
        &       ProcessK1Pad-NewSpecialCodeTable
        &       ProcessK1Pad-NewSpecialCodeTable
        &       ProcessK1Pad-NewSpecialCodeTable
        &       ProcessK1Pad-NewSpecialCodeTable
        &       ProcessK1Pad-NewSpecialCodeTable
        &       ProcessK1Pad-NewSpecialCodeTable
        &       ProcessK1Pad-NewSpecialCodeTable
        &       ProcessK1Pad-NewSpecialCodeTable
        &       ProcessK1Pad-NewSpecialCodeTable
        ]

; Now the code to handle it

; Initialise keyboard table handler
;
; in:   R12 -> my workspace
;       R5 = KeyBdStatus
;       R7 = PendingAltType
;
; out:  R5 = new KeyBdStatus
;       R7 = new PendingAltType

KeyStructInit ROUT
        MOV     R0, #0                  ; no shift or ctrl keys down
        STRB    R0, ShiftCount
        STRB    R0, CtrlCount
        STRB    R0, MyMouseButtons
        STRB    R0, KeyNULReturn+1      ; NUL for NUL char return
        MOV     R0, #1                  ; string length for single key return
        STRB    R0, KeyReturn+0
        MOV     R0, #2                  ; length for NUL char return
        STRB    R0, KeyNULReturn+0

        BIC     R5, R5, #(KBStat_ShiftEngaged :OR: KBStat_CtrlEngaged :OR: KBStat_PendingAlt)
        MOV     PC, R14

ProcessKShift ROUT
        ADR     R0, ShiftCount
        MOV     R2, #KBStat_ShiftEngaged
ProcessShiftOrCtrl
        TEQ     R1, #0                  ; R1=1 => down, R1=0 => up
        LDRB    R3, [R0]
        ADDNE   R3, R3, #1              ; if down then increment
        SUBEQ   R3, R3, #1              ; if up then decrement
        STRB    R3, [R0]
        TEQ     R3, #0
        ORRNE   R5, R5, R2              ; one or more shift/ctrl keys down
        BICEQ   R5, R5, R2              ; zero shift/ctrl keys down
        MOV     PC, R14

ProcessKCtrl ROUT
        ADR     R0, CtrlCount
        MOV     R2, #KBStat_CtrlEngaged
        B       ProcessShiftOrCtrl

ProcessKRight ROUT
        MOV     R2, #1
ProcessMouseButton
        TEQ     R1, #0
        LDRB    R0, MyMouseButtons
        ORRNE   R0, R0, R2              ; button going down
        BICEQ   R0, R0, R2              ; button going up
        STRB    R0, MyMouseButtons
        MOV     PC, R3                  ; call his routine and exit

ProcessKCentre ROUT
        MOV     R2, #2
        B       ProcessMouseButton

ProcessKAlt ROUT
        TST     R5, #KBStat_ShiftEngaged
        MOVEQ   R7, #1                          ; Alt
        MOVNE   R7, #2                          ; Shift-Alt
        TST     R5, #KBStat_CtrlEngaged
        ADDNE   R7, R7, #2                      ; Ctrl-Alt, Ctrl-Shift-Alt
        ORR     R5, R5, #KBStat_PendingAlt      ; indicate a pending alt
        MOV     PC, R14

ProcessKLeft ROUT
        MOV     R2, #4
        B       ProcessMouseButton

ProcessKCaps ROUT
        TEQ     R1, #2                          ; is it first press ?
        MOVNE   PC, R14                         ; don't auto-repeat

        TST     R5, #KBStat_ShiftEngaged        ; if shift down
        BICNE   R5, R5, #KBStat_NoCapsLock      ; then force CAPS on
        ORRNE   R5, R5, #KBStat_ShiftEnable     ; and SHIFT CAPS state
        EOREQ   R5, R5, #KBStat_NoCapsLock      ; else toggle caps lock state
        BICEQ   R5, R5, #KBStat_ShiftEnable     ; and cancel shift enable

        MOV     PC, R14

ProcessKTab ROUT
        LDROSB  R0, TABch                       ; TAB key code
        TST     R0, #&80                        ; top bit set ?
        BEQ     ReturnOneChar                   ; no, don't shift or ctrl it
        TST     R5, #KBStat_ShiftEngaged
        EORNE   R0, R0, #&10                    ; modify for shift
        TST     R5, #KBStat_CtrlEngaged
        EORNE   R0, R0, #&20                    ; modify for ctrl
ReturnOneChar
        ADR     R6, KeyReturn                   ; pass pointer back to MOS
        STRB    R0, [R6, #1]                    ; having poked byte in
        MOV     PC, R14

ProcessKNum ROUT
        TEQ     R1, #2                          ; is it first press ?
        EOREQ   R5, R5, #KBStat_NoNumLock       ; yes, then toggle num lock
        MOV     PC, R14                         ; (don't auto-repeat)

ProcessKScroll ROUT
        TEQ     R1, #2                          ; is it first press ?
        EOREQ   R5, R5, #KBStat_ScrollLock      ; yes, then toggle scroll lock
        MOV     PC, R14                         ; (don't auto-repeat)

ProcessKBreak ROUT
        ADD     PC, R3, #4                      ; offset for break routine

        [ Keyboard_Type = "A1A500"
ProcessKBackTick ROUT
        Push    R14
        BL      TestForBfont
        Pull    R14
        MOVEQ   R0, #&BB                        ; Bfont back tick
        MOVNE   R0, #&60                        ; anything else back tick
        B       ReturnNULChar

ProcessKPound ROUT
        Push    R14
        BL      TestForBfont
        Pull    R14
        BNE     %FT10
        TST     R5, #KBStat_ShiftEngaged
        MOVEQ   R0, #&60                        ; Bfont pound
        MOVNE   R0, #&9E                        ; Bfont currency
        B       ReturnNULChar
10
        TST     R5, #KBStat_ShiftEngaged
        MOVEQ   R0, #&A3                        ; anything else 'pound'
        MOVNE   R0, #&A4                        ; anything else currency
        B       ReturnNULChar
        ]

        [ Keyboard_Type = "A1A500"
ProcessK0Pad ROUT
        ADR     R0, PadK0NumTran-(SpecialListPad-SpecialList) ; on
        ADR     R2, PadK0CurTran-(SpecialListPad-SpecialList) ; off
        B       ProcessPad
        ]
        [ Keyboard_Type = "A1A500"
ProcessK1Pad ROUT
        ADR     R0, PadK1NumTran-(NewSpecialListPad-NewSpecialList) ; on
        ADR     R2, PadK1CurTran-(NewSpecialListPad-NewSpecialList) ; off
        ]
ProcessPad
        TST     R5, #KBStat_NoNumLock           ; test num lock
        MOVNE   R0, R2                          ; numlock off -> use R2
        LDRB    R0, [R0, R4]                    ; get table entry
        TEQ     R0, #&FF                        ; dummy key ?
        MOVEQ   PC, R14                         ; then exit

        LDROSB  R2, KeyBase                     ; add on numeric key base
        SUB     R0, R0, #"0"
        ADD     R0, R0, R2

        LDROSB  R2, KeyOpt                      ; zero => ctrl/shift modifies
        TEQ     R2, #0
        BNE     %FT10                           ; [don't modify]

        TST     R0, #&80                        ; top bit set ?
        BEQ     %FT10                           ; no, then don't modify

        TST     R5, #KBStat_ShiftEngaged
        EORNE   R0, R0, #&10                    ; modify for shift
        TST     R5, #KBStat_CtrlEngaged
        EORNE   R0, R0, #&20                    ; modify for ctrl
10
        B       ReturnOneChar


        [ Keyboard_Type = "A1A500"
PadK0NumTran
        =       "#%/*789-456+123",13,"0."
PadK0CurTran
        =       "#%/*",&1E,&8F,&9F,"-",&8C,&FF,&8D,"+",&8B,&8E,&9E,13,&CD,&7F
        ]
        [ Keyboard_Type = "A1A500"
PadK1NumTran
        =       "/*#789-456+123",13,"0."
PadK1CurTran
        =       "/*#",&1E,&8F,&9F,"-",&8C,&FF,&8D,"+",&8B,&8E,&9E,13,&CD,&7F
        ]
        ALIGN

        [ Keyboard_Type = "A1A500"
; *****************************************************************************
;
;       TestForBfont - Check if keyboard corresponds to a Bfont font
;
; in:   IRQ mode
;
; out:  Z => Bfont, NZ => anything else
;

TestForBfont ROUT

        [ {TRUE}
        Push    R14
        LDROSB  R14, KeyAlphabet
        TEQ     R14, #100                       ; EQ => Bfont
        Pull    PC
        |

        [ BleedinDaveBell
FTDefault * %FT20
        |
FTDefault * %FT10
        ]

        Push    "R0-R4,R14"

        TEQP    R14, #(SVC_mode :EOR: IRQ_mode) ; change to SVC mode
        MOVNV   R0, R0                          ; wait for it to happen

        Push    R14                             ; save R14_svc

        MOV     R0, #OsbyteSetAlphKey
        MOV     R1, #&FF                        ; indicate read keyboard no.
        SWI     XOS_Byte
        BVS     FTDefault                       ; error - indicate default

        MOV     R3, R1                          ; R3 = keyboard country no.
        MOV     R1, #Service_International
        MOV     R2, #Inter_CNoToANo             ; convert to alphabet number
        SWI     XOS_ServiceCall
        BVS     FTDefault

        TEQ     R1, #0                          ; was it claimed ?
        BNE     FTDefault                       ; no, then indicate default

        TEQ     R4, #100                        ; is it alphabet bfont ?
        BEQ     %FT10
20
        Pull    R14                             ; restore R14_svc
        MOV     R0, PC
        TEQP    R0, #(SVC_mode :EOR: IRQ_mode)  ; go back to IRQ mode
        MOVNV   R0, R0
        Pull    "R0-R4,R14"
        BICS    PC, R14, #Z_bit                 ; indicate not bfont

10
        Pull    R14                             ; restore R14_svc
        MOV     R0, PC
        TEQP    R0, #(SVC_mode :EOR: IRQ_mode)  ; go back to IRQ mode
        MOVNV   R0, R0
        Pull    "R0-R4,R14"
        ORRS    PC, R14, #Z_bit                 ; indicate is bfont
        ]
        ]

; *****************************************************************************
;
;       PendingAltCode - Process ALT+char
;
; in:   R0 -> key structure
;       R2 = internal key number for char
;       R3 = character
;       R5 = keyboard status
;       R7 = pending alt type
;
; out:  R6 -> returned key list
;

PendingAltCode ROUT
        BIC     R5, R5, #KBStat_PendingAlt ; cancel pending alt
        TEQ     R7, #4                  ; is it CTRL-SHIFT-ALT char ?
        MOVNE   R0, R3
        BNE     ReturnOneChar
        ORR     R0, R3, #&80            ; set top bit
ReturnNULChar
        ADR     R6, KeyNULReturn
        STRB    R0, [R6, #2]
        MOV     PC, R14

; *****************************************************************************

KeyTran ROUT
        [ Keyboard_Type = "A1A500"
; Column 0

00
        =       &82, &92, &A2, &B2      ; f2
        =       &83, &93, &A3, &B3      ; f3
        =       &84, &94, &A4, &B4      ; f4
        =       &86, &96, &A6, &B6      ; f6
        =       &CB, &DB, &EB, &FB      ; pad #, now f11
        &       -1                      ; ESCAPE
        &       -1                      ; pad *
        =       &CD, &DD, &ED, &FD      ; pad /, now f13     
        =       &88, &98, &A8, &B8      ; f8
        =       &CA, &DA, &EA, &FA      ; f10 - now really f10!

        &       -1,-1,-1,-1,-1,-1       ; SHIFT,LOOKS,CMD,RIGHT,CENTRE,LEFT

; Column 1

10
        =       &81, &91, &A1, &B1      ; f1
        =       "3#3#"                  ; 3
        =       "4$4$"                  ; 4
        =       &85, &95, &A5, &B5      ; f5
        &       -1                      ; pad 7
        =       "````"                  ; and copyright sign ?
        &       -1                      ; pad -
        =       &CC, &DC, &EC, &FC      ; pad %, now f12
        =       &87, &97, &A7, &B7      ; f7
        =       &89, &99, &A9, &B9      ; f9

        &       -1,-1,-1,-1,-1,-1

; Column 2

20
        =       "2""2"""                ; 2
        =       "eE", &05, &05          ; E
        =       "5%5%"                  ; 5
        =       "6&6&"                  ; 6
        &       -1                      ; pad 4
        =       "^~", &1E, &1E          ; ^
        &       -1                      ; not fitted
        &       -1                      ; pad 9
        =       "8(8("                  ; 8
        =       "0000"                  ; 0

        &       -1,-1,-1,-1,-1,-1

; Column 3

30
        &       -1                      ; TAB
        =       "zZ", &1A, &1A          ; Z
        =       "gG", &07, &07          ; G
        =       "bB", &02, &02          ; B
        =       &8C, &9C, &AC, &BC      ; left arrow
        =       ":*:*"                  ; :
        &       -1                      ; not fitted
        &       -1                      ; pad 2
        =       "kK", &0B, &0B          ; K
        =       ";+;+"                  ; ;

        &       -1,-1,-1,-1,-1,-1

; Column 4

40
        &       -1                      ; CAPS lock
        =       "xX", &18, &18          ; X
        =       "vV", &16, &16          ; V
        =       "    "                  ; SPACE BAR
        &       -1                      ; pad .
        =       &8B,&9B,&AB,&BB         ; AGAIN
        &       -1                      ; not fitted
        &       -1                      ; ENTER
        =       ",<,<"                  ; ,
        =       "/?/?"                  ; /

        &       -1,-1,-1,-1,-1,-1

; Column 5

50
        =       &80, &90, &A0, &B0      ; MENU now acts like f0
        =       "\|", &1C, &1C          ; \
        =       "cC", &03, &03          ; C
        =       "nN", &0E, &0E          ; N
        =       &8E, &9E, &AE, &BE      ; down arrow
        =       &0D, &0D, &0D, &0D      ; RETURN
        &       -1                      ; not fitted
        &       -1                      ; pad 3
        =       "mM", &0D, &0D          ; M
        =       ".>.>"                  ; .

        &       -1,-1,-1,-1,-1,-1

; Column 6

60
        =       "1!1!"                  ; 1
        =       "qQ", &11, &11          ; Q
        =       "tT", &14, &14          ; T
        =       "yY", &19, &19          ; Y
        &       -1                      ; pad 1
        =       "[{", &1B, &1B          ; [
        &       -1                      ; not fitted
        &       -1                      ; pad 6
        =       "iI", &09, &09          ; I
        =       "pP", &10, &10          ; P

        &       -1,-1,-1,-1,-1,-1

; Column 7

70
        =       &1B, &1B, &1B, &1B      ; HELP (another ESC key for Tutu)
        =       "wW", &17, &17          ; W
        =       "rR", &12, &12          ; R
        =       "7'7'"                  ; 7
        =       &8F, &9F, &AF, &BF      ; up arrow
        =       &7F, &7F, &7F, &7F      ; DELETE  
        &       -1                      ; not fitted
        &       -1                      ; pad 8
        =       "9)9)"                  ; 9
        =       "-=-="                  ; -

        &       -1,-1,-1,-1,-1,-1

; Column 8

80
        =       &8B, &9B, &AB, &BB      ; COPY ?
        =       "aA", &01, &01          ; A
        =       "dD", &04, &04          ; D
        =       "hH", &08, &08          ; H
        =       &8D, &9D, &AD, &BD      ; right arrow
        =       "]}", &1D, &1D          ; ]
        &       -1                      ; not fitted
        &       -1                      ; pad +
        =       "jJ", &0A, &0A          ; J
        =       "lL", &0C, &0C          ; L

        &       -1,-1,-1,-1,-1,-1

; Column 9

90
        =       "__", &1F, &1F          ; _
        =       "sS", &13, &13          ; S
        =       "fF", &06, &06          ; F
        =       "uU", &15, &15          ; U
        &       -1                      ; pad 0
        &       -1                      ; not fitted
        &       -1                      ; not fitted
        &       -1                      ; pad 5
        =       "oO", &0F, &0F          ; O
        =       "@@", &00, &00          ; @ (and grave accent ?)

        &       -1,-1,-1,-1,-1,-1
99
        ASSERT  %10-%00 = 64
        ASSERT  %20-%10 = 64
        ASSERT  %30-%20 = 64
        ASSERT  %40-%30 = 64
        ASSERT  %50-%40 = 64
        ASSERT  %60-%50 = 64
        ASSERT  %70-%60 = 64
        ASSERT  %80-%70 = 64
        ASSERT  %90-%80 = 64
        ASSERT  %99-%90 = 64
        ]
KeyTranEnd


; *****************************************************************************

; Keyboard table for new keyboard

KeyTran2 ROUT
        [ Keyboard_Type = "A1A500"
; Column 0

00
        =       &1B, &1B, &1B, &1B      ; Escape
        =       &81, &91, &A1, &B1      ; f1
        =       &82, &92, &A2, &B2      ; f2
        =       &83, &93, &A3, &B3      ; f3
        =       &84, &94, &A4, &B4      ; f4
        =       &85, &95, &A5, &B5      ; f5
        =       &86, &96, &A6, &B6      ; f6
        =       &87, &97, &A7, &B7      ; f7
        =       &88, &98, &A8, &B8      ; f8
        =       &89, &99, &A9, &B9      ; f9
        =       &CA, &DA, &EA, &FA      ; f10 (or is it f0)
        =       &CB, &DB, &EB, &FB      ; f11
        =       &CC, &DC, &EC, &FC      ; f12
        =       &80, &90, &A0, &B0      ; Print
        &       -1                      ; Scroll Lock
        &       -1                      ; Break

; Column 1

10
        =       &FF, "~", &FF, "~"      ; back tick (») and ~
        =       "1!1!"                  ; 1
        =       "2@", &00, &00          ; 2
        =       "3#3#"                  ; 3
        =       "4$4$"                  ; 4
        =       "5%5%"                  ; 5
        =       "6^", &1E, &1E          ; 6
        =       "7&7&"                  ; 7
        =       "8*8*"                  ; 8
        =       "9(9("                  ; 9
        =       "0)0)"                  ; 0
        =       "-_", &1F, &1F          ; -
        =       "=+=+"                  ; =
        &       -1                      ; pound and currency (ž)
        =       &08, &08, &08, &08      ; backspace
        =       &CD, &DD, &ED, &FD      ; Insert

; Column 2

20
        =       &1E, &1E, &1E, &1E      ; Home
        =       &9F, &8F, &BF, &AF      ; Page Up
        &       -1                      ; Num Lock
        &       -1                      ; pad /
        &       -1                      ; pad *
        &       -1                      ; pad #
        &       -1                      ; Tab
        =       "qQ", &11, &11          ; Q
        =       "wW", &17, &17          ; W
        =       "eE", &05, &05          ; E
        =       "rR", &12, &12          ; R
        =       "tT", &14, &14          ; T
        =       "yY", &19, &19          ; Y
        =       "uU", &15, &15          ; U
        =       "iI", &09, &09          ; I
        =       "oO", &0F, &0F          ; O

; Column 3

30
        =       "pP", &10, &10          ; P
        =       "[{", &1B, &1B          ; [
        =       "]}", &1D, &1D          ; ]
        =       "\|", &1C, &1C          ; \
        =       &7F, &7F, &7F, &7F      ; Delete
        =       &8B, &9B, &AB, &BB      ; Copy
        =       &9E, &8E, &BE, &AE      ; Page Down
        &       -1                      ; pad 7
        &       -1                      ; pad 8
        &       -1                      ; pad 9
        &       -1                      ; pad -
        &       -1                      ; Ctrl (left)
        =       "aA", &01, &01          ; A
        =       "sS", &13, &13          ; S
        =       "dD", &04, &04          ; D
        =       "fF", &06, &06          ; F

; Column 4

40
        =       "gG", &07, &07          ; G
        =       "hH", &08, &08          ; H
        =       "jJ", &0A, &0A          ; J
        =       "kK", &0B, &0B          ; K
        =       "lL", &0C, &0C          ; L
        =       ";:;:"                  ; ;
        =       "'""'"""                ; '
        =       &0D, &0D, &0D, &0D      ; Return
        &       -1                      ; pad 4
        &       -1                      ; pad 5
        &       -1                      ; pad 6
        &       -1                      ; pad +
        &       -1                      ; Shift (left)
        &       -1                      ; not fitted
        =       "zZ", &1A, &1A          ; Z
        =       "xX", &18, &18          ; X

; Column 5

50
        =       "cC", &03, &03          ; C
        =       "vV", &16, &16          ; V
        =       "bB", &02, &02          ; B
        =       "nN", &0E, &0E          ; N
        =       "mM", &0D, &0D          ; M
        =       ",<,<"                  ; ,
        =       ".>.>"                  ; .
        =       "/?/?"                  ; /
        &       -1                      ; Shift (right)
        =       &8F, &9F, &AF, &BF      ; cursor up
        &       -1                      ; pad 1
        &       -1                      ; pad 2
        &       -1                      ; pad 3
        &       -1                      ; Caps Lock
        &       -1                      ; Alt (left)                    ***
        =       "    "                  ; space bar

; Column 6

60
        &       -1                      ; Alt (right)                   ***
        &       -1                      ; Ctrl (right)
        =       &8C, &9C, &AC, &BC      ; cursor left
        =       &8E, &9E, &AE, &BE      ; cursor down
        =       &8D, &9D, &AD, &BD      ; cursor right

        [ {FALSE}
        &       -1                      ; pad 0
        &       -1                      ; pad .
        &       -1                      ; pad Enter
        &       -1                      ; not fitted
        &       -1                      ; not fitted
        &       -1                      ; not fitted
        &       -1                      ; not fitted
        &       -1                      ; not fitted
        &       -1                      ; not fitted
        &       -1                      ; not fitted
        &       -1                      ; not fitted

; Column 7

70
        &       -1                      ; mouse left
        &       -1                      ; mouse centre
        &       -1                      ; mouse right
        ]
        ASSERT  %10-%00 = 16*4
        ASSERT  %20-%10 = 16*4
        ASSERT  %30-%20 = 16*4
        ASSERT  %40-%30 = 16*4
        ASSERT  %50-%40 = 16*4
        ASSERT  %60-%50 = 16*4
        ]
KeyTran2End


; *****************************************************************************
;
;       Reverse table lookup for INKEY(-ve)
;

InkeyTran
    [ Keyboard_Type = "A1A500"
        IKT     &FF, &FF, &FF, &37, &97, &24, &84, &51          ; 80-87
        IKT     &19, &08, &03, &13, &01, &00, &10, &0570        ; 88-8F
        IKT     &FF, &FF, &FF, &57, &64, &94, &8045, &49        ; 90-97
        IKT     &59, &48, &58, &33, &42, &43, &31, &30          ; 98-9F
        IKT     &FF, &FF, &FF, &FF, &06, &04, &75, &85          ; A0-A7
        IKT     &39, &89, &53, &83, &32, &52, &91, &FF          ; A8-AF
        IKT     &FF, &FF, &FF, &44, &FF, &07, &55, &35          ; B0-B7
        IKT     &99, &38, &88, &63, &92, &41, &81, &40          ; B8-BF
        IKT     &FF, &FF, &07, &47, &16, &87, &74, &65          ; C0-C7
        IKT     &69, &98, &93, &23, &72, &82, &20, &60          ; C8-CF
        IKT     &FF, &FF, &FF, &FF, &27, &77, &54, &90          ; D0-D7
        IKT     &29, &78, &68, &73, &62, &21, &71, &50          ; D8-DF
        IKT     &FF, &09, &17, &04, &14, &67, &34, &25          ; E0-E7
        IKT     &79, &18, &28, &02, &22, &12, &11, &61          ; E8-EF
        IKT     &FF, &FF, &FF, &FF, &0D, &0E, &0F, &0B          ; F0-F7
        IKT     &0C, &0A, &0B, &0C, &0A, &0B, &0C, &0A          ; F8-FF
     ]

InkeyTran2
     [ Keyboard_Type = "A1A500"
        IKT     &FF, &FF, &FF, &5B, &49, &48, &64, &33          ; 80-87
        IKT     &09, &08, &06, &05, &03, &02, &01, &00          ; 88-8F
        IKT     &FF, &FF, &FF, &5C, &5A, &65, &35, &57          ; 90-97
        IKT     &56, &55, &54, &52, &51, &5F, &4E, &26          ; 98-9F
        IKT     &FF, &4D, &1C, &FF, &24, &25, &34, &32          ; A0-A7
        IKT     &45, &44, &53, &41, &40, &50, &3D, &FF          ; A8-AF
        IKT     &46, &36, &22, &66, &FF, &23, &47, &45          ; B0-B7
        IKT     &12, &43, &42, &2C, &3F, &4F, &3C, &5D          ; B8-BF
        IKT     &21, &20, &1F, &67, &3A, &4B, &59, &31          ; C0-C7
        IKT     &30, &2F, &2D, &16, &2A, &3E, &12, &11          ; C8-CF
        IKT     &1E, &1D, &10, &0F, &39, &38, &63, &1B          ; D0-D7
        IKT     &1A, &19, &2E, &17, &2B, &29, &28, &0D          ; D8-DF
        IKT     &0E, &0A, &0C, &0B, &37, &4A, &62, &16          ; E0-E7
        IKT     &1B, &07, &18, &04, &15, &14, &13, &27          ; E8-EF
        IKT     &FF, &FF, &FF, &FF, &72, &71, &70, &60          ; F0-F7
        IKT     &61, &58, &5E, &3B, &4C, &5E60, &3B61, &4C58    ; F8-FF
    ]

        END
