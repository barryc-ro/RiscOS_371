        TTL     A veneer for Econet/NetFS/NetPrint to look like NFS/ANFS ==> &.Arthur.BBCEconet.Module

        GET     Hdr:ListOpts
        GET     Hdr:Macros
        GET     Hdr:System
        GET     Hdr:Debug
        GET     Hdr:ModHand
        GET     Hdr:FSNumbers
        GET     Hdr:NewErrors
        GET     Hdr:Services
        GET     Hdr:Econet
        GET     Hdr:MsgTrans
        GET     Hdr:Symbols

        GET     Time+Date

        SUBT    Module header ==> &.Arthur.BBCEconet.Module

Origin
        DCD     0 ; Start
        DCD     InitModule - Origin             ; Initialisation
        DCD     KillModule - Origin             ; Finalisation
        DCD     Service - Origin
        DCD     ModuleTitle - Origin
        DCD     HelpString - Origin
        DCD     0 ; UtilsCommands               ; Command Table
        DCD     0 ; MySWIChunkBase
        DCD     0 ; SVCEntry
        DCD     0 ; SWINameTable
        DCD     0

        [       :LNOT: ReleaseVersion
        InsertDebugRoutines
        ]

MinRxHandle     * 3
MaxRxHandle     * 17

        ^       0, wp ; Store
        Word    TransmitHandle
        Word    LastTxStatus
        Word    RxHandles, MaxRxHandle - MinRxHandle + 1
        ; Compute back to 'begining' of table
ReceiveHandles * RxHandles - (MinRxHandle * 4)

TotalRAMRequired * @

        GBLL    Debug
Debug   SETL    (:LNOT:ReleaseVersion) :LAND: False

        SUBT    Module entry stuff
        OPT     OptPage
ModuleTitle
        DCB     "BBCEconet",0
        ALIGN

HelpString
        DCB     "BBCEconet", 9, "0.", CurrentVersion
        [       ReleaseVersion
        !       0, "Version 0.$CurrentVersion"
        |
        DCB     "/", CurrentIteration
        ]
        DCB     " (", CurrentDate, ")"
        [       :LNOT: ReleaseVersion
        DCB     " assembled at ", CurrentTime
        ]
        DCB     0
        ALIGN

        SUBT    Initialisation code
        OPT     OptPage

InitModule ROUT
        Push    "r0-r4, lr"
        LDR     r2, [ r12 ]
        TEQ     r2, #0
        BNE     StoreClaimed
        [       Debug
        DLINE   "BBCEconet - Init"
        ]
        MOV     r0, #0
        MOV     r1, #-1
        SWI     XEconet_SetProtection           ; Do a read to discover if Econet is there
        BVS     EconetError
        MOV     r0, #ModHandReason_Claim
        LDR     r3, =:INDEX: TotalRAMRequired
        SWI     XOS_Module
        BVS     ExitInitialisation
        STR     r2, [ r12 ]
StoreClaimed
        MOV     wp, r2
        [       Debug
        DLINE   "BBCEconet - Init"
        ]
        ; Now the stuff which just requires zeroing
        MOV     r0, #0
        MOV     r1, wp                          ; Start of variables
        ADR     r2, TotalRAMRequired
InitialisationLoop
        STR     r0, [ r1 ], #4                  ; It should be all words
        TEQ     r1, r2
        BNE     InitialisationLoop
ClaimVectors
        MOV     r0, #ByteV
        ADRL    r1, OsByte
        MOV     r2, wp
        SWI     XOS_Claim
        MOVVC   r0, #WordV
        ADRL    r1, OsWord
        MOV     r2, wp
        SWIVC   XOS_Claim
ExitInitialisation
        STRVS   r0, [ sp ]
        Pull    "r0-r4, pc"

EconetError
        LDR     r14, [ r0 ]
        LDR     r3, =ErrorNumber_NoSuchSWI
        TEQ     r14, r3
        ADREQ   r0, ErrorNoEconet
        MOVEQ   r1, #0
        MOVEQ   r2, #0
        SWIEQ   XMessageTrans_ErrorLookup
        B       ExitInitialisation

ErrorNoEconet
        DCD     ErrorNumber_NoEconet
        DCB     "NoEco", 0

Service         ROUT
        TEQ     r1, #Service_Reset
        MOVNE   pc, lr
        Push    "r0-r4, lr"
        LDR     wp, [ r12 ]
        MOV     r0, #253                        ; Now check reset was NOT hard
        MOV     r1, #0
        MOV     r2, #255
        SWI     XOS_Byte                        ; Read last reset type, only do reset when it was soft
        TEQ     r1, #0
        BEQ     ClaimVectors
        Pull    "r0-r4, pc"

KillModule      ROUT
        ; Totally ignore all errors in this section
        Push    "r0-r2, lr"
        LDR     wp, [ r12 ]
        [       Debug
        DLINE   "BBCEconet - Finalise"
        ]
        MOV     r0, #ByteV
        ADRL    r1, OsByte
        MOV     r2, wp
        SWI     XOS_Release
        [       Debug
        DLINE   "BBCEconet - OsByte released"
        ]
        MOV     r0, #WordV
        ADRL    r1, OsWord
        MOV     r2, wp
        SWI     XOS_Release
        [       Debug
        DLINE   "BBCEconet - OsWord released"
        ]
        LDR     r0, TransmitHandle
        [       Debug
        DREG    r0, "Abandon Tx &"
        ]
        SWI     XEconet_AbandonTransmit
        [       Debug
        DLINE   "BBCEconet - Transmit abandoned"
        ]
        ADR     r1, ReceiveHandles
        MOV     r2, #MinRxHandle
AbandonNextReceiveHandle
        LDR     r0, [ r1, r2, ASL #2 ]
        SWI     XEconet_AbandonReceive
        [       Debug
        DLINE   "BBCEconet - Receive abandoned"
        ]
        INC     r2
        CMP     r2, #MaxRxHandle + 1
        BLE     AbandonNextReceiveHandle
        [       Debug
        DLINE   "BBCEconet - Finalise finished"
        ]
        Pull    "r0-r2, lr"
        BICS    pc, lr, #VFlag                  ; Note that this can't fail

        LTORG
        GET     s.Interface
        END
