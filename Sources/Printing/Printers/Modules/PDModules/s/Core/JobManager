; > Core.JobManager

;----------------------------------------------------------------------------
;
; Job management subroutines
;
;----------------------------------------------------------------------------

; Subroutine to find the job with a given file handle in the list of print
; jobs.
;
; Entry:  R10 = file handle
;
; Exit:   If found:     V clear
;                       R0,R2 corrupt
;                       R3 points to link to job's workspace
;                       R11 points to job's workspace
;         If not found: V set
;                       R0,R2,R3,R11 corrupt

findjob
        ADR     R3,printjoblist
        LDR     R2,printjoblist
findjob_loop
        MOVS    R11,R2
        ErrorReturn EQ                  ;Return with VS if end of list
        ASSERT  :BASE:jobhandle = R11
        ASSERT  :INDEX:jobhandle = 0
        ASSERT  joblink = jobhandle + 4
        LDMIA   R11,{R0,R2}             ;R0 := jobhandle, R2 := joblink
        TEQ     R0,R10
        Return  EQ                      ;Return with VC if job found
        ADR     R3,joblink
        B       findjob_loop

; Subroutine to unlink a print job from the chain and release its workspace.
; Also calls font_losefont to lose any slaved fonts for the job.
;
; Entry:  R3 points to link to job's workspace
;         R11 points to job's workspace
;
; Exit:   R0,R2 corrupt

deletejob ROUT
        Push    "LR"

        MOV     R0,#0        
        BL      font_losefont           ;Lose slaved fonts
        BLVC    clearrectangles         ;Remove rectangles list
        ADRVC   R2,jobfontlist          
        BLVC    miscop_removefontsR2    ;And, finally, remove the fonts attached to the job
        BVS     %15                     ;Exit if it did not work!
 
        MOV     R0,#ModHandReason_Free
     
        LDR     R2,info_printer         ;Pointer to printer name block
        TEQ     R2,#0                   ;Is a printer name block allocated?
        BEQ     %10

        SWI     XOS_Module              ;Release the block
        BVS     %15

        MOV     LR,#0
        STR     LR,info_printer         ;Yes, so mark as zapped   
10
        LDR     R2,joblink              ;Unlink job
        STR     R2,[R3]
        SUB     R2,R11,#jobwsoffset
        SWI     XOS_Module              ;And release it
15
        Pull    "PC"

; Subroutine to get rid of a print job's rectangles.
;
; Entry:  R11 points to job's workspace
;
; Exit:   -

clearrectangles
        Push    "R0,R2,R9,LR"
        MOV     R0,#ModHandReason_Free
        LDR     R9,rectlist
clearrectangles_loop
        CMP     R9,#0                   ;Clears V
        STREQ   R9,rectlist
        Pull    "R0,R2,R9,PC",EQ
        MOV     R2,R9
        LDR     R9,nextrectangle
        SWI     XOS_Module
        BVC     clearrectangles_loop
        ADD     R13,R13,#4
        Pull    "R2,R9,PC"

        END
