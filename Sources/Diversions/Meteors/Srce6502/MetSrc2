REM .SRCED
REM /// Meteors definition file ///
 
*LOAD .Meteor2
 
osword=&FFF1:osbyte=&FFF4
osget=&FFE0:oswrch=&FFEE
oscli=&FFF7:eventvec=&220
 
screen=&30:linelen=&280
`cutoff=&F0:`base=screen*256+linelen*(256-`cutoff)DIV8
`depth=&8000-`base
`col0=0:`col1=&0F:`col2=&F0:`col3=&FF
looptime=2:`dieprob=40
`capslock=&BF:`ctrl=&FE:`shift=&FF
`space=&9D:`return=&B6
`esc=&1B:`copy=&87:`delete=&7F
`spcprob=10:`spcvel=480:`spccnt=12
`timelo=&5A:`timehi=&46:accfreq=8
 
P%=0
tempx=FNs(1):colour=FNs(1):colmask=FNs(1)
addr=FNs(2):mask=FNs(1):limit=FNs(1):temp=FNs(2)
raster=FNs(2):seed=FNs(3):stack=FNs(1)
newshcnt=FNs(1)
numcyc=FNs(1):score=FNs(2):gremno=FNs(1)
shipshape=FNs(1):acccntr=FNs(1):collision=FNs(1):dead=FNs(1)
hyperspace=FNs(1):time=FNs(1):oldtime=FNs(5):numroids=FNs(1)
supp=FNs(1):sheetno=FNs(1):numships=FNs(1):soundon=FNs(1)
oldscore=FNs(1):beepcount=FNs(1):beepdelay=FNs(1)
messX=FNs(1):messY=FNs(1):posnno=FNs(1):spacecounter=FNs(1)
killed=FNs(1):retflag=FNs(1):shipssofar=FNs(1):demo=FNs(1)
beating=FNs(1):cycletime=FNs(1):cyclecount=FNs(1):beattime=FNs(1)
beatcount=FNs(1):beatsnd=FNs(1):rocktime=FNs(1):beatdelay=FNs(1)
letgoyet=FNs(1):sptype=FNs(1):lastcheck=FNs(1):bulletX=FNs(1)
rightrocks=FNs(1):old224=FNs(1):V=FNs(1):K=FNs(1)
dx=FNs(1):dy=FNs(1):vx=FNs(1):vy=FNs(1):_x=FNs(1):_y=FNs(2)
_z=FNs(2):_a=FNs(4):_b=FNs(2):nextexp=FNs(1):currtime=FNs(1)
endZ%=P%
 
H%=&2B00:hiscore=H%:hiscoretab=H%
hientlen=23:maxhient=9*hientlen:endH%=H%+10*hientlen
posnxtab=5:scorextab=9
namextab=17:nameytab=5
 
rocklen=26:nb=5:len=rocklen+nb
T%=&2C00:ntyp=48:ntyp1=ntyp-32:P%=T%
size=FNs(ntyp):shapelo=FNs(ntyp):shapehi=FNs(ntyp)
xlength=FNs(ntyp1)-32:xoffset=FNs(ntyp1)-32
ylength=FNs(ntyp1)-32:yoffset=FNs(ntyp1)-32
exvlo=FNs(nb)-rocklen:exvhi=FNs(nb)-rocklen
eyvlo=FNs(nb)-rocklen:eyvhi=FNs(nb)-rocklen
egflags=FNs(nb)-rocklen
nx=6
extime=FNs(nx):exshapes=FNs(16):limtime=FNs(8)
exaddlo=FNs(8*nx):exaddhi=FNs(8*nx):exmask=FNs(8*nx)
endT%=P%
 
U%=&400:P%=U%
type=FNs(len):oldtype=FNs(len):xlo=FNs(len):xhi=FNs(len)
ylo=FNs(len):yhi=FNs(len):xvlo=FNs(len):xvhi=FNs(len)
yvlo=FNs(len):yvhi=FNs(len):oldlo=FNs(len):oldhi=FNs(len)
oldmask=FNs(len):newlo=FNs(len):newhi=FNs(len):newmask=FNs(len)
gflags=FNs(len)
endU%=P%
 
S%=&2400
 
data%=endT%:P%=data%:nsh=32:accn=7
xacclo=FNs(nsh):xacchi=FNs(nsh):yacclo=FNs(nsh):yacchi=FNs(nsh)
bxvlo=FNs(nsh):bxvhi=FNs(nsh):byvlo=FNs(nsh):byvhi=FNs(nsh)
 
nsnds=12
sndbuff=FNs(8):snd0=FNs(nsnds):snd1=FNs(nsnds):snd2=FNs(nsnds)
snd3=FNs(nsnds):snd4=FNs(nsnds):S_type=FNs(2):S_palette=FNs(2)
S_scorelo=FNs(2):S_scorehi=FNs(2):S_sound=FNs(2)
my224=FNs(1):oswordblk=FNs(5):bits=FNs(4):rockscore=FNs(3)
M%=P%:nmess=7
messptr=M%:messlen=messptr+2*nmess+2:currmess%=messlen+nmess
 
RETURN
 
DEFFNs(S%) P%=P%+S%:=P%-S%

REM .SRCE1
 
P%=&1100
 
[OPTpass
 
JMPentry
 
\ ***************************
\    Non-standard interfaces
\ ***************************
 
.vsyncevent
PHP:PHA:CMP#4:BNEendevent
INCmy224:JSRstart6522
.endevent PLA:PLP:RTS
.vsync JMPP%
.newvsync
LDAmy224:CMPold224:BEQnewvsync:STAold224:RTS
.oldvsync
LDA&224:CMPold224:BEQoldvsync:STAold224
\ $JSR start6522
.start6522
LDA#&C0:STA&FE6B:LDA#`timelo:STA&FE64:LDA#`timehi:STA&FE65:RTS
.timeout6522 BIT&FE60:RTS
.setcaps JMPP%
.newcaps
LDA#202:LDX#&20:LDY#&CF:JMPosbyte
.oldcaps
LDA#&20:STA&D8:RTS
\ ***************************
 
.crtcreg
PHA:LDA#23:JSRoswrch:LDA#0:JSRoswrch:TXA:JSRoswrch:PLA:JSRoswrch
JSRthree0s:JMPthree0s
 
.ospalette
PHA:LDA#19:JSRoswrch:PLA
PHA:LSRA:LSRA:LSRA:LSRA:JSRoswrch:PLA:AND#15:JSRoswrch
.three0s LDA#0:JSRoswrch:JSRoswrch:JMPoswrch
 
.inkey0
LDY#0:LDX#0:LDA#&81:JSRosbyte
TXA:CPY#`esc:BNEdoneinkey:LDA#126:JSRosbyte:LDA#`esc
.doneinkey RTS
.inkey
LDY#&FF:LDA#&81:JSRosbyte:TXA
.noupdate RTS
 
.sound
STAtemp:LDAdemo:BNEnosound:LDAsoundon:BEQnosound
TXA:PHA:TYA:PHA:LDXtemp
LDAsnd0,X:STAtemp:.sndlp
LDAsnd0,X:STAsndbuff+1
LDAsnd1,X:STAsndbuff
LDY#2:LDAsnd2,X:JSRputsnd:LDAsnd3,X:JSRputsnd:LDAsnd4,X:JSRputsnd
TXA:PHA
LDX#sndbuff AND&FF:LDY#sndbuff DIV256:LDA#7:JSRosword
PLA:TAX:INX:DECtemp:BPLsndlp
PLA:TAY:PLA:TAX:.nosound RTS
 
.putsnd
STAsndbuff,Y:INY:ASLA:LDA#0:BCCP%+4:LDA#&FF:STAsndbuff,Y:INY:RTS
 
.addvels
LDAtype,X:ASLA:BMInoupdate
LSRA:STAtype,X
CLC:LDAxlo,X:ADCxvlo,X:STAxlo,X
LDAxhi,X:ADCxvhi,X:STAxhi,X
CMP#160:BCCokup:CMP#210:BCSoffleft
SEC:SBC#160:STAxhi,X
SEC:LDAyhi,X:SBC#8:STAyhi,X
JMPokup:.offleft
CLC:ADC#160:STAxhi,X
CLC:LDAyhi,X:ADC#8:STAyhi,X
.okup
CLC:LDAylo,X:ADCyvlo,X:STAylo,X
LDAyhi,X:ADCyvhi,X
CMP#(256+`cutoff)DIV2:BCCL1:ADC#`cutoff-1
.L1 CMP#`cutoff:BCCL2:SBC#`cutoff
.L2 STAyhi,X:RTS
 
.update
JSRaddvels
\ $JSR getaddr
 
.getaddr
LDA#screen DIV4:STAaddr+1
LDAxhi,X:AND#&FE:ASLA:ROLaddr+1
ASLA:ROLaddr+1:STAaddr
LDAyhi,X:AND#7:EOR#7:ORAaddr
STAnewlo,X:LDA#0:STAaddr
LDAyhi,X:EOR#&FF:AND#&F8:LSRA:LSRA
STAtemp:LSRA:LSRA:RORaddr
ADCtemp:STAtemp
CLC:LDAaddr:ADCnewlo,X:STAnewlo,X
LDAaddr+1:ADCtemp:STAnewhi,X
STXtemp
LDAxlo,X:ASLA:LDAxhi,X:ROLA:AND#3
TAX:LDAbits,X:LDXtemp:STAnewmask,X
RTS
 
.setcol
LDA#`col2:CPX#32:BCCL10:CPX#35:BCCL11
.L10 LDA#`col3:BCCL11:LDA#`col1:CPX#35:BEQL11:CPX#36:BEQL11
CPX#42:BEQL11:LDA#`col3
.L11 STAcolour:ANDmask:STAcolmask
RTS
 
.getexplos
LDY#nx-1:.findexplp LDAextime,Y:BEQgotanexp:DEY:BPLfindexplp:RTS
.gotanexp LDA#10:STAextime,Y
TYA:PHA:CPX#0:BEQnooffset
LDAtype,X:AND#&7F:TAY
LDAxlength,Y:LSRA:STAtemp:LDAylength,Y:LSRA:STAtemp+1
SEC:LDAxhi,X:PHA:SBCxoffset,Y:CLC:ADCtemp:STAxhi,X
SEC:LDAyhi,X:PHA:SBCyoffset,Y:CLC:ADCtemp+1
CMP#`cutoff:BCCL7:SBC#`cutoff:.L7 STAyhi,X
JSRgetaddr:PLA:STAyhi,X:PLA:STAxhi,X
.nooffset PLA:ASLA:ASLA:ASLA:TAY
.startexplp LDAnewlo,X:STAexaddlo,Y:LDAnewhi,X:STAexaddhi,Y
LDAnewmask,X:STAexmask,Y:INY:TYA:AND#7:BNEstartexplp:RTS
 
.doexplosion
LDXnextexp:INX:CPX#nx:BNEnowrap1:LDX#0:.nowrap1 STXnextexp
LDAextime,X:BEQretn:SEC:SBC#1:STAextime,X:STAcurrtime:BEQremexp
TXA:ASLA:ASLA:ASLA:TAX
.plotexplp TXA:AND#7:TAY:LDAlimtime,Y:CMPcurrtime:BCCnotyet
JSRplotexpbit:.notyet INX:TXA:AND#7:BNEplotexplp
.retn RTS
.remexp
TXA:ASLA:ASLA:ASLA:TAX:LDY#0
.remexplp LDAexaddlo,X:STAaddr:LDAexaddhi,X:STAaddr+1
LDAexmask,X:AND#`col3:EOR(addr),Y:STA(addr),Y
INX:TXA:AND#7:BNEremexplp:RTS
 
.plotexpbit
LDAexaddlo,X:AND#7:TAY:LDAexaddlo,X:AND#&F8:STAaddr
LDAexaddhi,X:STAaddr+1:LDA#2:STAlimit
LDAexmask,X:STAmask:LDA#`col3:STAcolour:ANDmask:STAcolmask
TXA:PHA:AND#7:ASLA:ADC#exshapes AND&FF:STAP1+1:STAP2+1:STAP3+1
LDA#0:ADC#exshapes DIV256:STAP1+2:STAP2+2:STAP3+2
LDX#0:JSRplot4:PLA:TAX
TYA:ORAaddr:STAexaddlo,X:LDAaddr+1:STAexaddhi,X:LDAmask:STAexmask,X
RTS
 
.plotroid JSRsetcol
.plotshape LDAaddr:AND#7:TAY
LDAaddr:AND#&F8:STAaddr
TXA:PHA:LDAsize,X:STAlimit
LDAshapelo,X
STAP1+1:STAP2+1:STAP3+1
LDAshapehi,X
STAP1+2:STAP2+2:STAP3+2
LDA#0:STAcollision
LDX#0:JSRplot4:PLA:TAX
RTS
 
.qinc1 INCaddr+1:BNEqplot2
.qplot1 ASLmask:BCCqplot2:LDA#&11:STAmask
SEC:LDAaddr:SBC#8:STAaddr:BCSqplot2:DECaddr+1:BNEqplot2
.qplotdone RTS
 
.quickplot
.P4 LDAP%,X:AND#&20:BNEnoqplot
LDAcolour:ANDmask:EOR(addr),Y:STA(addr),Y
.noqplot INX:CPXlimit:BEQqplotdone
.P5 LDAP%,X:BMIqplot1:ASLA:BPLqplot2
LSRmask:BCCqplot2:LDA#&88:STAmask
CLC:LDAaddr:ADC#8:STAaddr:BCSqinc1
.qplot2
.P6 LDAP%,X:LSRA:BCSqplot3:LSRA:BCCquickplot
DEY:BPLquickplot:LDY#7
SEC:LDAaddr:SBC#linelen AND&FF:STAaddr
LDAaddr+1:SBC#linelen DIV256:STAaddr+1:BNEquickplot
.qplot3 INY:CPY#8:BNEquickplot:LDY#0
CLC:LDAaddr:ADC#linelen AND&FF:STAaddr
LDAaddr+1:ADC#linelen DIV256:STAaddr+1:BNEquickplot
 
.plotdone RTS
 
.plot4
.P3 LDAP%,X:AND#&20:BNEnoplot
LDAcolmask:EOR(addr),Y:STA(addr),Y
ANDmask:EORcolmask:BEQnoplot:STAtemp:LDA#`col3:ANDmask:EORtemp
STAcollision:.noplot
INX:CPXlimit:BEQplotdone
.P1 LDAP%,X:BMIplot1:ASLA:BPLplot2
LSRcolmask:LSRmask:BCCplot2
LDA#&88:STAmask:ANDcolour:STAcolmask
CLC:LDAaddr:ADC#8:STAaddr:BCCplot2
INCaddr+1:BPLplot2:LDAaddr:SEC:SBC#`depth AND&FF:STAaddr
LDAaddr+1:SBC#`depth DIV256:STAaddr+1:JMPplot2
.plot1
ASLcolmask:ASLmask:BCCplot2
LDA#&11:STAmask:ANDcolour:STAcolmask
SEC:LDAaddr:SBC#8:STAaddr:BCSL6
DECaddr+1:.L6 LDAaddr+1
CMP#`base DIV256:BCCL4:BNEplot2:LDAaddr:CMP#`base AND&FF:BCSplot2
.L4 CLC:LDAaddr:ADC#`depth AND&FF:STAaddr
LDAaddr+1:ADC#`depth DIV256:STAaddr+1
.plot2
.P2 LDAP%,X
LSRA:BCSplot3:LSRA:BCCplot4b
DEY:BPLplot4b:LDY#7:SEC
LDAaddr:SBC#linelen AND&FF:STAaddr
LDAaddr+1:SBC#linelen DIV256:STAaddr+1:CMP#`base DIV256:BCCL5:BNEplot4b
LDAaddr:CMP#`base AND&FF:BCSplot4b
.L5 CLC:LDAaddr:ADC#`depth AND&FF:STAaddr
LDAaddr+1:ADC#`depth DIV256:STAaddr+1:.plot4b JMPplot4
.plot3
INY:CPY#8:BNEplot4a:LDY#0:CLC
LDAaddr:ADC#linelen AND&FF:STAaddr
LDAaddr+1:ADC#linelen DIV256:STAaddr+1:BPLplot4a
SEC:LDAaddr:SBC#`depth AND&FF:STAaddr
LDAaddr+1:SBC#`depth DIV256:STAaddr+1
.plot4a JMPplot4
 
.execute
LDX#0:.exlp
LDAtype,X:BMInoex:CMP#32:BCSex1
.exwait LDYyhi,X:JSRtestraster
CMP#24:BCCexwait
.ex1 JSRunplot
LDAnewlo,X:STAoldlo,X:STAaddr
LDAnewhi,X:STAoldhi,X:STAaddr+1
BEQex2:LDAnewmask,X:STAoldmask,X
STAmask:TXA:PHA
LDAtype,X:STAoldtype,X:TAX
JSRplotroid:PLA:TAX
CPX#0:BNEex2a
LDAcollision:ORAdead:STAdead
JMPex2
.ex2a CPX#rocklen:BCCex2
LDAkilled:BNEex2
LDAcollision:BEQex2:JSRdirecthit
.ex2 LDAtype,X:ORA#&80:STAtype,X
.noex INX:CPX#len:BNEexlp
.endex RTS
 
.unplot
LDAoldlo,X:STAaddr
LDAoldhi,X:STAaddr+1:BEQex1a
LDAoldmask,X:STAmask
TXA:PHA:LDAoldtype,X:TAX
JSRplotroid:PLA:TAX
.ex1a RTS
 
.kill
LDArocktime:STAbeatcount
LDA#2:JSRsound
JSRgetexplos
CPX#1:BNEitsarock:JSRkillspc
LDXsptype:LDYS_scorehi,X:LDAS_scorelo,X:TAX:JMPaddscXY
.itsarock DECnumroids
.gorock
LDAtype,X:ASLA:BMInokill:LSRA
SEC:SBC#32:CMP#3:BCSnokill
PHA:STXtemp:TAY
LDAbulletX:CMP#rocklen:BEQnoscore
LDXrockscore,Y:LDY#0:JSRaddscXY
.noscore LDXtemp:LDYtemp:PLA
CMP#0:BNEnotbig:INY:INY:BNEkill1
.notbig CMP#1:BNEnotmedium
INY:.kill1 JMPgetrocks
.notmedium JMPdie
.nokill RTS
 
.getrocks TXA:PHA
JSRveladd:ASLtemp:ROLtemp+1
CLC:LDAxvlo,X:ADCtemp:STAxvlo,Y
LDAxvhi,X:ADCtemp+1:STAxvhi,Y
SEC:LDAxvlo,X:SBCtemp:STAxvlo,X
LDAxvhi,X:SBCtemp+1:STAxvhi,X
JSRveladd
CLC:LDAyvlo,X:ADCtemp:STAyvlo,Y
LDAyvhi,X:ADCtemp+1:STAyvhi,Y
SEC:LDAyvlo,X:SBCtemp:STAyvlo,X
LDAyvhi,X:SBCtemp+1:STAyvhi,X
JSRcopycoords
CLC:LDAyhi,Y:ADC#8:STAyhi,Y
LDAtype,X:ORA#&80:CLC:ADC#1:STAtype,Y:STAtype,X
TYA:TAX:JSRzeroaddrs:PLA:TAX:RTS
 
.veladd
JSRrandom:AND#31:STA_x:LDAV:STA_y
JSRmultiply:LDA_a+1:STAtemp+1
LDA_a:STAtemp:RTS
 
.die
LDAtype,X:ASLA:BMInodie
JSRunplot:LDA#&FF:STAtype,X
.nodie RTS
 
.directhit
CPX#rocklen:BNEnotspbullet
LDA#2:BNEeitherb
.notspbullet \JSRdie:LDA#1
.eitherb STAlastcheck
TXA:PHA:LDAxhi,X:STAtempx
LDAyhi,X:STAtempx+1:LDX#rocklen-1
.hitlp
LDAtype,X:ASLA:BMIendhit:LSRA:TAY
LDAxlength,Y:STAtemp+1:SEC:LDAxhi,X:SBCxoffset,Y:STAtemp
SEC:LDA#160:SBCtemp:CMPtemp+1:BCSnolrwrap
CLC:LDAtemp+1:ADC#256-160:STAtemp+1:.nolrwrap
SEC:LDAtempx:SBCtemp:CMPtemp+1:BCSendhit
LDAylength,Y:STAtemp+1:SEC:LDAyhi,X:SBCyoffset,Y:STAtemp
SEC:LDA#`cutoff:SBCtemp:CMPtemp+1:BCSnoudwrap
CLC:LDAtemp+1:ADC#256-`cutoff:STAtemp+1:.noudwrap
SEC:LDAtempx+1:SBCtemp:CMPtemp+1:BCCgothit
.endhit DEX:CPXlastcheck:BCShitlp
PLA:TAX:RTS
.gothit JSRkill:PLA:TAX:JMPdie
 
.waitspace JSRcycle
LDAtype+1:ASLA:BPLwaitspace
LDX#rocklen-1
.wsplp LDAtype,X:ASLA:BMInowsp
LDAxhi,X
CMP#50:BCCnowsp:CMP#90:BCSnowsp
LDAyhi,X
CMP#85:BCCnowsp:CMP#148:BCSnowsp
JMPwaitspace
.nowsp DEX:BNEwsplp:RTS
 
]RETURN
 
DEFPROCFIND(A$)
Z%=PAGE:REPEATA%=Z%+4
IFLEN$A%>=LENA$IFINSTR($A%,A$)PRINTZ%?1*256+Z%?2;
Z%=Z%+Z%?3:UNTILZ%?1>&7F:ENDPROC

REM .SRCE2
 
[OPTpass
 
.getspaceship
LDAtype+1:ASLA:BPLnospc
LDAdemo:BNEgetspc
LDAtype:ASLA:BMInospc
LDAbeating:BEQnospc
LDAbeatcount:BEQgetspc:.nospc RTS
.getspc LDA#0:STArightrocks
LDArocktime:STAbeatcount
LDX#1:JSRzeroaddrs
INCshipssofar:LDAshipssofar:CMP#7:LDA#0:BCCgotsptype:LDA#1
.gotsptype STAsptype:TAX:LDAS_type,X:STAtype+1
LDAS_palette,X:JSRospalette:LDAS_sound,X:JSRsound
LDA#0:STAxhi+1:JSRrandom:STAyhi+1
LDA#`spcvel AND&FF:STAxvlo+1:LDA#`spcvel DIV256:STAxvhi+1
JSRrandom:BPLposvel
LDA#159:STAxhi+1
LDA#-`spcvel AND&FF:STAxvlo+1:LDA#(-`spcvel AND&FFFF)DIV256:STAxvhi+1
.posvel LDA#1:STAspacecounter
\ $JSR rndspcvel
 
.rndspcvel
JSRrndvel:STAyvlo+1:LDAtemp+1:STAyvhi+1
RTS
 
.spaceship
TYA:PHA
LDAxvhi+1:ASLA:LDAxhi+1:BCCtryr
CMP#10:BCCspcoff:BCSokspc
.tryr CMP#150:BCCokspc
.spcoff JSRkillspc
LDA#8:JSRsound:JMPspcdone
.okspc DECspacecounter:BNEtryb
LDA#`spccnt:STAspacecounter:JSRrndspcvel
.tryb LDAtype+rocklen:ASLA:BPLspcdone:LDAkilled:BNEspcdone
LDA#&80+35:LDX#rocklen:JSRinitobj:LDX#1:LDY#rocklen:JSRcopycoords
LDX#rocklen:JSRfirevels
LDA#40:STAgflags,Y
LDA#6:JSRsound
.spcdone PLA:TAY:LDX#1:RTS
 
.killspc
LDA#0:STArightrocks
LDX#1:JSRdie:LDA#8:JMPsound
 
.scale8
PHA:PHP:LDA#0:PLP:BPLL8:LDA#&FF:.L8 STAtemp:PLA:JMPmult8
 
.gremlins
LDXgremno:LDYnumcyc
.grlp LDAtype,X:ASLA:BMIgr2
CPX#1:BNEgr1:JSRspaceship
.gr1 JSRupdate
.gr2 INX:CPX#rocklen:BNEP%+4:LDX#1
DEY:BNEgrlp:STXgremno
LDX#rocklen
.grlp1 LDAtype,X:ASLA:BMIgrend
DECgflags,X:BNEgrok:JSRdie
.grok JSRupdate
.grend INX:CPX#len:BNEgrlp1
RTS
 
.random
TXA:PHA:LDX#8
.rndlp
LDAseed:AND#&48:ADC#&38
ASLA:ASLA
ROLseed+2:ROLseed+1:ROLseed
DEX:BNErndlp:PLA:TAX:LDAseed:RTS
 
.addscXY
SED
CLC:TXA:ADCscore:STAscore:TYA:ADCscore+1:STAscore+1
CLD
AND#&F0:CMPoldscore:STAoldscore:BEQP%+5:JSRextraship
\ $JSR pscore
 
.pscore
LDX#2:LDY#0:JSRtabxy
LDAscore+1:JSRpfbyte:LDAscore
\ $JSR pbyte
 
.pbyte
PHA:LSRA:LSRA:LSRA:LSRA:JSRpdig
PLA:AND#15:\ $JSR pdig
 
.pdig STXmask
TAX:ORAsupp:STAsupp:BNEpdig1
LDX#(ASC" "-ASC"0")AND&FF
.pdig1 TXA:CLC:ADC#ASC"0"
LDXmask:JMPoswrch
 
.pfbyte
PHA:LDA#0:STAsupp:PLA:JMPpbyte
 
.phiscore
LDX#17:LDY#1:JSRtabxy
LDAhiscore+1:JSRpfbyte
LDAhiscore:JMPpbyte
 
.psheetno
LDX#37:LDY#0:JSRtabxy
LDAsheetno:JMPpfbyte
 
.tabxy
LDA#31:JSRoswrch:TXA:JSRoswrch
TYA:JMPoswrch
 
.cycles
TXA:PHA:JSRcycle:PLA:TAX
DEX:BNEcycles:RTS
 
.cycle
LDX#`space:JSRinkey:ANDletgoyet:STAletgoyet
JSRmvship
JSRsetcaps
JSRgremlins
JSRdoexplosion:JSRdoexplosion
JSRvsync
JSRexecute
JSRgetspaceship
JSRdobeeps
JSRinkey0:PHA:JSRcheckoptions:PLA
RTS
 
.checkoptions
LDXdemo:BNEsoundoptions
CMP#`esc:BNEnotesc:.isesc
LDA#0:STAscore:STAscore+1:STAnumships
LDX#0:JSRdie:LDA#&FF:STAkilled
LDX#rocklen:.bulloff JSRdie:INX:CPX#len:BNEbulloff
LDXstack:TXS:JMPallover
.notesc CMP#`copy:BNEnotcopy
.pauselp JSRinkey0:CMP#`esc:BEQisesc
PHA:JSRsoundoptions:PLA:CMP#`delete:BNEpauselp:RTS
.notcopy \ $JSR soundoptions
 
.soundoptions
AND#&DF:CMP#ASC"S":BNEnotS:LDA#&FF:STAsoundon:RTS
.notS CMP#ASC"Q":BNEnotQ:LDA#0:STAsoundon:LDA#15:LDX#0:JSRosbyte
.notQ RTS
 
.dobeeps
DECcyclecount:BNEnodec:LDAcycletime:STAcyclecount
LDAbeattime:CMP#5:BCCnodec:DECbeattime:.nodec
LDAbeepcount:BEQnobeep
DECbeepdelay:BNEnobeat
LDA#8:STAbeepdelay:DECbeepcount
LDA#1:JMPsound
.nobeep LDAtype+1:ASLA:BPLnobeat
LDAbeating:BEQnobeat
DECbeatdelay:BNEnobeat
LDAbeattime:STAbeatdelay
LDAbeatcount:BEQdobeat:DECbeatcount
.dobeat LDAbeatsnd:EOR#(9EOR10):STAbeatsnd:JMPsound
.nobeat RTS
 
.entry
LDA#200:LDX#0:LDY#&FE:JSRosbyte    \ re-enable escape
LDA#&FF:STAsoundon:LDA#4:LDX#1:JSRosbyte
.keylp1 JSRinkey0:CMP#ASC" ":BEQletsgo:JSRsoundoptions:JMPkeylp1
.letsgo JSRinit:TSX:STXstack
.gameloop JSRgameinit
.restart LDA#0:STAkilled
.mainloop JSRcycle
LDAkilled:BNEneedanother
LDAnumroids:BNEmainloop:JSRnewsheet:JMPmainloop
.needanother JSRgetship:BNErestart
.gameover
LDA#2:JSRmessage:LDX#100:JSRcycles
.allover
LDA#8:JSRsound:JSRhighscores
LDA#&FF:STAdemo
LDA#6:JSRmessage:JSRzaddrs
LDA#15:LDX#1:JSRosbyte
.keylp JSRcycle:CMP#ASC" ":BNEkeylp:JMPgameloop
 
.testraster
LDA#0:STAraster:TYA:SEC:SBC#40
LSRA:RORraster:LSRA:RORraster
ADC#4:STAraster+1
SEC:LDA&FE64:SBCraster:STAraster
LDA&FE65:SBCraster+1:STAraster+1
BMIP%+3:RTS
CLC:LDAraster:ADC#`timelo
LDAraster+1:ADC#`timehi:RTS
 
.message
STXmessX:STYmessY
ASLA:TAX:LDAmessptr,X:STAaddr:LDAmessptr+1,X:STAaddr+1:LDY#0:TXA:LSRA:TAX
.messlp LDA(addr),Y:JSRoswrch
INY:TYA:CMPmesslen,X:BNEmesslp
LDXmessX:LDYmessY
RTS
 
.init
LDA#0:JSRmessage
LDX#8:LDA#0:JSRcrtcreg
LDX#10:LDA#32:JSRcrtcreg
LDA#0:STAnextexp
LDA#1:STAseed:LDX#24
.init1 JSRrandom:DEX:BNEinit1
LDA#oldvsync AND&FF:STAvsync+1:LDA#oldvsync DIV256:STAvsync+2
LDA#oldcaps AND&FF:STAsetcaps+1:LDA#oldcaps DIV256:STAsetcaps+2
LDX#0:JSRinkey:BEQgotvsync
LDA#newcaps AND&FF:STAsetcaps+1:LDA#newcaps DIV256:STAsetcaps+2
SEI:LDA#vsyncevent AND&FF:STAeventvec
LDA#vsyncevent DIV256:STAeventvec+1:CLI
LDA#14:LDX#4:JSRosbyte
LDA#newvsync AND&FF:STAvsync+1:LDA#newvsync DIV256:STAvsync+2
.gotvsync RTS
 
.gameinit
LDA#&11:JSRospalette
LDA#&26:JSRospalette
LDA#&33:JSRospalette
LDA#&FF:STAletgoyet
LDA#1:JSRmessage
LDA#3:STAV
LDA#20:STArocktime:STAbeatcount
LDA#100:STAcycletime:STAcyclecount
LDA#1:STAgremno:LDA#8:STAnumcyc
LDA#0:STAshipshape:STAshipssofar:STAdemo:STAoldscore
STAscore:STAscore+1:STAbeating:STAbeepcount:STArightrocks
JSRpscore:JSRphiscore
LDA#1:STAacccntr
LDX#len-1:.initlp LDA#&FF:STAtype,X:DEX:BPLinitlp
LDX#nx-1:.initlp1 LDA#0:STAextime,X:DEX:BPLinitlp1
LDA#0:STAsheetno:LDA#3:STAnumships
JSRzaddrs:JSRgetship:JSRnewsheet
RTS
 
.zaddrs
LDX#len-1:.zaddlp JSRzeroaddrs:DEX:BPLzaddlp
LDX#nx-1:.zaddlp1 LDA#0:STAextime,X:DEX:BPLzaddlp1:RTS
 
.getship
LDAnumships:BNEP%+3:RTS
LDA#&FF:STAtype
JSRwaitspace
DECnumships:JSRpships
LDA#&80:STAtype
LDA#0:STAdead:STAhyperspace
LDA#80:STAxhi:LDA#128:STAyhi
LDX#0:JSRzerovels:JSRzeroaddrs
LDA#1:STAbeating:RTS
 
.killship
LDA#0:STAbeating:LDA#8:JSRsound
LDA#&FF:STAkilled:LDA#4:JSRsound
LDX#0:JSRdie:LDX#rocklen
.getexplp JSRdie
TXA:TAY:LDX#0:JSRcopycoords:TYA:TAX
LDAexvlo,X:STAxvlo,X:LDAexvhi,X:STAxvhi,X
LDAeyvlo,X:STAyvlo,X:LDAeyvhi,X:STAyvhi,X
CLC:TXA:ADC#&A5-rocklen:STAtype,X
LDAegflags,X:STAgflags,X
JSRzeroaddrs
INX:CPX#len:BNEgetexplp
LDX#100:JMPcycles
 
.extraship
LDA#5:STAbeepcount
LDA#1:STAbeepdelay:INCnumships
\ $JSR pships
.pships
LDX#4:LDY#1:JSRtabxy
LDXnumships:BEQnoprint
.wrshlp LDA#224:JSRoswrch
DEX:BNEwrshlp
.noprint LDA#ASC" ":JMPoswrch
 
.nonewsh RTS
 
.newsheet
LDA#0:STAbeating:LDAsheetno:BEQnowait
LDX#70:STXnewshcnt:.newshwlp JSRcycle:LDAkilled:BNEnonewsh
DECnewshcnt:BNEnewshwlp
.canthavespc JSRcycle:LDAtype+1:ASLA:BPLcanthavespc
.nowait SED:CLC:LDAsheetno:ADC#1:STAsheetno:CLD:JSRpsheetno
INCV:LDAV:CMP#10:BCCokv:LDA#9:STAV:.okv
DECrocktime:LDArocktime:STAbeatcount
SEC:LDAcycletime:SBC#3:CMP#60:BCSnotlimit:LDA#60:.notlimit STAcycletime
LDA#26:STAbeattime:STAbeatdelay:LDA#9:STAbeatsnd:LDA#&FF:STAbeating
LDAsheetno:LDX#4*7:LDY#4:CMP#1:BEQgotnorocks
LDX#5*7:LDY#5:CMP#4:BCCgotnorocks:LDX#6*7:LDY#6
.gotnorocks STXnumroids:LDX#2
.sinitlp TYA:PHA:JSRrndvel
STAxvlo,X:LDAtemp+1:STAxvhi,X
JSRrndvel:ASLA:ROLtemp+1
STAyvlo,X:LDAtemp+1:STAyvhi,X
LDA#0:STAxhi,X:STAyhi,X
JSRrandom:PHP:JSRrandom:PLP
BPLtopbot:STAyhi,X:JMPeither
.topbot STAxhi,X:CMP#160:BCCeither:JSRrandom:JMPtopbot:.either
JSRzeroaddrs:LDA#&A0:STAtype,X
INX:INX:INX:INX
PLA:TAY:DEY:BNEsinitlp:RTS
 
.rndvel
JSRrandom:AND#63:CMP#15:BCCrndvel
\ $JSR getrndadd
 
.getrndadd
STA_x:LDAV:STA_y
TYA:PHA
JSRmultiply:JSRrandom:BPLgotrndv
SEC:LDA#0:SBC_a:STA_a:LDA#0:SBC_a+1:STA_a+1
.gotrndv PLA:TAY
LDA_a+1:STAtemp+1:LDA_a:STAtemp
RTS
 
]RETURN
 
DEFPROCFIND(A$)
Z%=PAGE:REPEATA%=Z%+4
B$=$A%+" ":IFLENB$>=LENA$IFINSTR(B$,A$)PRINTZ%?1*256+Z%?2;
Z%=Z%+Z%?3:UNTILZ%?1>&7F
ENDPROC

REM .SRCE3
 
[OPTpass
 
.highscores
LDX#0
.tryhighlp
LDAhiscoretab,X:CMPscore
LDAhiscoretab+1,X:SBCscore+1
BCCgotscore
TXA:CLC:ADC#hientlen:TAX
CPX#maxhient+1:BCCtryhighlp
BCSprintscores
.gotscore
STXtemp:CPX#maxhient:BEQnoshuffle
LDX#maxhient
.shufflp DEX
LDAhiscoretab,X:STAhiscoretab+hientlen,X:CPXtemp:BNEshufflp
.noshuffle
LDA#13:STAhiscoretab+2,X
LDAscore:STAhiscoretab,X:LDAscore+1:STAhiscoretab+1,X
JSRprintscores:JSRaskforname
 
.printscores
LDA#4:JSRmessage
LDX#0:STXposnno:LDY#nameytab
.printhilp TXA:PHA:PHA
SED:CLC:LDAposnno:ADC#1:STAposnno:CLD
LDX#posnxtab:JSRtabxy
LDAposnno:JSRpfbyte:LDA#ASC".":JSRoswrch
LDX#scorextab:JSRtabxy:PLA:TAX
LDAhiscoretab+1,X:JSRpfbyte
LDAhiscoretab,X:JSRpbyte
CPXtemp:BNEnotthishi:STYtemp+1
.notthishi LDA#5:JSRmessage
INX:INX
.pnamelp LDAhiscoretab,X:JSRoswrch
INX:CMP#13:BNEpnamelp:INY:INY
PLA:CLC:ADC#hientlen:TAX
CPX#maxhient+hientlen:BNEprinthilp
RTS
 
.askforname
LDA#4:LDX#0:JSRosbyte
LDA#3:JSRmessage
LDA#15:LDX#1:JSRosbyte
LDX#namextab:LDYtemp+1:JSRtabxy
LDX#10:LDA#&67:JSRcrtcreg
CLC:LDAtemp:ADC#(hiscoretab+2)AND&FF:STAoswordblk+0
LDA#0:ADC#(hiscoretab+2)DIV256:STAoswordblk+1
LDA#hientlen-3:STAoswordblk+2
LDA#32:STAoswordblk+3
LDA#126:STAoswordblk+4
LDX#oswordblk AND&FF:LDY#oswordblk DIV256:LDA#0:JSRosword
BCCnoescline:LDA#13:LDXtemp:STAhiscoretab+2,X:.noescline
LDA#4:LDX#1:JSRosbyte
LDX#10:LDA#32:JMPcrtcreg
 
.zerovels
LDA#0:STAxvlo,X:STAxvhi,X
STAyvlo,X:STAyvhi,X:RTS
.initobj
STAtype,X:\ $JSR zeroaddrs
.zeroaddrs
LDA#0:STAnewhi,X:STAoldhi,X:RTS
.copycoords
LDAxlo,X:STAxlo,Y:LDAxhi,X:STAxhi,Y
LDAylo,X:STAylo,Y:LDAyhi,X:STAyhi,Y
RTS
 
.fire
LDX#rocklen+1
.findlp LDAtype,X:ASLA:BMIgotfire
INX:CPX#len:BNEfindlp:RTS
.gotfire LDA#0:JSRsound
LDAshipshape:LSRA:TAY
LDAbxvlo,Y:STAxvlo,X:LDAbxvhi,Y:STAxvhi,X
LDAbyvlo,Y:STAyvlo,X:LDAbyvhi,Y:STAyvhi,X
LDA#&80+35:STAtype,X
LDA#50:STAgflags,X
TXA:TAY:LDX#0:JSRcopycoords:TYA:TAX
LDY#4:.offlp JSRaddvels:DEY:BNEofflp
CLC:LDAxvlo,X:ADCxvlo:STAxvlo,X:LDAxvhi,X:ADCxvhi:STAxvhi,X
CLC:LDAyvlo,X:ADCyvlo:STAyvlo,X:LDAyvhi,X:ADCyvhi:STAyvhi,X
JMPzeroaddrs
 
.mult16 ASLA:ROLtemp
.mult8 ASLA:ROLtemp
.mult4 ASLA:ROLtemp
.mult2 ASLA:ROLtemp
RTS
 
.mvship
LDAtype:ASLA:BMInomvship
LDAdead:BEQshipok
LDAkilled:BNEP%+5:JSRkillship
.nomvship RTS
.shipok LDAhyperspace:BEQmvsh1a
DEChyperspace:BEQP%+3:RTS
JSRrandom:STAxhi:JSRrandom:STAyhi
LDX#0:JSRzerovels
JSRrandom:CMP#`dieprob:BCSmvsh1b
LDA#1:STAdead:RTS
.mvsh1a
LDX#`space:JSRinkey:BEQmvsh1b
LDAletgoyet:BNEmvsh1b
LDX#0:JSRupdate:LDA#0:STAnewhi
LDA#30:STAhyperspace:RTS
.mvsh1b
LDX#`return:JSRinkey:BEQL3
EORretflag:.L3 STXretflag
BEQP%+5:JSRfire
LDX#`capslock:JSRinkey:BEQmvsh1
INCshipshape:.mvsh1
LDX#`ctrl:JSRinkey:BEQmvsh2
DECshipshape:.mvsh2
LDAshipshape:AND#63:STAshipshape
LSRA:ORA#&80:STAtype
LDX#`shift:JSRinkey:BEQmvsh3
LDAshipshape:LSRA:TAX
CLC:LDAxvlo:ADCxacclo,X:STAxvlo
LDAxvhi:ADCxacchi,X:STAxvhi
CLC:LDAyvlo:ADCyacclo,X:STAyvlo
LDAyvhi:ADCyacchi,X:STAyvhi
.mvsh3
DECacccntr:BNEmvsh4
LDA#accfreq:STAacccntr
LDAxvlo:STAtemp:LDAxvhi:JSRscale
SEC:LDAxvlo:SBCtemp:STAxvlo
LDAxvhi:SBCaddr:STAxvhi
LDAyvlo:STAtemp:LDAyvhi:JSRscale
SEC:LDAyvlo:SBCtemp:STAyvlo
LDAyvhi:SBCaddr:STAyvhi
.mvsh4
LDX#0:JMPupdate
 
.scale
PHP:LSRA:RORtemp:LSRA:RORtemp:LSRA:RORtemp:LSRA:RORtemp
PLP:BPLP%+4:ORA#&F0:STAaddr
RTS
 
.firevels
LDAsptype:BNEitsalittlun
.sillyco JSRrandom:AND#&7F:STAdx
JSRrandom:AND#&7F:STAdy
CLC:ADCdx:CMP#20:BCCsillyco
JSRrandom:ASLA:PHP:ASLA:PHP:JMPnohalve
.itsalittlun
SEC:LDAxhi:SBCxhi+1:PHP:JSRabs:STAdx
SEC:LDAyhi:SBCyhi+1:PHP:JSRabs:STAdy
LDAdx:BMIhalveit:LDAdy:BPLnohalve
.halveit LSRdx:LSRdy
.nohalve LSRdy
LDA#55:STAK:JSRcalcvels
LDA#0:STAtemp:LDAvy:JSRmult16
PLP:JSRplusminus
STAyvlo,X:LDAtemp:STAyvhi,X
LDA#0:STAtemp:LDAvx:JSRmult8
PLP:JSRplusminus
STAxvlo,X:LDAtemp:STAxvhi,X
LDAsptype:BEQitsabigun
CLC:LDAxvlo,X:ADCxvlo:STAxvlo,X
LDAxvhi,X:ADCxvhi:STAxvhi,X
CLC:LDAyvlo,X:ADCyvlo:STAyvlo,X
LDAyvhi,X:ADCyvhi:STAyvhi,X
JSRrndadd:CLC:ADCxvlo,X:STAxvlo,X
LDAtemp:ADCxvhi,X:STAxvhi,X
JSRrndadd:ASLA:ROLtemp
CLC:ADCyvlo,X:STAyvlo,X
LDAtemp:ADCyvhi,X:STAyvhi,X
.itsabigun RTS
 
.rndadd
LDA#0:STAtemp:JSRrandom
SEC:SBC#128:BCSrndadone:DECtemp
.rndadone RTS
 
.abs
BCSokabs:EOR#&FF:CLC:ADC#1
.okabs RTS
 
.plusminus
BCSokpm:STAtemp+1:SEC
LDA#0:SBCtemp+1:PHA
LDA#0:SBCtemp:STAtemp:PLA
.okpm RTS
 
.calcvels
TXA:PHA:TYA:PHA
LDAdx:STA_x:STA_y:JSRmultiply
LDA_a:STA_b:LDA_a+1:STA_b+1
LDAdy:STA_x:STA_y:JSRmultiply
CLC:LDA_a:ADC_b:STA_a
LDA_a+1:ADC_b+1:STA_a+1
JSRsquareroot
LDAdx:STA_x:LDAK:STA_y:JSRmultiply
JSRdivide:LDA_x:STAvx
LDAdy:STA_x:LDAK:STA_y:JSRmultiply
JSRdivide:LDA_x:STAvy
PLA:TAY:PLA:TAX
RTS
 
.multiply
TYA:PHA
LDA#0:STA_y+1:STA_a:STA_a+1:LDY#8
.mloop LSR_x:BCCnoadd
CLC:LDA_a:ADC_y:STA_a
LDA_a+1:ADC_y+1:STA_a+1
.noadd ASL_y:ROL_y+1
DEY:BNEmloop:PLA:TAY:RTS
 
.divide
LDA#0:STA_y:LDY#16
.dloop
ASL_a:ROL_a+1:ROL_y
SEC:LDA_y:SBC_z:STAtemp
BCCnosub:STA_y
.nosub ROL_x
DEY:BNEdloop:RTS
 
.squareroot
LDA#0:STA_z:STA_z+1:STAtemp
STA_a+2:STA_a+3:LDA#&80:STAtemp+1
.sqloop
ASL_a:ROL_a+1:ROL_a+2:ROL_a+3
SEC:LDA_a+2:SBCtemp:TAY
LDA_a+3:SBCtemp+1:TAX:BCCnosq
TYA:SBC_z:TAY:TXA:SBC_z+1:TAX:BCCnosq
TYA:SBC_z:TAY:TXA:SBC_z+1:BCCnosq
STA_a+3:STY_a+2
LDA_z:ORAtemp:STA_z:LDA_z+1:ORAtemp+1:STA_z+1
.nosq LSRtemp+1:RORtemp:BCCsqloop
RTS
 
]RETURN
 
"Extra bit for saving pictures
"ƒ.vsyncint JMP(intvec)
"ƒ.cycle JSRtrysave
"ƒJMPex1 \ before exwait
"…DIMsavebuff 20
"…$savebuff="ENDIF PIC 5800 8000"
 
.trysave
JSRinkey0:AND#&DF:CMP#ASC"S":BNEnosave
LDX#savebuff AND&FF:LDY#savebuff DIV256:JSRoscli
.nosave RTS
 
]RETURN
 
DEFPROCFIND(A$)
Z%=PAGE:REPEATA%=Z%+4
IFLEN$A%>=LENA$IFINSTR($A%,A$)PRINTZ%?1*256+Z%?2;
Z%=Z%+Z%?3:UNTILZ%?1>&7F:ENDPROC

    0REM .SRCEI
  100REM /// Meteors initialisation file ///
  200 
  300English=1:German=2:language=English
  400PROCinit
  500CLS:PRINTCHR$&82"Meteors Memory Map"
  600overflow%=FALSE:prevaddr%=&3000
  700PROCd("Messages",M%,currmess%)
  800PROCd("Init data",endT%,M%)
  900PROCd("Shape props",T%,endT%)
 1000PROCd("Hiscores",H%,endH%)
 1100PROCd("Shape data",S%,S%!1 AND&FFFF)
 1200PROCd("Program",&1100,P%)
 1300PROCd("Object tables",U%,endU%)
 1400PROCd("Variables",0,endZ%)
 1500IFoverflow%THENRETURN
 1600*SAVE .Meteor2 1100 3000
 1700L%=language:CHAIN"GENASYS"
 1800 
 1900DEFPROCd(A$,start%,end%)
 2000IFend%=prevaddr%THEN2400
 2100PRINT" +"STRING$(15,"-")"+";
 2200IFend%>prevaddr%THENVDU&81,&88,7:overflow%=TRUE ELSEPRINT"  ";
 2300PRINT;~end%
 2400PRINT" ! "A$TAB(17)"!       (";end%-start%")"
 2500PRINT" +"STRING$(15,"-")"+  ";~start%
 2600prevaddr%=start%:ENDPROC
 2700 
 2800DEFPROCinit
 2900RESTORE
 3000FORI%=0TO3:READbits?I%:NEXT
 3100DATA&88,&44,&22,&11
 3200FORI%=0TO14STEP2:READJ%
 3300exshapes?I%=0:exshapes?(I%+1)=J%:NEXT
 3400DATA &01,&02,&40,&80,&41,&82,&42,&81
 3500FORI%=0TO7:READlimtime?I%:NEXT
 3600DATA 6,6,6,6,9,9,9,9
 3700R%=accn*50:FORI%=0TOnsh-1
 3800I=PI*I%/16:X%=-R%*SINI:Y%=R%*COSI
 3900X1%=(X%DIV50)AND&FFFF
 4000Y1%=(Y%DIV25)AND&FFFF
 4100X%=X%AND&FFFF:Y%=(Y%*2)AND&FFFF
 4200bxvlo?I%=X%:bxvhi?I%=X%DIV256
 4300byvlo?I%=Y%:byvhi?I%=Y%DIV256
 4400xacclo?I%=X1%:xacchi?I%=X1%DIV256
 4500yacclo?I%=Y1%:yacchi?I%=Y1%DIV256
 4600NEXT
 4700FORI%=rocklen TOlen-1
 4800READX%,Y%,egflags?I%
 4900X%=X%AND&FFFF:Y%=Y%AND&FFFF
 5000exvlo?I%=X%:exvhi?I%=X%DIV256
 5100eyvlo?I%=Y%:eyvhi?I%=Y%DIV256
 5200NEXT
 5300DATA -40,-8,70,-50,60,60
 5400DATA 40,48,70,50,-10,65
 5500DATA 0,-60,80
 5600FORI%=32TO42
 5700READxoffset?I%,xlength?I%
 5800READyoffset?I%,ylength?I%
 5900NEXT
 6000DATA3,17,3,29,3,10,1,18,2,6,1,10
 6100DATA0,0,0,0,4,7,5,8,0,0,0,0
 6200DATA0,0,0,0,0,0,0,0
 6300DATA0,0,0,0,0,0,0,0,8,13,8,14
 6400FORI%=0TOnsnds-1
 6500READsnd0?I%,snd1?I%,snd2?I%,snd3?I%,snd4?I%:NEXT
 6600DATA 0,&12,1,100,10
 6700DATA 0,&13,-15,200,2
 6800DATA 1,&11,2,80,-1,1,&10,3,7,10
 6900DATA 1,&11,2,80,-1,1,&10,-15,7,4
 7000DATA 0,&12,1,170,10
 7100DATA 0,&13,4,120,-1,0,&13,0,0,1
 7200DATA 0,&13,-15,0,1,0,&13,-15,4,1
 7300DATA 0,&13,4,150,-1
 7400FORI%=0TO2:READrockscore?I%:NEXT
 7500DATA &02,&05,&10
 7600FORI%=0TO1
 7700READS_type?I%,S_palette?I%,S_scorelo?I%,S_scorehi?I%,S_sound?I%:NEXT
 7800DATA &AA,&11,&20,0,7
 7900DATA &A4,&15,&00,1,11
 8000FORI%=0TOmaxhient STEPhientlen
 8100hiscoretab!I%=&100
 8200$(hiscoretab+I%+2)="Acornsoft"
 8300NEXT
 8400 
 8500ON language GOTO 8600,9500 ELSE PRINT"Unknown language":END
 8600PROCRMESS(0,"1601")
 8700PROCRMESS(1,"1A0C1F0600301F10001102.Hi-Score.1F1F00.Sheet.11031F150130")
 8800PROCRMESS(2,"1F100F.GAME OVER")
 8900PROCRMESS(3,"11011F0B01.CONGRATULATIONS !!!.1F081A.Please enter your name.1103")
 9000PROCRMESS(4,"1C001F27020C1F0A031102.Meteors Hall of Fame.1103")
 9100PROCRMESS(5,".0   ")
 9200PROCRMESS(6,"1F061C1102.Press SPACE BAR to play again.1103")
 9300ENDPROC
 9400 
 9500PROCRMESS(0,"1601")
 9600PROCRMESS(1,"1A0C1F0600301F0E001102.Bestresultat.1F1F00.Seite.11031F150130")
 9700PROCRMESS(2,"1F0C0F.Ende des Spieles")
 9800PROCRMESS(3,"11011F0D01.GRATULATION !!!.1F041A.Bitte tippen Sie Ihren Namen ein.1103")
 9900PROCRMESS(4,"1C001F27020C1F0A031102.Meteors - Ehrenhalle.1103")
10000PROCRMESS(5,".0   ")
10100PROCRMESS(6,"1F071B1102.Dr.E1.cken Sie die Leertaste.1F0B1C.f.E1.r ein neues Spiel.1103")
10200ENDPROC
10300 
10400DEFPROCRMESS(M%,M$)
10500messptr!(2*M%)=currmess%
10600MD%=FALSE:I%=0:REPEAT
10700IFFNR="."THENMD%=NOTMD%:CH$=FNR
10800IFMD%THENcurrmess%?I%=ASCCH$ELSEcurrmess%?I%=FND(CH$)*16+FND(FNR)
10900I%=I%+1:UNTILM$=""
11000messlen?M%=I%:currmess%=currmess%+I%
11100ENDPROC
11200DEFFNR
11300CH$=LEFT$(M$,1):M$=MID$(M$,2):=CH$
11400DEFFND(C$)
11500IFC$>="A"THEN=ASCC$-ASC"A"+10
11600=VALC$
11700 
11800DEFPROCFIND(A$)
11900Z%=PAGE:REPEATIFINSTR($(Z%+4),A$)PRINTZ%?1*256+Z%?2;
12000Z%=Z%+Z%?3:UNTILZ%?1>&7F:ENDPROC
