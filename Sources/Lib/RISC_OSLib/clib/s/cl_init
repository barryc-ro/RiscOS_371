;
; Copyright (C) Acorn Computers Ltd., 1988.
;
       AREA    |RTSK$$Data|, READONLY
        IMPORT  |C$$code$$Base|
        IMPORT  |C$$code$$Limit|

rtskBase
        &       EndRTSK-.
        &       |C$$code$$Base|
        &       |C$$code$$Limit|
        &       CLanguageString
        &       Initialise
 [ SharedLibrary
        &       0       ; compatibility - sharedlibrary here means stub
 |
        &       Finalise
 ]
        &       TrapHandler
        &       UncaughtTrapHandler
        &       EventHandler
        &       UnhandledEventHandler
EndRTSK

        AREA    |C$$code|, CODE, READONLY
        IMPORT  main, WEAK
        EXPORT  |__main|

CLanguageString
        =       "C",0
        ALIGN

; __main is a dummy definition to statisfy references generated by the
; compiler. It should never be called.
|__main|

Initialise
; argument for _armsys_lib_init:
;                   top of stack,
;                   start of code area,
;                   end of code area,
 [ SharedLibrary
; Here, SharedLibrary means stub. In this case, we have to enable Wimp-Slot
; extension by _kernel_alloc, as for compatibility with old binaries it has
; to be off by default.
        LDR     a1, =|_stub_kallocExtendsWS|
        LDR     a2, [sl, #SL_Client_Offset]
        ADD     a1, a1, a2
        MOV     a2, #1
        STRB    a2, [a1]                ; enable new _kernel_alloc behaviour
 ]
        STMFD   sp!, {r14}
        ADD     a1, sp, #4
        LDR     a2, =rtskBase
        LDMIB   a2, {a2, a3}
        BL      |_clib_initialise|
        LDR     a1, =main
        CMP     a1, #0
        ADRNE   a1, RunSubMain
        LDMFD   sp!, {pc}^

RunSubMain
; args for _main(): command line
;                   address of main
        MOV     ip, sp
        STMFD   sp!, {fp, ip, r14, pc}
        SUB     fp, ip, #4
        BL      |_kernel_command_string|
        LDR     a2, =main
        BL      |_main|
; Doesn't return, but just in case...
        LDMDB   fp, {fp, sp, pc}^

        END
